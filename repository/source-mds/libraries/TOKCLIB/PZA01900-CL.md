# PZA01900

**種別**: JCL  
**ライブラリ**: TOKCLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIB/PZA01900.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　新基幹システム                        *  ./
/. *   SYSTEM-NAME :    在庫管理                             *  ./
/. *   JOB-ID      :    PZA01900                             *  ./
/. *   JOB-NAME    :    年次在庫データ繰り越し               *  ./
/. *   UPDATE      :    2011/11/24 MIURA MOからLTOへ変更     *  ./
/. *   UPDATE      :    2020/01/27 S11301570 オフコンクラウド*  ./
/. ***********************************************************  ./
    PGM
    VAR       ?PGMEC    ,INTEGER
    VAR       ?PGMECX   ,STRING*11
    VAR       ?PGMEM    ,STRING*99
    VAR       ?MSG      ,STRING*99(6)
    VAR       ?MSGX     ,STRING*99
    VAR       ?PGMID    ,STRING*8,VALUE-'PZA01900'
    VAR       ?STEP     ,STRING*8
    VAR       ?OPR1     ,STRING*50                  /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?OPR2     ,STRING*50                  /.      2    ./
    VAR       ?OPR3     ,STRING*50                  /.      3    ./
    VAR       ?OPR4     ,STRING*50                  /.      4    ./
    VAR       ?OPR5     ,STRING*50                  /.      5    ./

    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

    ?OPR1  :=  '　＃＃＃＃＃＃＃　年次更新処理　＃＃＃＃＃＃＃＃'
    ?OPR2  :=  ''
    ?OPR3  :=  '　　年次更新を行ないます。確認して下さい。'
    ?OPR4  :=  ''
    ?OPR5  :=  '　＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃'
    CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)

    DEFLIBL    TOKFLIB/TOKELIB

ZAIKOBAK:

    ?MSGX :=  '## 在庫ﾏｽﾀ ﾊﾞｯｸｱｯﾌﾟ中 ##'
    SNDMSG    ?MSGX,TO-XCTL

/.##SAVFILE FILE-ZAMZAIF.TOKFLIB/ZAMJISF.TOKFLIB,TODEV-LTO, ##./
/.##        ADD-@YES,MODE-@USED                             ##./
    SAVFILE FILE-ZAMZAIF.TOKFLIB/ZAMJISF.TOKFLIB,TODEV-@NONE,
            MODE-@USED,
      /.社内TOPATH-'/NAVPC/PZA01900/',REP-@YES,COMPRESS-@YES./
            TOPATH-'/HGNAS/PZA01900/',REP-@YES,COMPRESS-@YES
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.  年次在庫データ繰り越し                                      ./
SZA0190B:
    ?STEP :=   'SZA0190B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZAMJISF,TOFILE-ZAMJISF.TOKFLIB
    CALL      PGM-SZA0190B.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'


    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
