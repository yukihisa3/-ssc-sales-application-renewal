# PKY14041

**種別**: JCL  
**ライブラリ**: TOKCLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIB/PKY14041.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　特販システム（本社システム）          *  ./
/. *   SYSTEM-NAME :    オフライン                           *  ./
/. *   JOB-ID      :    PKY14041                             *  ./
/. *   JOB-NAME    :    スポット発注集計表（取引先＋_番順） *  ./
/. ***********************************************************  ./
    PGM
    VAR       ?PGMEC    ,INTEGER
    VAR       ?PGMECX   ,STRING*11
    VAR       ?PGMEM    ,STRING*99
    VAR       ?MSG      ,STRING*99(6)
    VAR       ?MSGX     ,STRING*99
    VAR       ?PGMID    ,STRING*8,VALUE-'PKY14041'
    VAR       ?STEP     ,STRING*8
/.##ﾌｧｲﾙ名称編集用##./
    VAR       ?WKSTN    ,NAME
    VAR       ?WS       ,STRING*8,VALUE-'        '
    VAR       ?MEMO     ,STRING*5,VALUE-'     '
    VAR       ?SOKCD    ,STRING*2,VALUE-'  '
    VAR       ?S        ,STRING*1,VALUE-'S'
    VAR       ?W        ,STRING*2,VALUE-'WK'
    VAR       ?FILNM    ,STRING*8,VALUE-'        '
    VAR       ?LIBNM    ,STRING*8,VALUE-'TOKFLIB '
    VAR       ?FILID    ,NAME
    VAR       ?LIBID    ,NAME
    VAR       ?JHTSPTL1 ,NAME!MOD
    VAR       ?SPOTWK   ,NAME!MOD
    VAR       ?JHTSPTF  ,NAME!MOD
    VAR       ?NJHTSPTF ,STRING*17,VALUE-'                 '
    VAR       ?NJHTSPTL ,STRING*17,VALUE-'                 '
    VAR       ?WKSPOT   ,STRING*17,VALUE-'                 '
    VAR       ?OPR1     ,STRING*50
    VAR       ?OPR2     ,STRING*50
    VAR       ?OPR3     ,STRING*50
    VAR       ?OPR4     ,STRING*50
    VAR       ?OPR5     ,STRING*50

    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    DEFLIBL TOKFLIB/TOKELIB
/.##対象ﾒﾓ番号取得##./
SKY1406B:

    ?STEP :=   'SKY1406B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1406B.TOKELIB,PARA-(?WS,?MEMO)
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
    ?MSGX := '## 量販店ﾒﾓNO = ' && ?MEMO
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##倉庫コード取得##./
SKY1405B:

    ?STEP :=   'SKY1405B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1405B.TOKELIB,PARA-(?WS,?SOKCD)
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
    ?MSGX := '## 倉庫ｺｰﾄﾞ   = ' && ?SOKCD
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾌｧｲﾙ名称取得##./
    ?FILNM     :=    ?S && ?MEMO && '1'
    ?FILID     :=    %NAME(?FILNM)
    ?LIBID     :=    %NAME(?LIBNM)
    ?JHTSPTL1  :=    %NCAT(?FILID,?LIBID)
    ?NJHTSPTL :=    %STRING(?JHTSPTL1)
    ?MSGX      :=    '## ｽﾎﾟｯﾄ出荷F(LF1) = ' && ?NJHTSPTL
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ?FILNM     :=    ?S && ?MEMO
    ?FILID     :=    %NAME(?FILNM)
    ?LIBID     :=    %NAME(?LIBNM)
    ?JHTSPTF   :=    %NCAT(?FILID,?LIBID)
    ?NJHTSPTF  :=    %STRING(?JHTSPTF)
    ?MSGX      :=    '## ｽﾎﾟｯﾄ出荷F(PF) = ' && ?NJHTSPTF
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ?FILNM     :=    ?W && ?MEMO
    ?FILID     :=    %NAME(?FILNM)
    ?LIBID     :=    %NAME(?LIBNM)
    ?SPOTWK    :=    %NCAT(?FILID,?LIBID)
    ?WKSPOT    :=    %STRING(?SPOTWK)
    ?MSGX      :=    '## ｽﾎﾟｯﾄﾜｰｸF 名   = ' && ?WKSPOT
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾜｰｸﾌｧｲﾙ作成##./
 CRTFILE:

    ?STEP :=   'CRTFILE '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CRTFILE FILE-?SPOTWK,SIZE-500,ESIZE-500,ORG-@SF,BF-5,
            RL-200
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##スポット出荷入力##./
SKY1404B:

    ?STEP :=   'SKY1404B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-TOKMS2,TOFILE-TOKMS2.TOKFLIB
    OVRF      FILE-JHTSPTL1,TOFILE-?JHTSPTL1
    OVRF      FILE-SPOTWK,TOFILE-?SPOTWK
    CALL      PGM-SKY1404B.TOKELIB,PARA-(?SOKCD)
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##スポット出荷入力##./
PSORT:
/.ＳＯＲＴ順＝発注日＋納品日＋_番（６）＋メモ_＋商品ＣＤ＋店舗./
    ?STEP :=   'PSORT    '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    SORT      INFILE-?SPOTWK,INRL-200,INBF-5,
              OUTFILE-?SPOTWK,OUTBF-5,
              KEY-13!8!CA,KEY1-32!8!CA,KEY2-40!8!CA,KEY3-77!6!CA,
              KEY4-183!4!CA,KEY5-187!2!CA,KEY6-61!16!CA,KEY7-21!5!CA,
              RCDL-@DSP

/.##スポット出荷ピッキングリスト発行（_番順）##./
SKY1409L:

    ?STEP :=   'SKY1409L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-SPOTWK,TOFILE-?SPOTWK
    CALL      PGM-SKY1409L.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
/.##ｽﾎﾟｯﾄﾜｰｸﾌｧｲﾙ削除##./
    DLTFILE ?SPOTWK

    ?OPR1  :=  '　＃＃＃＃＃＃＃　スッポト出荷　＃＃＃＃＃＃＃＃　'
    ?OPR2  :=  '　＃　　　　　　　　　　　　　　　　　　　　　＃　'
    ?OPR3  :=  '　＃　スッポト出荷データを初期化しますか？　　＃　'
    ?OPR4  :=  '　＃　　　　　　　　　　　　　　　　　　　　　＃　'
    ?OPR5  :=  '　＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃　'
    CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)

/.##条件Ｆメモ_更新##./
SKY1408B:

    ?STEP :=   'SKY1408B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1408B.TOKELIB,PARA-(?WS)
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##スポット出荷ファイル初期化##./
    CLRFILE FILE-?JHTSPTF

RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:

/.##ｽﾎﾟｯﾄﾜｰｸﾌｧｲﾙ削除##./
    DLTFILE ?SPOTWK
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'


    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
