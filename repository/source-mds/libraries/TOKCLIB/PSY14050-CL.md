# PSY14050

**種別**: JCL  
**ライブラリ**: TOKCLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIB/PSY14050.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　特販システム（本社システム）          *  ./
/. *   SYSTEM-NAME :    ドイトオンラインシステム　　　       *  ./
/. *   JOB-ID      :    PSY14050                             *  ./
/. *   JOB-NAME    :    仕入伝票発行           　　　　　　  *  ./
/. *               :    ＜全社分出力＞                       *  ./
/. * 2003/12/15 ｵﾝﾗｲﾝ変更対応                                *  ./
/. ***********************************************************  ./
    PGM  (P1-?KBN)
/.###ﾊﾟﾗﾒﾀ定義####./
    PARA ?KBN     ,STRING*1,IN,VALUE-' ' /.1=自,2=選択,3=倉庫./
/.###ﾜｰｸｴﾘｱ定義####./
    VAR ?FILNM    ,STRING*8,VALUE-'        '
    VAR ?FILNMH   ,STRING*6,VALUE-'      '
    VAR ?FILNMD   ,STRING*6,VALUE-'DENPYO'
    VAR ?FILNMO   ,STRING*5,VALUE-'ONLDT'
    VAR ?LIBNM    ,STRING*8,VALUE-'TOKFLIB '
    VAR ?FILID    ,NAME
    VAR ?LIBID    ,NAME
    VAR ?JHSDENF  ,NAME!MOD
    VAR ?JHSDENFN ,STRING*17
    VAR ?DENPYO   ,NAME!MOD
    VAR ?DENPYON  ,STRING*17
    VAR ?ONLDT    ,NAME!MOD
    VAR ?ONLDTN   ,STRING*17
    VAR ?ONLDT1   ,NAME!MOD
    VAR ?ONLDT1N  ,STRING*17
    VAR ?ONLDT2   ,NAME!MOD
    VAR ?ONLDT2N  ,STRING*17
    VAR ?ONLDT3   ,NAME!MOD
    VAR ?ONLDT3N  ,STRING*17
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR ?PGMEC    ,INTEGER                      /.ｴﾗｰｽﾃｲﾀｽ./
    VAR ?PGMECX   ,STRING*11                    /.ｴﾗｰｽﾃｲﾀｽ変換./
    VAR ?PGMEM    ,STRING*99                    /.ｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ定義./
    VAR ?MSGX     ,STRING*99                    /.ﾒｯｾｰｼﾞ定義変換./
    VAR ?PGMID    ,STRING*8,VALUE-'PSY14050'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.ﾌﾟﾛｸﾞﾗﾑｽﾃｯﾌﾟ./
    VAR ?SOKCD    ,STRING*2,VALUE-'  '          /.自倉庫ｺｰﾄﾞ./
    VAR ?DSOKCD   ,STRING*2,VALUE-'  '          /.代表倉庫ｺｰﾄﾞ./
    VAR ?JYUN     ,STRING*1,VALUE-' '           /.出力順./
    VAR ?NOUDT1   ,STRING*8,VALUE-'00000000'    /.開始納品日./
    VAR ?NOUDT2   ,STRING*8,VALUE-'00000000'    /.終了納品日./
    VAR ?KEYA     ,STRING*1,VALUE-'1'           /.KEY1./
    VAR ?KEYB     ,STRING*1,VALUE-'2'           /.KEY2./
    VAR ?KEYC     ,STRING*1,VALUE-'3'           /.KEY3./
    VAR   ?PRT ,NAME
    VAR   ?PRTR,STRING*8

    ?PRT      :=  @OUTQN
    ?PRTR     :=  %STRING(?PRT)

/.###ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ###./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    DEFLIBL TOKELIB/TOKFLIB

/.##自倉庫／選択倉庫判定##./
CHK:
    ?FILNMH := 'JHSDEN'
/.## ﾌﾟﾘﾝﾀｰ切替 ##./
    IF      ?KBN   =    '1'   THEN
            CHGCMVAR '@OUTQN',XXPXL512
            ?PRT      :=  @OUTQN
            ?PRTR     :=  %STRING(?PRT)
            ?MSGX    :=  '## PRT1名 = ' && ?PRTR
            SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    END
    IF      ?KBN   =    '2'   THEN
            CHGCMVAR '@OUTQN',XXPXL512
            ?PRT      :=  @OUTQN
            ?PRTR     :=  %STRING(?PRT)
            ?MSGX    :=  '## PRT2名 = ' && ?PRTR
            SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
            GOTO              SKY1701I
    END
    IF      ?KBN   =    '3'   THEN
            IF   ?WS  =  'WKSTNH83'  THEN
                 CHGCMVAR '@OUTQN',XXPXLX83
                 ?PRT      :=  @OUTQN
                 ?PRTR     :=  %STRING(?PRT)
                 ?MSGX    :=  '## PRT3名 = ' && ?PRTR
                 SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
            END
            ?FILNMH := 'SOKDEN'
    END
/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKCD,?DSOKCD)
    IF        @PGMEC    ^=   0
          THEN
              GOTO ABEND
          ELSE
              GOTO FILNMCHG
    END

/.##倉庫ｺｰﾄﾞ選択##./
SKY1701I:

    ?STEP :=   'SKY1701I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    CALL      PGM-SKY1701I.TOKELIB,PARA-(?SOKCD)
    IF        @PGMEC    ^=   0    THEN
              ?PGMEC := @PGMEC
              IF    ?PGMEC  =  4010
                 THEN
                    ?MSGX := '##取消終了##'
                    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                    GOTO   RTN
                 ELSE
                    GOTO   ABEND
                 END
    END

/.##ﾌｧｲﾙ名称取得##./
FILNMCHG:

    ?MSGX := '## 実行倉庫ｺｰﾄﾞ = ' && ?SOKCD && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM    :=    ?FILNMH && ?SOKCD
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?JHSDENF  :=    %NCAT(?FILID,?LIBID)
    ?JHSDENFN :=    %STRING(?JHSDENF)
    ?MSGX     :=    '## 倉庫ﾌｧｲﾙ名 = ' && ?JHSDENFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ?FILNM    :=    ?FILNMD && ?SOKCD
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?DENPYO   :=    %NCAT(?FILID,?LIBID)
    ?DENPYON  :=    %STRING(?DENPYO)
    ?MSGX     :=    '## 伝票ﾌｧｲﾙ名 = ' && ?DENPYON && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                                      /.##SORT順 FILE##./
    ?FILNM    :=    ?FILNMO && ?SOKCD
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?ONLDT    :=    %NCAT(?FILID,?LIBID)
    ?ONLDTN   :=    %STRING(?ONLDT)
    ?MSGX     :=    '## 中間F(PF)  = ' && ?ONLDTN  && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                                      /.##SORT順 FILE##./
    ?FILNM    :=    ?FILNMO && ?SOKCD && ?KEYA
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?ONLDT1   :=    %NCAT(?FILID,?LIBID)
    ?ONLDT1N  :=    %STRING(?ONLDT1)
    ?MSGX     :=    '## 中間F(LF1) = ' && ?ONLDT1N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                                      /.##SORT順 FILE##./
    ?FILNM    :=    ?FILNMO && ?SOKCD && ?KEYB
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?ONLDT2   :=    %NCAT(?FILID,?LIBID)
    ?ONLDT2N  :=    %STRING(?ONLDT2)
    ?MSGX     :=    '## 中間F(LF2) = ' && ?ONLDT2N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                                      /.##SORT順 FILE##./
    ?FILNM    :=    ?FILNMO && ?SOKCD  && ?KEYC
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?ONLDT3   :=    %NCAT(?FILID,?LIBID)
    ?ONLDT3N  :=    %STRING(?ONLDT3)
    ?MSGX     :=    '## 中間F(LF3) = ' && ?ONLDT3N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##伝票出力順指定画面表示##./
SKY2001I:

    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    CALL      PGM-SKY2001I.TOKELIB,PARA-(?SOKCD,?JYUN,?NOUDT1,?NOUDT2)
    IF        @PGMEC    ^=   0    THEN
              ?PGMEC := @PGMEC
              IF    ?PGMEC  =  4010
                 THEN
                    ?MSGX := '##取消終了##'
                    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                    GOTO   RTN
                 ELSE
                    GOTO   ABEND
                 END
    END
/.##オンラインデータ物理Ｆへコピー##./
    SETPF FILE-?JHSDENF,TOFILE-?ONLDT,ADD-@NO,ACTCHK-@NO
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##出力順別抽出処理##./
SKY2101B:

    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    OVRF      FILE-ONLDT1,TOFILE-?ONLDT1
    OVRF      FILE-ONLDT2,TOFILE-?ONLDT2
    OVRF      FILE-ONLDT3,TOFILE-?ONLDT3
    OVRF      FILE-JHSDENF,TOFILE-?DENPYO
    CALL      PGM-SKY2101B.TOKELIB,PARA-(?JYUN,?NOUDT1,?NOUDT2)
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.  仕入伝票発行（専用）                              ./
PSY14050:


    ?PRT      :=  @OUTQN
    ?PRTR     :=  %STRING(?PRT)
    ?MSGX    :=  '## 選択ﾌﾟﾘﾝﾀｰ = ' && ?PRTR
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    OVRPRTF   FILE-PRTF,TOFILE-DOITOPRT.TOKELIB,MEDLIB-TOKELIB
    OVRF      FILE-DTSHIRED,TOFILE-?DENPYO
    CALL      PGM-SSY1405L.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

RTN:

/.## ﾌﾟﾘﾝﾀｰ切替 ##./
    IF      ?KBN   =    '1'   THEN
            CHGCMVAR '@OUTQN',XSYSLSTQ
    END
    IF      ?KBN   =    '2'   THEN
            CHGCMVAR '@OUTQN',XSYSLSTQ
    END
    IF      ?KBN   =    '3'   THEN
            IF   ?WS  =  'WKSTNH83'  THEN
                 CHGCMVAR '@OUTQN',XXPTOMQ
            END
    END
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:  /.ﾌﾟﾛｸﾞﾗﾑ異常終了時処理./

    IF      ?KBN   =    '1'   THEN
            CHGCMVAR '@OUTQN',XSYSLSTQ
    END
    IF      ?KBN   =    '2'   THEN
            CHGCMVAR '@OUTQN',XSYSLSTQ
    END
    IF      ?KBN   =    '3'   THEN
            IF   ?WS  =  'WKSTNH83'  THEN
                 CHGCMVAR '@OUTQN',XXPTOMQ
            END
    END
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
