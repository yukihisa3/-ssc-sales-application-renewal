# PKE01001

**種別**: JCL  
**ライブラリ**: TOKCLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIB/PKE01001.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　特販システム（本社システム）          *  ./
/. *   SYSTEM-NAME :    出荷検品システム                     *  ./
/. *   JOB-ID      :    PKE01000                             *  ./
/. *   JOB-NAME    :    （出荷検品）オンラインデータ抽出     *  ./
/. ***********************************************************  ./
    PGM
/.###ﾜｰｸｴﾘｱ定義####./
    VAR ?FILNM    ,STRING*8,VALUE-'        '    /.ﾌｧｲﾙ名合併用./
    VAR ?FILNMH   ,STRING*6,VALUE-'SNDDEN'      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMK   ,STRING*6,VALUE-'SNDDEK'      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?LIBNM    ,STRING*8,VALUE-'TOKFLIB '    /.ﾗｲﾌﾞﾗﾘ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILID    ,NAME                         /.ﾌｧｲﾙ名名前型./
    VAR ?LIBID    ,NAME                         /.ﾗｲﾌﾞﾗﾘ名名前型./
    VAR ?SNDDENF  ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?SNDDENFN ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?SNDDEKF  ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?SNDDEKFN ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR ?PGMEC    ,INTEGER                      /.ﾌﾟﾛｸﾞﾗﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMECX   ,STRING*11                    /.ｼｽﾃﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMEM    ,STRING*99                    /.ｼｽﾃﾑｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ格納ﾃｰﾌﾞﾙ./
    VAR ?MSGX     ,STRING*99                    /.SNDMSG表示用./
    VAR ?PGMID    ,STRING*8,VALUE-'PSY00130'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.STEP-ID./
    VAR ?P1       ,STRING*8,VALUE-'00000000'    /.受信日付  ./
    VAR ?P2       ,STRING*4,VALUE-'0000'        /.受信時間  ./
    VAR ?P3       ,STRING*8,VALUE-'00000000'    /.受信取引先./
    VAR ?P4       ,STRING*2,VALUE-'00'          /.倉庫(自)  ./
    VAR ?P5       ,STRING*2,VALUE-'00'          /.倉庫(代表)./
/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL
/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL   TOKELIB/TOKFLIB
/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?P4,?P5)
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END

/.##オンラインデータ抽出指示入力##./
SKE0100I:

    ?STEP :=   'SKE0100I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    OVRF      FILE-SHTDENLA,TOFILE-SHTDENLA.TOKFLIB
    OVRF      FILE-TOKMS2,TOFILE-TOKMS2.TOKFLIB
    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    CALL      PGM-SKE0100I.TOKELIB,PARA-(?P1,?P2,?P3,?P4,?P5)
    IF        @PGMEC    ^=   0    THEN
         IF   @PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              GOTO RTN
         ELSE
              GOTO ABEND
         END
    END
/.##全倉庫選択の場合は、全倉庫処理へ##./
    IF   ?P4  =  '  '  THEN
          GOTO  SKE0111B
    END

/.##ﾌｧｲﾙ名称取得##./
FILNMCHG:

    ?MSGX := '## 実行倉庫ｺｰﾄﾞ = ' && ?P4 && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM    :=    ?FILNMH && ?P4        /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?SNDDENF  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?SNDDENFN :=    %STRING(?SNDDENF)
    ?MSGX     :=    '## 倉庫ﾌｧｲﾙ名 = ' && ?SNDDENFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM    :=    ?FILNMK && ?P4        /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?SNDDEKF  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?SNDDEKFN :=    %STRING(?SNDDEKF)
    ?MSGX     :=    '## 件数ﾌｧｲﾙ名 = ' && ?SNDDEKFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES


/.##オンラインデータ抽出##./
SKE0110B:

    ?STEP :=   'SKE0110B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-SHTDENLA,TOFILE-SHTDENLA.TOKFLIB
    OVRF      FILE-SNDDENF,TOFILE-?SNDDENF
    OVRF      FILE-SNDDEKF,TOFILE-?SNDDEKF
    OVRF      FILE-SHOTBL1,TOFILE-SHOTBL1.TOKFLIB
    CALL      PGM-SKE0110B.TOKELIB,PARA-(?P1,?P2,?P3,?P4)
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
    FIMPORT FILE-SNDDEN6A.TOKFLIB,PARA-SYUKA,UNIT-1
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
    FIMPORT FILE-SNDDEK6A.TOKFLIB,PARA-SYUKA,UNIT-2
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
    FIMPORT FILE-SNDFIN62.TOKFLIB,PARA-SYUKA,UNIT-6
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
    CLRFILE FILE-SNDFIN62.TOKFLIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
/.##ﾌﾟﾛｸﾞﾗﾑ終了へ##./
    GOTO      RTN

/.##オンラインデータ抽出（全件数）##./
SKE0111B:

    ?STEP :=   'SKE0111B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    DEFLIBL   TOKELIB

    OVRF      FILE-SHTDENF,TOFILE-SHTDENLA.TOKFLIB
    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    OVRF      FILE-SHOTBL1,TOFILE-SHOTBL1.TOKFLIB
    OVRF      FILE-SNDDEN01,TOFILE-SNDDEN01.TOKFLIB
    OVRF      FILE-SNDDEN02,TOFILE-SNDDEN62.TOKFLIB
    OVRF      FILE-SNDDEN03,TOFILE-SNDDEN63.TOKFLIB
    OVRF      FILE-SNDDEN04,TOFILE-SNDDEN04.TOKFLIB
    OVRF      FILE-SNDDEN05,TOFILE-SNDDEN05.TOKFLIB
    OVRF      FILE-SNDDEN06,TOFILE-SNDDEN06.TOKFLIB
    OVRF      FILE-SNDDEN07,TOFILE-SNDDEN07.TOKFLIB
    OVRF      FILE-SNDDEN08,TOFILE-SNDDEN08.TOKFLIB
    OVRF      FILE-SNDDEN09,TOFILE-SNDDEN09.TOKFLIB
    OVRF      FILE-SNDDEN10,TOFILE-SNDDEN10.TOKFLIB
    OVRF      FILE-SNDDEN11,TOFILE-SNDDEN11.TOKFLIB
    OVRF      FILE-SNDDEN12,TOFILE-SNDDEN12.TOKFLIB
    OVRF      FILE-SNDDEN13,TOFILE-SNDDEN13.TOKFLIB
    OVRF      FILE-SNDDEN14,TOFILE-SNDDEN14.TOKFLIB
    OVRF      FILE-SNDDEN15,TOFILE-SNDDEN15.TOKFLIB
    OVRF      FILE-SNDDEN16,TOFILE-SNDDEN16.TOKFLIB
    OVRF      FILE-SNDDEN17,TOFILE-SNDDEN17.TOKFLIB
    OVRF      FILE-SNDDEN18,TOFILE-SNDDEN18.TOKFLIB
    OVRF      FILE-SNDDEN19,TOFILE-SNDDEN19.TOKFLIB
    OVRF      FILE-SNDDEN20,TOFILE-SNDDEN20.TOKFLIB
    OVRF      FILE-SNDDEK01,TOFILE-SNDDEN01.TOKFLIB
    OVRF      FILE-SNDDEK02,TOFILE-SNDDEK62.TOKFLIB
    OVRF      FILE-SNDDEK03,TOFILE-SNDDEK63.TOKFLIB
    OVRF      FILE-SNDDEK04,TOFILE-SNDDEK04.TOKFLIB
    OVRF      FILE-SNDDEK05,TOFILE-SNDDEK05.TOKFLIB
    OVRF      FILE-SNDDEK06,TOFILE-SNDDEK06.TOKFLIB
    OVRF      FILE-SNDDEK07,TOFILE-SNDDEK07.TOKFLIB
    OVRF      FILE-SNDDEK08,TOFILE-SNDDEK08.TOKFLIB
    OVRF      FILE-SNDDEK09,TOFILE-SNDDEK09.TOKFLIB
    OVRF      FILE-SNDDEK10,TOFILE-SNDDEK10.TOKFLIB
    OVRF      FILE-SNDDEK11,TOFILE-SNDDEK11.TOKFLIB
    OVRF      FILE-SNDDEK12,TOFILE-SNDDEK12.TOKFLIB
    OVRF      FILE-SNDDEK13,TOFILE-SNDDEK13.TOKFLIB
    OVRF      FILE-SNDDEK14,TOFILE-SNDDEK14.TOKFLIB
    OVRF      FILE-SNDDEK15,TOFILE-SNDDEK15.TOKFLIB
    OVRF      FILE-SNDDEK16,TOFILE-SNDDEK16.TOKFLIB
    OVRF      FILE-SNDDEK17,TOFILE-SNDDEK17.TOKFLIB
    OVRF      FILE-SNDDEK18,TOFILE-SNDDEK18.TOKFLIB
    OVRF      FILE-SNDDEK19,TOFILE-SNDDEK19.TOKFLIB
    OVRF      FILE-SNDDEK20,TOFILE-SNDDEK20.TOKFLIB
    CALL      PGM-SKE0111B.TOKELIB,PARA-(?P1,?P2,?P3,?P4)
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC


ABEND:

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=    '### ' && ?PGMID && ' ABEND' && ' ###'
    ?MSG(2)   :=    '### ' && ' PGMEC = ' &&
                     %SBSTR(?PGMECX,8,4) && ' ###'
    ?MSG(3)   :=    '###' && ' LINE = '  && %LAST(LINE)      && ' ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
