# PNI00100

**種別**: JCL  
**ライブラリ**: TOKCLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIB/PNI00100.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　特販システム（本社システム）          *  ./
/. *   SYSTEM-NAME :                                         *  ./
/. *   JOB-ID      :    PNI00100                                *  ./
/. *   JOB-NAME    :    日次更新                             *  ./
/. *               :    売上日次更新                         *  ./
/. *               :    在庫日時更新を行う                   *  ./
/. ***********************************************************  ./
    PGM
    VAR       ?PGMEC  ,INTEGER
    VAR       ?PGMECX ,STRING*11
    VAR       ?PGMEM  ,STRING*99
    VAR       ?MSG    ,STRING*99(6)
    VAR       ?MSGX   ,STRING*99
    VAR       ?PGMID  ,STRING*8,VALUE-'PNI00100'
    VAR       ?STEP   ,STRING*8
    VAR       ?WKSTN  ,STRING*8
    VAR       ?JBNM   ,STRING*24!MIXED            /.業務漢字名 ./
    VAR       ?JBID   ,STRING*10                  /.業務ＩＤ   ./
    VAR       ?PGNM   ,STRING*24!MIXED            /.ＰＧ漢字名 ./
    VAR       ?PGID   ,STRING*10                  /.ＰＧＩＤ　 ./
/.-----------------------------------------------------------./
    VAR       ?CLID   ,STRING*8                   /.ＣＬＩＤ   ./
    VAR       ?MSG1   ,STRING*80                  /.開始終了MSG./
    VAR       ?OPR1   ,STRING*50                  /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?OPR2   ,STRING*50                  /.      2    ./
    VAR       ?OPR3   ,STRING*50                  /.      3    ./
    VAR       ?OPR4   ,STRING*50                  /.      4    ./
    VAR       ?OPR5   ,STRING*50                  /.      5    ./

    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

CHK:          /.資源の事前獲得./
    ASSIGN FILE-SHTDENF.TOKFLIB!@XCL/HSHOTBL.TOKFLIB!@XCL/
           HMEIMS.TOKFLIB!@XCL/ZNYUKDT.TOKFLIB!@XCL/
           ZNYUSDT.TOKFLIB!@XCL/ZSGYODT.TOKFLIB!@XCL/
           TOKU.TOKFLIB!@XCL/
           ZZAIMS.TOKFLIB!@XCL
    IF     @PGMEC  ^=    0    THEN    GOTO   ERR  END

TANMCHK:   /.実行端末チェック./

    ?WKSTN    :=      %STRING(@ORGWS)
    SNDMSG ?WKSTN,TO-XCTL

    IF     ?WKSTN     ^=    'WKSTN001'
        THEN
            SNDMSG '＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊',TO-XCTL
            SNDMSG '＊　　　　　　　　　　　　　　　＊',TO-XCTL
            SNDMSG '＊この端末からは処理できません　＊',TO-XCTL
            SNDMSG '＊　　　　　　　　　　　　　　　＊',TO-XCTL
            SNDMSG '＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊',TO-XCTL
            GOTO   RTN   END

/.##売上データ抽出処理確認##./
STEP1:

    ?JBID    :=   'PNI00100'
    ?CLID    :=   'PNI00100'
    ?MSG1    := '<<< ' && ?CLID && ' START '
               && %SBSTR(@SDATEY,1,2) && '/'
               && %SBSTR(@SDATEY,3,2) && '/'
               && %SBSTR(@SDATEY,5,2) && ' '
               && %SBSTR(@STIME,1,2)  && ':'
               && %SBSTR(@STIME,3,2)  && ':'
               && %SBSTR(@STIME,5,2)  && ' >>>'
     SNDMSG  MSG-?MSG1,TOWS-@ORGWS,JLOG-@YES

    ?OPR1  :=  '　＃＃＃＃＃＃＃　日次更新処理　＃＃＃＃＃＃＃＃　'
    ?OPR2  :=  '　　日次更新処理を開始します。確認して下さい。　　'
    ?OPR3  :=  '　　１．データ退避用のＭＯをセットして下さい。　　'
    ?OPR4  :=  '　　２．データ退避用ＤＡＴをセットして下さい。　　'
    ?OPR5  :=  '　＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃　'
    CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)

    DEFLIBL TOKELIB

/.##日次更新条件入力##./
STEP2:

    ?STEP :=   'SNI0010I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    OVRF      FILE-TOKMS2,TOFILE-TOKMS2.TOKFLIB
    CALL      PGM-SNI0010I.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              IF    @PGMEC = 4010 THEN
                    ?MSGX := '##取消終了##'
                    SNDMSG MSG-?MSGX,TO-XCTL
                    RETURN PGMEC-@PGMEC
              ELSE
                    GOTO ABEND
              END
    END

/.##日次データバックアップ##./
STEP3:

    ?STEP :=   'SAVFILE '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ?MSG1    := '<<< '
               && %SBSTR(@SDATEY,1,2) && '/'
               && %SBSTR(@SDATEY,3,2) && '/'
               && %SBSTR(@SDATEY,5,2) && ' '
               && %SBSTR(@STIME,1,2)  && ':'
               && %SBSTR(@STIME,3,2)  && ':'
               && %SBSTR(@STIME,5,2)  && ' >>>'
     SNDMSG  MSG-?MSG1,TOWS-@ORGWS,JLOG-@YES
    SNDMSG MSG-'売上ファイル　　退避中',TO-XCTL
       SAVFILE FILE-SHTDENF.TOKFLIB,TODEV-MO,ADD-@NO,MODE-@USED
    SNDMSG MSG-'売上中間データ　退避中',TO-XCTL
       SAVFILE FILE-JHSHENF.TOKFLIB,TODEV-MO,ADD-@YES,MODE-@USED
    SNDMSG MSG-'商品変換テーブル退避中',TO-XCTL
       SAVFILE FILE-HSHOTBL.TOKFLIB,TODEV-MO,ADD-@YES,MODE-@USED
    SNDMSG MSG-'商品名称マスタ　退避中',TO-XCTL
       SAVFILE FILE-HMEIMS.TOKFLIB,TODEV-MO,ADD-@YES,MODE-@USED
    SNDMSG MSG-'入庫ファイル　　退避中',TO-XCTL
       SAVFILE FILE-ZNYUKDT.TOKFLIB,TODEV-MO,ADD-@YES,MODE-@USED
    SNDMSG MSG-'出庫ファイル　　退避中',TO-XCTL
       SAVFILE FILE-ZNYUSDT.TOKFLIB,TODEV-MO,ADD-@YES,MODE-@USED
    SNDMSG MSG-'作業実績ファイル退避中',TO-XCTL
       SAVFILE FILE-ZSGYODT.TOKFLIB,TODEV-MO,ADD-@YES,MODE-@USED
    SNDMSG MSG-'当日計上ファイル退避中',TO-XCTL
       SAVFILE FILE-TOKU.TOKFLIB,TODEV-MO,ADD-@YES,MODE-@USED
    SNDMSG MSG-'在庫マスタ　　　退避中',TO-XCTL
       SAVFILE FILE-ZZAIMS.TOKFLIB,TODEV-MO,ADD-@YES,MODE-@USED
/.獲得資源の解放./
    RELEASE FILE-SHTDENF.TOKFLIB!@XCL/HSHOTBL.TOKFLIB!@XCL/
            HMEIMS.TOKFLIB!@XCL/ZNYUKDT.TOKFLIB!@XCL/
            ZNYUSDT.TOKFLIB!@XCL/ZSGYODT.TOKFLIB!@XCL/
            TOKU.TOKFLIB!@XCL/
            ZZAIMS.TOKFLIB!@XCL

/.##全データＤＡＴ退避処理##./
STEP4:

    ?STEP :=   'SAVLIB  '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    SNDMSG MSG-'全データＤＡＴ退避開始',TO-XCTL
    ?MSG1    := '<<< '
               && %SBSTR(@SDATEY,1,2) && '/'
               && %SBSTR(@SDATEY,3,2) && '/'
               && %SBSTR(@SDATEY,5,2) && ' '
               && %SBSTR(@STIME,1,2)  && ':'
               && %SBSTR(@STIME,3,2)  && ':'
               && %SBSTR(@STIME,5,2)  && ' >>>'
     SNDMSG  MSG-?MSG1,TOWS-@ORGWS,JLOG-@YES
    SNDMSG MSG-'ＯＮＬＢＬＩＢ　退避中',TO-XCTL
    SNDMSG MSG-'ＴＯＫＦＬＩＢ　退避中',TO-XCTL
    SAVLIB LIB-TOKFLIB/ONLBLIB,TODEV-DAT,ADD-@NO,REWIND-@NO,
           MODE-@USED
    SNDMSG MSG-'全データＤＡＴ退避終了',TO-XCTL
    ?MSG1    := '<<< '
               && %SBSTR(@SDATEY,1,2) && '/'
               && %SBSTR(@SDATEY,3,2) && '/'
               && %SBSTR(@SDATEY,5,2) && ' '
               && %SBSTR(@STIME,1,2)  && ':'
               && %SBSTR(@STIME,3,2)  && ':'
               && %SBSTR(@STIME,5,2)  && ' >>>'
     SNDMSG  MSG-?MSG1,TOWS-@ORGWS,JLOG-@YES
/.資源の獲得を再度行う./
    ASSIGN FILE-SHTDENF.TOKFLIB!@XCL/HSHOTBL.TOKFLIB!@XCL/
           HMEIMS.TOKFLIB!@XCL/ZNYUKDT.TOKFLIB!@XCL/
           ZNYUSDT.TOKFLIB!@XCL/ZSGYODT.TOKFLIB!@XCL/
           TOKU.TOKFLIB!@XCL/
           ZZAIMS.TOKFLIB!@XCL

/.##在庫引落し##./
STEP5:

    ?STEP :=   'SNI0020B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-DENJNL6,TOFILE-SHTDENL6.TOKFLIB
    OVRF      FILE-ZZAIMS1,TOFILE-ZZAIMS1.TOKFLIB
    OVRF      FILE-SHOTBL1,TOFILE-SHOTBL1.TOKFLIB
    OVRF      FILE-SHOTBL2,TOFILE-SHOTBL2.TOKFLIB
    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    OVRF      FILE-MEIMS1,TOFILE-MEIMS1.TOKFLIB
    CALL      PGM-SNI0020B.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##売上日次更新##./
STEP6:

    ?STEP :=   'SNI0030B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-DENJNL4,TOFILE-SHTDENL4.TOKFLIB
    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    OVRF      FILE-TOKMS2,TOFILE-TOKMS2.TOKFLIB
    OVRF      FILE-TENMS1,TOFILE-TENMS1.TOKFLIB
    OVRF      FILE-SHOTBL1,TOFILE-SHOTBL1.TOKFLIB
    OVRF      FILE-TOKU,TOFILE-TOKU.TOKFLIB
    OVRF      FILE-MEIMS1,TOFILE-MEIMS1.TOKFLIB
    CALL      PGM-SNI0030B.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END
/.##電算室売上計上ＤＴ→バックアップ##./
CPY:

    SNDMSG MSG-'データＣＯＰＹ待合せ中',TO-XCTL
    CALL TWAIT3.XUCL

    CNVFILE FILE-TOKU.TOKFLIB,TOFILE-TOKUWK.TOKWLIB,BF-4
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##売上データ確認リスト##./
STEP7:

    ?STEP :=   'SNI0040L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRPRTF   FILE-PRTF,TOFILE-XU04LP.XUCL,MEDLIB-TOKELIB
    OVRF      FILE-TOKU1,TOFILE-TOKU1.TOKFLIB
    OVRF      FILE-TOKMS3,TOFILE-TOKMS3.TOKFLIB
    CALL      PGM-SNI0040L.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##仕入計上##./
STEP8:

    ?STEP :=   'ZDA0120B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZNYUKDT1,TOFILE-ZNYUKDT1.TOKFLIB
    OVRF      FILE-ZHACHDT1,TOFILE-ZHACHDT1.TOKFLIB
    OVRF      FILE-SHOTBL4,TOFILE-SHOTBL4.TOKFLIB
    OVRF      FILE-TOKMS2,TOFILE-TOKMS2.TOKFLIB
    OVRF      FILE-TOKU,TOFILE-TOKUZ.TOKFLIB
    CALL      PGM-ZDA0120B.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##入出庫計上##./
STEP9:

    ?STEP :=   'ZDA0130B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZNYUSDT1,TOFILE-ZNYUSDT1.TOKFLIB
    OVRF      FILE-ZSGYODT1,TOFILE-ZSGYODT1.TOKFLIB
    OVRF      FILE-TOKU,TOFILE-TOKUZ.TOKFLIB
    CALL      PGM-ZDA0130B.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##仕入／入出庫ＤＴ件数カウント##./
STEP10:

    ?STEP :=   'SNI0050B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-TOKU,TOFILE-TOKUZ.TOKFLIB
    CALL      SNI0050B.TOKELIB
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##売上計上ＤＴ／仕入計上ＤＴ→ワークバックアップ##./
STEP11:

    ?STEP :=   'CNVFILE '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CNVFILE FILE-TOKUWK.TOKWLIB,TOFILE-TOKU.TOKWLIB,ADD-@NO,
            BF-4
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

    CNVFILE FILE-TOKUZ.TOKFLIB,TOFILE-TOKU.TOKWLIB,BF-4
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##売上中間ファイルクリア##./
    CLRFILE JHSHENF.TOKFLIB

RTN:  /.資源の解放./

    RELEASE FILE-SHTDENF.TOKFLIB!@XCL/HSHOTBL.TOKFLIB!@XCL/
            HMEIMS.TOKFLIB!@XCL/ZNYUKDT.TOKFLIB!@XCL/
            ZNYUSDT.TOKFLIB!@XCL/ZSGYODT.TOKFLIB!@XCL/
            TOKU.TOKFLIB!@XCL/
            ZZAIMS.TOKFLIB!@XCL

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:
      /.資源の解放./
    RELEASE FILE-SHTDENF.TOKFLIB!@XCL/HSHOTBL.TOKFLIB!@XCL/
            HMEIMS.TOKFLIB!@XCL/ZNYUKDT.TOKFLIB!@XCL/
            ZNYUSDT.TOKFLIB!@XCL/ZSGYODT.TOKFLIB!@XCL/
            TOKU.TOKFLIB!@XCL/
            ZZAIMS.TOKFLIB!@XCL

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'

    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    DMPWSLOG WKSTN-WKSTN001,FROM-@NEWACT

    ?OPR1  :=  '　＃＃＃＃＃＃　日次更新異常終了　＃＃＃＃＃＃　　'
    ?OPR2  :=  '　日次更新にて異常終了が発生しました。　　　　　　'
    ?OPR3  :=  '　ログリストが出力されていますので，確認後，　　　'
    ?OPR4  :=  '　_ナブ・アシストまで連絡して下さい。　　　　　　'
    ?OPR5  :=  '　異常発生＊異常発生＊異常発生＊異常発生＊異常＊　'
    CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)

    RETURN    PGMEC-@PGMEC
ERR:  /.資源の解放./

    ?OPR1  :=  '　　＃＃＃＃＃＃＃　資源使用中　＃＃＃＃＃＃＃　　'
    ?OPR2  :=  '　　　　　　　　　　　　　　　　　　　　　　　　　'
    ?OPR3  :=  '　　他端末にて資源を使用中です。　　　　　　　　　'
    ?OPR4  :=  '　　全端末の使用状況を確認して下さい。　　　　　　'
    ?OPR5  :=  '　ＥＮＴＥＲでリスタート，ＰＦ９でプログラム終了　'
    CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)

    GOTO      CHK


```
