# PKE10300

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PKE10300.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　出荷検品システム（本社システム）      *  ./
/. *   SYSTEM-NAME :    検品システム　　　　　　　　　　　　 *  ./
/. *   JOB-ID      :    PKE10300                             *  ./
/. *   JOB-NAME    :    出荷検品データ受信＋件数照会         *  ./
/. *                      （カインズＴＣ１）                 *  ./
/. ***********************************************************  ./
    PGM
/.##ﾜｰｸ定義##./
    VAR   ?SOKO  ,STRING*2,VALUE-'  '       /.倉庫CD./
    VAR   ?DSOKO ,STRING*2,VALUE-'  '       /.倉庫CD./
    VAR   ?CHK   ,STRING*1,VALUE-' '        /.存在ﾁｪｯｸ   ./
    VAR   ?KENSU ,STRING*6,VALUE-'      '   /.データ件数./
    VAR   ?WS    ,STRING*8,VALUE-'        ' /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR   ?WKSTN ,NAME!MOD                  /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR   ?PGMEC ,INTEGER
    VAR   ?PGMECX,STRING*11
    VAR   ?PGMEM ,STRING*99
    VAR   ?CLNM  ,STRING*40                 /.CL名称       ./
    VAR   ?KEKA1 ,STRING*40                 /.ﾒｯｾｰｼﾞ1      ./
    VAR   ?KEKA2 ,STRING*40                 /.ﾒｯｾｰｼﾞ2      ./
    VAR   ?KEKA3 ,STRING*40                 /.ﾒｯｾｰｼﾞ3      ./
    VAR   ?KEKA4 ,STRING*40                 /.ﾒｯｾｰｼﾞ4      ./
    VAR   ?MSG   ,STRING*99(6)
    VAR   ?MSGX  ,STRING*99
    VAR   ?CLID ,STRING*8,VALUE-'PKE10300'
    VAR   ?STEP  ,STRING*8
    VAR   ?FILNM ,STRING*8,VALUE-'        ' /.ﾌｧｲﾙ名合併用./
    VAR   ?FILNMS,STRING*6,VALUE-'RCVCZD'   /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR   ?LIBNM ,STRING*8,VALUE-'TOKKPLIB' /.ﾗｲﾌﾞﾗﾘ名ﾃﾞﾌｫﾙﾄ./
    VAR   ?FILID ,NAME                      /.ﾌｧｲﾙ名名前型./
    VAR   ?LIBID ,NAME                      /.ﾗｲﾌﾞﾗﾘ名名前型./
    VAR   ?RCVCZD ,NAME!MOD                /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR   ?RCVCZDN,STRING*17               /.ﾌｧｲﾙ名表示用./
    VAR   ?RCVFUTF ,NAME!MOD                /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR   ?RCVFUTFN,STRING*17               /.ﾌｧｲﾙ名表示用./
    VAR   ?RCVKONF ,NAME!MOD                /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR   ?RCVKONFN,STRING*17               /.ﾌｧｲﾙ名表示用./
    VAR   ?RCVERRF ,NAME!MOD                /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR   ?RCVERRFN,STRING*17               /.ﾌｧｲﾙ名表示用./

    ?MSGX :=  '***   '  && ?CLID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL TOKELIB/TOKFLIB/TOKELIBO/TOKKLIB/BMSFLIB/TOKDLIB/
            TOKDTLIB/TOKKPLIB

/.##実行PG名称ｾｯﾄ##./
    ?CLNM := 'カインズ出荷検品データ受信・件数照会'

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKO,?DSOKO)
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END

/.##倉庫ｺｰﾄﾞ選択##./
SKY1701I:

    ?STEP :=   'SKY1701I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    CALL      PGM-SKY1702I.TOKELIB,PARA-(?SOKO,?DSOKO)
    IF        @PGMEC    ^=   0    THEN
              ?PGMEC := @PGMEC
              IF    ?PGMEC  =  4010
                 THEN
                    ?MSGX := '##取消終了##'
                    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                    RETURN PGMEC-@PGMEC
                 ELSE
                    GOTO   ABEND
              END
    END

/.##ﾌｧｲﾙ名称取得##./
FILNMCHG:

    ?MSGX := '## 実行倉庫ｺｰﾄﾞ = ' && ?SOKO && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM    :=    ?FILNMS && ?SOKO      /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?RCVCZD  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?RCVCZDN :=    %STRING(?RCVCZD)
    ?MSGX     :=    '## ファイル = ' && ?RCVCZDN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##カインズ出荷梱包データ存在チェック##./
SBT0400B:

    ?STEP :=   'SBT0400B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-CNZLNKF,TOFILE-SYKSNDCZ.ONLBLIB
    CALL      PGM-SBT0400B.TOKELIBO,PARA-(?CHK)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 :=  'カインズ出荷梱包データ存在チェック'
              GOTO ABEND
    END
    ?MSGX :=  '## KEKKA = ' && ?CHK   &&   ' ##*'
    SNDMSG    ?MSGX,TO-XCTL
  /.##チェック区分＝０（未送信の場合）異常終了させる##./
    IF        ?CHK      =  '0'  THEN
              ?KEKA1 :=  '＃＃カインズ出荷梱包データチェック＃＃'
              ?KEKA2 :=  '出荷梱包データが未送信状態です。'
              ?KEKA3 :=  '送信済か確認し、再度、取込処理を実行'
              ?KEKA4 :=  'して下さい。'
              GOTO ABEND1
    END

/.##検品結果データ転送##./
PFEXPORT:

    ?STEP :=   'PFEXPORT'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FEXPORT FILE-?RCVCZD,MODE-@REP,PARA-KENPIN,
            UNIT-17
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END
/.##検品結果データ件数カウント##./
NKE1030B:

    ?STEP :=   'NKE1030B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-RCVCZDXX,TOFILE-?RCVCZD
    CALL      PGM-NKE1030B.TOKSOLIB,PARA-(?KENSU)

    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END
    SNDMSG ?KENSU,TO-XCTL.@ORGPROF

/.##検品結果データ件数照会##./
NKE1031I:

    ?STEP :=   'NKE1031I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKMDLIB
    CALL      PGM-NKE1031I.TOKSOLIB,PARA-(?SOKO,?KENSU)

    IF        @PGMEC    ^=   0    THEN
              GOTO   ABEND
    END

RTN: /.##ﾌﾟﾛｸﾞﾗﾑ正常時、終了ﾒｯｾｰｼﾞ##./

    ?MSGX :=  '***   '  && ?CLID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:/.##ﾌﾟﾛｸﾞﾗﾑ異常終了時、終了ﾒｯｾｰｼﾞ##./

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=    '### ' && ?CLID && ' ABEND' && ' ###'
    ?MSG(2)   :=    '### ' && ' PGMEC = ' &&
                     %SBSTR(?PGMECX,8,4) && ' ###'
    ?MSG(3)   :=    '###' && ' LINE = '  && %LAST(LINE)      && ' ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

/.##未送信時終了処理##./
ABEND1:

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
              /.１２３４５６７８９０１２３４５６７８９０./
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?CLNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=    '### ' && ?CLID && ' ABEND' && ' ###'
    ?MSG(2)   :=    '### ' && ' PGMEC = ' &&
                     %SBSTR(?PGMECX,8,4) && ' ###'
    ?MSG(3)   :=    '###' && ' LINE = '  && %LAST(LINE)      && ' ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

```
