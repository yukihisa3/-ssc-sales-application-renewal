# PFU30500

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PFU30500.CL`

## ソースコード

```jcl
/. ***TEST****************************************************  ./
/. *   サカタのタネ　                                        *  ./
/. *   SYSTEM-NAME :    ＨＧ基幹システム                     *  ./
/. *   JOB-ID      :    PFU30500                             *  ./
/. *   JOB-NAME    :    社内振替明細入力　　　　　           *  ./
/. ***********************************************************  ./
    PGM (P1-?SYUBETU)

/.##INパラメタ##./
    PARA  ?SYUBETU ,STRING*1,IN,VALUE-' '
            /.実行種別                               ./
            /.  '1'    :   発注明細入力     ./
            /.  '2'    :   入荷明細入力　   ./

/.--------------------------------------------------------------./
/.###ﾜｰｸｴﾘｱ定義####./
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR ?PGMEC    ,INTEGER                      /.ﾌﾟﾛｸﾞﾗﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMECX   ,STRING*11                    /.ｼｽﾃﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMEM    ,STRING*99                    /.ｼｽﾃﾑｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?PGMES    ,STRING*5                     /.ｼｽﾃﾑｴﾗｰｺｰﾄﾞ  ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ格納ﾃｰﾌﾞﾙ./
    VAR ?MSGX     ,STRING*99                    /.SNDMSG表示用./
    VAR ?PGMID    ,STRING*8,VALUE-'PFU30500'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.STEP-ID./

    VAR ?SOKO     ,STRING*2,VALUE-'  '           /.実行倉庫ＣＤ./
    VAR ?DSOKO    ,STRING*2,VALUE-'  '           /.代表倉庫ＣＤ./
    VAR ?PGNM     ,STRING*40                     /.ﾒｯｾｰｼﾞ1    ./
    VAR ?KEKA1    ,STRING*40                     /.      2    ./
    VAR ?KEKA2    ,STRING*40                     /.      3    ./
    VAR ?KEKA3    ,STRING*40                     /.      4    ./
    VAR ?KEKA4    ,STRING*40                     /.      5    ./
    VAR ?DELDATE  ,STRING*8,VALUE-'00000000'
    VAR ?OPR1     ,STRING*50,VALUE-              /.ﾒｯｾｰｼﾞ1    ./
        '                                                  '
    VAR ?OPR2     ,STRING*50,VALUE-              /.ﾒｯｾｰｼﾞ2    ./
        '                                                  '
    VAR ?OPR3     ,STRING*50,VALUE-              /.ﾒｯｾｰｼﾞ3    ./
        '                                                  '
    VAR ?OPR4     ,STRING*50,VALUE-              /.ﾒｯｾｰｼﾞ4    ./
        '                                                  '
    VAR ?OPR5     ,STRING*50,VALUE-              /.ﾒｯｾｰｼﾞ5    ./
        '                                                  '

    VAR ?CALL     ,STRING*1,VALUE-'C'            /.呼出区分./
    VAR ?SYORI    ,STRING*1,VALUE-' '            /.処理区分./
    VAR ?BUMON    ,STRING*4,VALUE-'    '         /.部門CD./
    VAR ?TANCD8   ,STRING*8,VALUE-'        '     /.担当者CD./
    VAR ?TANCD    ,STRING*2,VALUE-'  '           /.担当者CD./
    VAR ?DENKU    ,STRING*2,VALUE-'  '           /.伝票区分./
    VAR ?NENDO    ,STRING*4,VALUE-'    '         /.年度./
    VAR ?SEASON   ,STRING*2,VALUE-'  '           /.シーズン./
    VAR ?SHOHIN   ,STRING*8,VALUE-'        '     /.商品ＣＤ./
    VAR ?HINTAN1  ,STRING*5,VALUE-'     '        /.品単１./
    VAR ?HINTAN2  ,STRING*2,VALUE-'  '           /.品単２./
    VAR ?HINTAN3  ,STRING*1,VALUE-' '            /.品単３./

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    CASE  ?SYUBETU    OF
          #'1'# ?PGNM := '社内振替明細入力（発注）'
          #'2'# ?PGNM := '社内振替明細入力（入荷）'
          ELSE  SNDMSG   '！！！！！！！！！！！！！！！！！！',
                          TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
                SNDMSG   '！実行区分を指定し、実行して下さい！',
                          TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
                SNDMSG   '！！！！！！！！！！！！！！！！！！',
                          TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
                RETURN
    END

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG   MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL TOKELIB/TOKFLIB/TOKELIBO/TOKDTLIB/TOKKLIB/TOKDLIB/
           ZAIEXCEL/TOKMDLIB

/.##部門ｺｰﾄﾞ担当者ｺｰﾄﾞ取得##./
SIT9000B:

    ?STEP :=   'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMES    :=    @PGMES
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0
          THEN
              GOTO ABEND
    END
    ?TANCD := %SBSTR(?TANCD8,1,2)

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKO,?DSOKO)
    ?PGMEC    :=    @PGMEC
    ?PGMES    :=    @PGMES
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4  :=  '【倉庫ＣＤ取得】'
              GOTO ABEND
    END

/.##社内振替明細入力##./
SFU3050I:

    ?STEP :=   'SFU3050I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL      SFU3050I.TOKSOLIB,PARA-(?CALL,?SYUBETU,?SYORI,
                                      ?BUMON,?TANCD,
                                      ?DENKU,?NENDO,?SEASON,?SOKO,
                                      ?SHOHIN,
                                      ?HINTAN1,?HINTAN2,?HINTAN3)

    ?PGMEC    :=    @PGMEC
    ?PGMES    :=    @PGMES
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
         IF   ?PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              RETURN
         ELSE
              ?KEKA4  :=  '【社内振替明細入力】'
              GOTO ABEND
         END
    END

RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-?PGMEC


ABEND:

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  '社内振替明細入力が異常終了'
    ?KEKA2 :=  'しました。ログ採取し，ＮＡＶへ連絡して'
    ?KEKA3 :=  '下さい。'
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
  /.?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM ./
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=    '### ' && ?PGMID && ' ABEND' && ' ###'
    ?MSG(2)   :=    '### ' && ' PGMEC = ' &&
                     %SBSTR(?PGMECX,8,4) && ' ###'
    ?MSG(3)   :=    '###' && ' LINE = '  && %LAST(LINE)      && ' ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

```
