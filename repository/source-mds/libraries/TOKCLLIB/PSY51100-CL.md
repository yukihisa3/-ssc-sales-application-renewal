# PSY51100

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PSY51100.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *   サカタのタネ                                          *  ./
/. *   SYSTEM-NAME :    ナフコ出荷支援                       *  ./
/. *   JOB-ID      :    PSY51100                             *  ./
/. *   JOB-NAME    :    作場・数量一括変更用データ作成　     *  ./
/. *   UPDATE      :    2019/02/18                           *  ./
/. ***********************************************************  ./
    PGM   (P1-?PAR1,P2-?PAR2,P3-?PAR3)

    PARA ?PAR1  ,STRING*8,IN,VALUE-'00000000'
    PARA ?PAR2  ,STRING*4,IN,VALUE-'0000'
    PARA ?PAR3  ,STRING*8,IN,VALUE-'00000000'

    VAR       ?WS       ,STRING*8,VALUE-'        ' /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR       ?WKSTN    ,NAME!MOD                  /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR       ?PGMEC    ,INTEGER
    VAR       ?PGMES    ,STRING*5,VALUE-'     '
    VAR       ?PGMECX   ,STRING*11
    VAR       ?PGMEM    ,STRING*99
    VAR       ?MSG      ,STRING*99(6)
    VAR       ?MSGX     ,STRING*99
    VAR       ?PGMID    ,STRING*8,VALUE-'PSY51100'
    VAR       ?STEP     ,STRING*8
    VAR       ?BUMON    ,STRING*4,VALUE-'    '     /.部門./
    VAR       ?TANCD8   ,STRING*8,VALUE-'      '   /.部門+担当者./
    VAR       ?TANCD    ,STRING*2,VALUE-'  '       /.担当者./
    VAR       ?P1       ,STRING*8,VALUE-'00000000' /.受信日付    ./
    VAR       ?BTYMD    ,STRING*8,VALUE-'00000000' /.受信日付    ./
    VAR       ?P2       ,STRING*4,VALUE-'0000'     /.受信時間    ./
    VAR       ?BTTIME   ,STRING*4,VALUE-'0000'     /.受信時間    ./
    VAR       ?P3       ,STRING*8,VALUE-'00000000' /.受信取引先  ./
    VAR       ?BTTOKC   ,STRING*8,VALUE-'00000000' /.受信取引先  ./
    VAR       ?P4       ,STRING*2,VALUE-'0'        /.倉庫ＣＤ  ./
    VAR       ?SAKUCD   ,STRING*40,
                VALUE-'                                        '
  /.VAR       ?SAKUCDS  ,STRING*2,VALUE-'00'./     /.作場(開始)./
  /.VAR       ?SAKUCDE  ,STRING*2,VALUE-'00'./     /.作場(終了)./
    VAR       ?P7       ,STRING*2,VALUE-'00'       /.代表倉庫    ./
    VAR       ?OPR1     ,STRING*50                 /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?OPR2     ,STRING*50                 /.      2    ./
    VAR       ?OPR3     ,STRING*50                 /.      3    ./
    VAR       ?OPR4     ,STRING*50                 /.      4    ./
    VAR       ?OPR5     ,STRING*50                 /.      5    ./

/.-----------------------------------------------------------./
    VAR       ?MSG1   ,STRING*80                  /.開始終了MSG./
    VAR       ?PGNM   ,STRING*40                  /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?KEKA1  ,STRING*40                  /.      2    ./
    VAR       ?KEKA2  ,STRING*40                  /.      3    ./
    VAR       ?KEKA3  ,STRING*40                  /.      4    ./
    VAR       ?KEKA4  ,STRING*40                  /.      5    ./
/.-----------------------------------------------------------./
    /.端末別ファイルＩＤ編集用./
           /.端末番号./
    VAR       ?TANNO    ,STRING*03,VALUE-'   '
           /.一括変更データ出力ワーク./
    VAR       ?CHG      ,STRING*3,VALUE-'CHG'
    VAR       ?CHGW     ,STRING*1,VALUE-'W'
    VAR       ?CHGXXXW  ,NAME!MOD

           /.SORT用ワーク./
    VAR       ?SORT     ,STRING*3,VALUE-'CHG'
    VAR       ?SORTF    ,STRING*1,VALUE-'S'
    VAR       ?CHGXXXS  ,NAME!MOD

           /.一括変更CSVデータ./
    VAR       ?CSVF     ,STRING*1,VALUE-'F'
    VAR       ?CHGXXXF  ,NAME!MOD

           /.ファイルＩＤ表示用./
    VAR       ?FILENAME ,STRING*17,VALUE-'                 '

    VAR       ?FILNM    ,STRING*8,VALUE-'        '
    VAR       ?LIBNM    ,STRING*8,VALUE-'TOKDTLIB'
    VAR       ?FILID    ,NAME
    VAR       ?LIBID    ,NAME

/.----------------------------------------------------------------./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM :=  '作場・数量一括変更用データ作成'

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
STEP000:
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?P4,?P7)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 := '倉庫コード取得'
              GOTO ABEND
    END

/.##ログインユーザー情報取得##./
SIT9000B:

    ?STEP :=   'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              GOTO ABEND
    END
    ?TANCD := %SBSTR(?TANCD8,1,2)

/.----------------------------------------------------------------./
/.##端末番号を取得##./
SBT0620B:

    ?STEP :=   'SBT0620B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SBT0620B.TOKELIBO,PARA-(?WS,?TANNO)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              GOTO ABEND END
    ?MSGX := '## 端末NO = ' && ?TANNO
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##端末別ファイルＩＤ取得##./
   /.一括変更データ出力ワーク./
    ?FILNM    :=    ?CHG && ?TANNO && ?CHGW
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?CHGXXXW  :=    %NCAT(?FILID,?LIBID)
    ?FILENAME :=    %STRING(?CHGXXXW)
    ?MSGX     :=    '##一括変更データ　　=' && ?FILENAME
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

   /.SORT用ワーク./
    ?FILNM    :=    ?SORT && ?TANNO && ?SORTF
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?CHGXXXS  :=    %NCAT(?FILID,?LIBID)
    ?FILENAME :=    %STRING(?CHGXXXS)
    ?MSGX     :=    '##ＳＯＲＴ用ワーク　=' && ?FILENAME
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

   /.一括変更CSVデータ./
    ?FILNM    :=    ?CHG  && ?TANNO && ?CSVF
    ?FILID    :=    %NAME(?FILNM)
    ?LIBID    :=    %NAME(?LIBNM)
    ?CHGXXXF  :=    %NCAT(?FILID,?LIBID)
    ?FILENAME :=    %STRING(?CHGXXXF)
    ?MSGX     :=    '##一括変更CSVデータ =' && ?FILENAME
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

FILECHK:

    ASSIGN FILE-?CHGXXXW!@XCL
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF     ?PGMEC  ^=    0    THEN
           ?KEKA4 := 'ファイル排他獲得１'
           SNDMSG 'ファイル排他獲得１',TOWS-@ORGWS
           GOTO   ERR
    END

    DLTFILE FILE-?CHGXXXS
    CPYFILE FILE-?CHGXXXW,TOFILE-?CHGXXXS,CRTFILE-@YES
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF     ?PGMEC  ^=    0    THEN
           ?KEKA4 := 'ファイル排他獲得２－１'
           SNDMSG 'ファイル排他獲得２－１',TOWS-@ORGWS
           GOTO   ERR
    END
    ASSIGN FILE-?CHGXXXS!@XCL
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF     ?PGMEC  ^=    0    THEN
           ?KEKA4 := 'ファイル排他獲得２－２'
           SNDMSG 'ファイル排他獲得２－２',TOWS-@ORGWS
           GOTO   ERR
    END

    ASSIGN FILE-?CHGXXXF!@XCL
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF     ?PGMEC  ^=    0    THEN
           ?KEKA4 := 'ファイル排他獲得３'
           SNDMSG 'ファイル排他獲得３',TOWS-@ORGWS
           GOTO   ERR
    END

/.----------------------------------------------------------------./
/.##作場・数量一括変更データ作成指示./
SSY5110I:

    ?STEP :=   'SSY5110I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    DEFLIBL   TOKELIB/TOKELIBO/TOKDTLIB/TOKDLIB/TOKFLIB/TOKKLIB

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKMDLIB

    CALL      PGM-SSY5110I.TOKSOLIB,PARA-(?BTYMD,?BTTIME,?BTTOKC,
                                          ?SAKUCD)

    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              GOTO   RTN
    END
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := '作場一括変更データ作成指示'
              GOTO   ABEND
    END

    ?MSGX :=  '受信日　＝'  && ?BTYMD
    SNDMSG    ?MSGX,TO-XCTL

    ?MSGX :=  '受信時刻　　＝'  && ?BTTIME
    SNDMSG    ?MSGX,TO-XCTL

    ?MSGX :=  '取引先　　　＝'  && ?BTTOKC
    SNDMSG    ?MSGX,TO-XCTL

    ?MSGX :=  '作場ＣＤ１　＝'  && %SBSTR(?SAKUCD,1,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ２　＝'  && %SBSTR(?SAKUCD,3,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ３　＝'  && %SBSTR(?SAKUCD,5,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ４　＝'  && %SBSTR(?SAKUCD,7,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ５　＝'  && %SBSTR(?SAKUCD,9,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ６　＝'  && %SBSTR(?SAKUCD,11,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ７　＝'  && %SBSTR(?SAKUCD,13,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ８　＝'  && %SBSTR(?SAKUCD,15,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ９　＝'  && %SBSTR(?SAKUCD,17,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１０＝'  && %SBSTR(?SAKUCD,19,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１１＝'  && %SBSTR(?SAKUCD,21,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１２＝'  && %SBSTR(?SAKUCD,23,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１３＝'  && %SBSTR(?SAKUCD,25,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１４＝'  && %SBSTR(?SAKUCD,27,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１５＝'  && %SBSTR(?SAKUCD,29,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１６＝'  && %SBSTR(?SAKUCD,31,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１７＝'  && %SBSTR(?SAKUCD,33,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１８＝'  && %SBSTR(?SAKUCD,35,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ１９＝'  && %SBSTR(?SAKUCD,37,2)
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '作場ＣＤ２０＝'  && %SBSTR(?SAKUCD,39,2)
    SNDMSG    ?MSGX,TO-XCTL

/.##作場・数量一括変更用データ抽出##./
SSY5120B:

    ?STEP :=   'SSY5120B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    OVRF FILE-CHGXXXW,TOFILE-?CHGXXXW
    CALL      PGM-SSY5120B.TOKSOLIB,PARA-(?BTYMD,?BTTIME,?BTTOKC,
                                          ?SAKUCD)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := '作場一括変更用データ抽出'
              GOTO ABEND END

/.----------------------------------------------------------------./
/.##ＳＯＲＴ##./
SORT1:

    ?STEP :=  'SORT1   '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    SORTD     INFILE-?CHGXXXW,INRL-350,INBF-11,
              WORKFILE-?CHGXXXS,
              OUTFILE-?CHGXXXW,OUTBF-11,
              KEY-26!2!CA,    /.倉庫ＣＤ        ./
              KEY1-1!8!DA,    /.バッチ（受信日）./
              KEY2-9!4!DA,    /.バッチ（時刻）  ./
              KEY3-13!8!DA,   /.バッチ（取引先）./
              KEY4-40!8!DA,   /.納品日          ./
              KEY5-21!5!DA,   /.店舗ＣＤ        ./
              KEY6-287!9!DA,  /.伝票番号        ./
              KEY7-296!2!DA,  /.行番号          ./
              RCDL-@DSP

    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := 'データＳＯＲＴ'
              GOTO ABEND
    END

/.##作場・数量一括変更用CSVデータ作成##./
SSY5130V:

    ?STEP :=   'SSY5130V'
      ?MSGX :=  '***   '  && ?STEP   &&   '        ***'

    OVRF      FILE-CHGXXXW,TOFILE-?CHGXXXW
    OVRF      FILE-CHGXXXF,TOFILE-?CHGXXXF
    CALL      PGM-SSY5130V.TOKSOLIB
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := '作場一括変更ＣＳＶ作成'
              GOTO ABEND
    END

FIMPORT1:   /.作場一括変更ＣＳＶ転送./

    ?STEP :=   'FIMPORT1'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FIMPORT   FILE-?CHGXXXF,TYPE-@FILE,
              PARA-NFHACCSK,UNIT-7,OPR-@NO
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '作場一括変更ＣＳＶ転送'
              GOTO ABEND
    END

RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    RETURN    PGMEC-@PGMEC

ABEND:

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  '作場一括変更データ作成が異常しました。'
    ?KEKA2 :=  'ログ採取し，ＮＡＶへ連絡して下さい。'
    ?KEKA3 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

ERR:  /.資源の解放./

    ?OPR1  :=  '　　＃＃＃＃＃＃＃　資源使用中　＃＃＃＃＃＃＃　　'
    ?OPR2  :=  '　　現在、振分リストワークファイルを他で使用中　　'
    ?OPR3  :=  '　　です。少し時間をおいて再度、実行して下さい。　'
    ?OPR4  :=  ''
    ?OPR5  :=  '　　ＥＮＴＥＲ＝再実行，ＰＦ９＝プログラム終了　　'
    CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
    GOTO   FILECHK

```
