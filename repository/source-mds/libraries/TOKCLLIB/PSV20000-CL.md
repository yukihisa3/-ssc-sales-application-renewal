# PSV20000

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PSV20000.CL`

## ソースコード

```jcl
/. *********************************************************** ./
/. *   サカタのタネ　 　本社システム                         * ./
/. *   SYSTEM-NAME :    出荷サブシステム 　       　　　     * ./
/. *   JOB-ID      :    PSV20000                             * ./
/. *                    流用：PSV01000.TOKCLIBS              * ./
/. *   JOB-NAME    :    新．発注集計データＣＳＶ出力　　　   * ./
/. *   UPDATE      :    2022/05/12  INOUE                    * ./
/. *********************************************************** ./
    PGM (P1-?KBN)
/.###パラメタ定義####./
    PARA ?KBN     ,STRING*1,IN,VALUE-' ' /.1:ﾙｰﾄｱﾘ,,2:ﾙｰﾄﾅｼ./
/.###ﾜｰｸｴﾘｱ定義####./
    VAR ?PGMEC    ,INTEGER                      /.ｴﾗｰｽﾃｲﾀｽ./
    VAR ?PGMECX   ,STRING*11                    /.ｴﾗｰｽﾃｲﾀｽ変換./
    VAR ?PGMEM    ,STRING*99                    /.ｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ定義./
    VAR ?MSGX     ,STRING*99                    /.ﾒｯｾｰｼﾞ定義変換./
    VAR ?PGMID    ,STRING*8,VALUE-'PSV20000'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.ﾌﾟﾛｸﾞﾗﾑｽﾃｯﾌﾟ./
/.###PGﾊﾟﾗﾒﾀｴﾘｱ###./
    VAR ?BATNO    ,STRING*20,VALUE-'                    '
    VAR ?SOKOCD   ,STRING*4,VALUE-'    '
    VAR ?HIDUKE   ,STRING*8,VALUE-'        '
    VAR ?JIKAN    ,STRING*4,VALUE-'    '
    VAR ?TOKCD    ,STRING*8,VALUE-'        '
    VAR ?OUTKBN   ,STRING*1,VALUE-' '
    VAR ?CNTMEI   ,STRING*7,VALUE-'0000000'
    VAR ?CNTTEN   ,STRING*5,VALUE-'00000'
    VAR ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1    ./
    VAR ?KEKA1    ,STRING*40                    /.      2    ./
    VAR ?KEKA2    ,STRING*40                    /.      3    ./
    VAR ?KEKA3    ,STRING*40                    /.      4    ./
    VAR ?KEKA4    ,STRING*40                    /.      5    ./
    VAR ?OPR1     ,STRING*50                    /.ﾒｯｾｰｼﾞ1    ./
    VAR ?OPR2     ,STRING*50                    /.      2    ./
    VAR ?OPR3     ,STRING*50                    /.      3    ./
    VAR ?OPR4     ,STRING*50                    /.      4    ./
    VAR ?OPR5     ,STRING*50                    /.      5    ./
/.##実行PG名称ｾｯﾄ##./
    ?PGNM := '新．発注集計データＣＳＶ出力'
/.###ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ###./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL
/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL TOKELIB/TOKFLIB/TOKKLIB/TOKDTLIB/TOKSOLIB
/.##資源の獲得##./
ASS:
     ASSIGN FILE-NHTMEIF.TOKDTLIB!@XCL
     IF     @PGMEC ^=  0  THEN
       ?OPR1 := '　＃＃　発注集計表ＣＳＶ出力確認　使用状態　＃＃　'
       ?OPR2 := '　＃他の端末にて資源が使用されております。　　＃　'
       ?OPR3 := '　＃確認して下さい。（暫くお待ち下さい。）　　＃　'
       ?OPR4 := '　＃続行＝ＥＮＴＥＲ　終了＝ＰＦ９　　　　　　＃　'
       ?OPR5 := '　＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃　'
       CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
       GOTO   ASS     END

     ASSIGN FILE-NHTTENF.TOKDTLIB!@XCL
     IF     @PGMEC ^=  0  THEN
       ?OPR1 := '　＃＃　発注集計表ＣＳＶ出力確認　使用状態　＃＃　'
       ?OPR2 := '　＃他の端末にて資源が使用されております。　　＃　'
       ?OPR3 := '　＃確認して下さい。（暫くお待ち下さい。）　　＃　'
       ?OPR4 := '　＃続行＝ＥＮＴＥＲ　終了＝ＰＦ９　　　　　　＃　'
       ?OPR5 := '　＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃　'
       CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
       GOTO   ASS     END

     ASSIGN FILE-NHTMEICV.TOKDTLIB!@XCL
     IF     @PGMEC ^=  0  THEN
       ?OPR1 := '　＃＃　発注集計表ＣＳＶ出力確認　使用状態　＃＃　'
       ?OPR2 := '　＃他の端末にて資源が使用されております。　　＃　'
       ?OPR3 := '　＃確認して下さい。（暫くお待ち下さい。）　　＃　'
       ?OPR4 := '　＃続行＝ＥＮＴＥＲ　終了＝ＰＦ９　　　　　　＃　'
       ?OPR5 := '　＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃　'
       CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
       GOTO   ASS     END

     ASSIGN FILE-NHTTENCV.TOKDTLIB!@XCL
     IF     @PGMEC ^=  0  THEN
       ?OPR1 := '　＃＃　発注集計表ＣＳＶ出力確認　使用状態　＃＃　'
       ?OPR2 := '　＃他の端末にて資源が使用されております。　　＃　'
       ?OPR3 := '　＃確認して下さい。（暫くお待ち下さい。）　　＃　'
       ?OPR4 := '　＃続行＝ＥＮＴＥＲ　終了＝ＰＦ９　　　　　　＃　'
       ?OPR5 := '　＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃　'
       CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
       GOTO   ASS     END

/.###ファイルクリア###./
CLRFILE1:
    /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CLRFILE   NHTMEIF.TOKDTLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【ＣＬＲＦＩＬＥ１】'
              GOTO ABEND END

CLRFILE2:
    /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CLRFILE   NHTTENF.TOKDTLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【ＣＬＲＦＩＬＥ２】'
              GOTO ABEND END

/.###新発注集計データＣＳＶ出力指示###./
CSV2000I:
    /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-SHTDENLA,TOFILE-SHTDENLA.TOKFLIB
    OVRF      FILE-TOKMS2,TOFILE-TOKMS2.TOKFLIB
    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKMDLIB

    CALL      PGM-CSV2000I.TOKSOLIB,PARA-(?BATNO,?SOKOCD,
                                          ?HIDUKE,?JIKAN,?TOKCD,
                                          ?OUTKBN)
    ?PGMEC := @PGMEC
    IF        ?PGMEC    ^=   0    THEN
              IF ?PGMEC  =   4001 THEN
                  SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF

                  ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
                  SNDMSG    ?MSGX,TO-XCTL

                  RETURN    PGMEC-@PGMEC

              ELSE
                  ?KEKA4 :=  '【範囲指定入力】'
                  GOTO   ABEND
              END
    END

    ?MSGX :=  '##日付     = ' && ?HIDUKE
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '##時間     = ' && ?JIKAN
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '##取引先CD = ' && ?TOKCD
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '##倉庫ｺｰﾄﾞ = ' && ?SOKOCD
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '##出力区分 = ' && ?OUTKBN
    SNDMSG    ?MSGX,TO-XCTL

/.###新発注集計データ抽出###./
CSV2010B:
    /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-SHTDENLA,TOFILE-SHTDENLA.TOKFLIB
    OVRF      FILE-TENMS1,TOFILE-TENMS1.TOKFLIB
    OVRF      FILE-SHOTBL1,TOFILE-SHOTBL1.TOKFLIB
    OVRF      FILE-SUBTBLL1,TOFILE-SUBTBLL1.TOKDTLIB
    OVRF      FILE-NHTMEIL1,TOFILE-NHTMEIL1.TOKDTLIB
    OVRF      FILE-NHTTENL1,TOFILE-NHTTENL1.TOKDTLIB

    CALL      PGM-CSV2010B.TOKSOLIB,PARA-(?BATNO,?SOKOCD,?OUTKBN,
                                          ?CNTMEI,?CNTTEN,?KBN)
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【対象データ抽出】'
              GOTO ABEND END

    ?MSGX :=  '##明細件数 = ' && ?CNTMEI
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '##店舗数　 = ' && ?CNTTEN
    SNDMSG    ?MSGX,TO-XCTL

/.###新発注集計データ店舗順取得##./
CSV2020B:

    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-NHTMEIL1,TOFILE-NHTMEIL1.TOKDTLIB
    OVRF      FILE-NHTTENL1,TOFILE-NHTTENL1.TOKDTLIB
    CALL      PGM-CSV2020B.TOKSOLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【発注集計データ店舗順取得】'
              GOTO ABEND END

/.###新発注集計データＣＳＶ出力##./
CSV2030B:

    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-NHTMEIL1,TOFILE-NHTMEIL1.TOKDTLIB
    OVRF      FILE-NHTTENL1,TOFILE-NHTTENL1.TOKDTLIB
    OVRF      FILE-NHTMEICV,TOFILE-NHTMEICV.TOKDTLIB
    OVRF      FILE-NHTTENCV,TOFILE-NHTTENCV.TOKDTLIB
    CALL      PGM-CSV2030B.TOKSOLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【ＣＳＶ形式データ作成】'
              GOTO ABEND END

/.###ＰＣへデータ転送１##./
FIMPORT1:
    /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FIMPORT FILE-NHTMEICV.TOKDTLIB,PARA-NEWHACCV,UNIT-1
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【ＰＣへデータ転送１】'
              GOTO ABEND END

/.###ＰＣへデータ転送２##./
FIMPORT2:
    /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FIMPORT FILE-NHTTENCV.TOKDTLIB,PARA-NEWHACCV,UNIT-2
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【ＰＣへデータ転送２】'
              GOTO ABEND END

/.###ﾌﾟﾛｸﾞﾗﾑ終了###./
RTN:

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    ?KEKA1 :=  '処理が正常終了しました。'
    ?KEKA2 :=  'ＰＣ上で確認願います。'
    ?KEKA3 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:  /.ﾌﾟﾛｸﾞﾗﾑ異常終了時処理./

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    ?KEKA1 :=  '処理が異常終了しました。'
    ?KEKA2 :=  'ログリストを採取後、ＮＡＶへ連絡'
    ?KEKA3 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
