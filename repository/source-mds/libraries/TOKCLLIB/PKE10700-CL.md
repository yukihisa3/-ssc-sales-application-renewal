# PKE10700

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PKE10700.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　本社システム                          *  ./
/. *   SYSTEM-NAME :    検品システム　　                     *  ./
/. *   JOB-ID      :    PKE10700                             *  ./
/. *   JOB-NAME    :    出荷確定データ更新（カインズＴＣ１） *  ./
/. ***********************************************************  ./
    PGM
/.###ﾜｰｸｴﾘｱ定義####./
    VAR ?FILNM    ,STRING*8,VALUE-'        '    /.ﾌｧｲﾙ名合併用./
    VAR ?FILNMH   ,STRING*5,VALUE-'     '       /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMK   ,STRING*6,VALUE-'      '      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?SNDFIN   ,STRING*6,VALUE-'      '      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMH1  ,STRING*5,VALUE-'CZSUM'       /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?LIBNM    ,STRING*8,VALUE-'TOKKPLIB'    /.ﾗｲﾌﾞﾗﾘ名ﾃﾞﾌｫﾙﾄ./
    VAR ?KEY2     ,STRING*1,VALUE-'2'           /.KEY-NO./
    VAR ?KEY3     ,STRING*1,VALUE-'3'           /.KEY-NO./
    VAR ?FILID    ,NAME                         /.ﾌｧｲﾙ名名前型./
    VAR ?LIBID    ,NAME                         /.ﾗｲﾌﾞﾗﾘ名名前型./
    VAR ?CZSUMXX  ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZSUMXXN ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?CZSUMX2  ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZSUMX2N ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?CZSUMX3  ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZSUMX3N ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR ?PGMEC    ,INTEGER                      /.ﾌﾟﾛｸﾞﾗﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMECX   ,STRING*11                    /.ｼｽﾃﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMEM    ,STRING*99                    /.ｼｽﾃﾑｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ格納ﾃｰﾌﾞﾙ./
    VAR ?MSGX     ,STRING*99                    /.SNDMSG表示用./
    VAR ?PGMID    ,STRING*8,VALUE-'PKE10700'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.STEP-ID./
    VAR ?BUMON    ,STRING*4,VALUE-'    '        /.部門CD./
    VAR ?TANCD8   ,STRING*8,VALUE-'        '    /.担当者CD./
    VAR ?TANCD    ,STRING*2,VALUE-'  '          /.担当者CD./
    VAR ?JISOKO   ,STRING*2,VALUE-'00'          /.倉庫(自)  ./
    VAR ?DSOKO    ,STRING*2,VALUE-'00'          /.倉庫(代表)./
    VAR ?NO       ,STRING*1,VALUE-'2'           /.処理種別./
    VAR ?KENSU    ,STRING*7,VALUE-'0000000'     /.抽出件数./
    VAR ?DATKBN   ,STRING*1,VALUE-' '           /.取込対象区分./
    VAR ?HAKKBN   ,STRING*1,VALUE-' '           /.出荷実績発行区分./
    VAR ?OUTKBN   ,STRING*1,VALUE-' '           /.出力区分./
    VAR ?TDATE    ,STRING*8,VALUE-'00000000'    /.取込日付./
    VAR ?TTIME    ,STRING*6,VALUE-'000000'      /.取込時刻./
    VAR ?SYORINM1 ,STRING*50
    VAR ?SYORINM2 ,STRING*40
    VAR ?OPR1     ,STRING*50                 /.ﾒｯｾｰｼﾞ1    ./
    VAR ?OPR2     ,STRING*50                 /.      2    ./
    VAR ?OPR3     ,STRING*50                 /.      3    ./
    VAR ?OPR4     ,STRING*50                 /.      4    ./
    VAR ?OPR5     ,STRING*50                 /.      5    ./
    VAR ?PGNM     ,STRING*40                 /.ﾒｯｾｰｼﾞ1    ./
    VAR ?KEKA1    ,STRING*40                 /.      2    ./
    VAR ?KEKA2    ,STRING*40                 /.      3    ./
    VAR ?KEKA3    ,STRING*40                 /.      4    ./
    VAR ?KEKA4    ,STRING*40                 /.      5    ./

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM :=  '出荷確定データ更新（カインズＴＣ１）'

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL   TOKELIB/TOKFLIB/TOKKLIB/TOKDLIB/TOKDTLIB/TOKKPLIB
             /TOKELIBO

/.##ログインユーザー情報取得##./
STEP030:

    ?STEP :=   'STEP030'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '異常－ログインユーザー情報取得'
              GOTO ABEND
    END
    ?TANCD := ?TANCD8

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?JISOKO,?DSOKO)
    IF        @PGMEC    ^=   0   THEN
              ?KEKA4 :=  '異常－実行倉庫ＣＤ取得'
              GOTO ABEND
    END
    ?JISOKO := 'TK'

/.##ﾌｧｲﾙ名称取得##./
FILNMCHG:

    ?FILNMH := ?FILNMH1
    ?MSGX := '## 実行倉庫ｺｰﾄﾞ = ' && ?JISOKO && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM    :=    ?FILNMH && ?JISOKO    /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZSUMXX  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZSUMXXN :=    %STRING(?CZSUMXX)
    ?MSGX     :=    '## 倉庫ﾌｧｲﾙ名(PF) = ' && ?CZSUMXXN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM    :=    ?FILNMH && ?JISOKO  &&  ?KEY2
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZSUMX2  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZSUMX2N :=    %STRING(?CZSUMX2)
    ?MSGX     :=    '## 倉庫ﾌｧｲﾙ名(L2) = ' && ?CZSUMX2N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM    :=    ?FILNMH && ?JISOKO  &&  ?KEY3
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZSUMX3  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZSUMX3N :=    %STRING(?CZSUMX3)
    ?MSGX     :=    '## 倉庫ﾌｧｲﾙ名(L3) = ' && ?CZSUMX3N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##検品結果集計ファイル（カインズ）ロック##./
ASSIGN:

    ?STEP :=   'ASSIGN'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ASSIGN    FILE-?CZSUMXX!@XCL
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              SNDMSG '他で連携処理が行われています',TOWS-@ORGWS
              SNDMSG '検品結果集計ファイルロック',TOWS-@ORGWS
              ?KEKA4 :=  '異常－検品結果集計ファイルロック'
              GOTO ABEND
    END

/.##出荷検品確定データ取込指示##./
NKE0060I:

    ?STEP :=   'NKE0060I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKMDLIB
    OVRF      FILE-RCVSYKXX,TOFILE-?CZSUMXX
    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRF      FILE-TANMS1,TOFILE-TANMS1.TOKKLIB
    CALL      PGM-NKE0060I.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?DATKBN,?HAKKBN,?OUTKBN,
                                          ?TDATE,?TTIME)
  /.?TDATE    :=    '00000000'
    ?TTIME    :=    '0000'    ./

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
         IF   ?PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              RETURN    PGMEC-?PGMEC
         ELSE
              ?KEKA4 :=  '異常－出荷検品確定データ取込指示'
              GOTO ABEND
         END
    END
    ?MSGX :=  '## 指定パラメタ確認 ################'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '## 部門     = ' &&  ?BUMON
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '## 担当者   = ' &&  ?TANCD
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '## 実行倉庫 = ' &&  ?JISOKO
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '## ＤＴ区分 = ' &&  ?DATKBN
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '## 発行区分 = ' &&  ?HAKKBN
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '## 出力区分 = ' &&  ?OUTKBN
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '## 取込日付 = ' &&  ?TDATE
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '## 取込時刻 = ' &&  ?TTIME
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?MSGX :=  '####################################'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
/.##カインズ以外を指定の場合は異常終了させる##./
    IF  ?DATKBN  ^=  '2'  THEN
         ?MSGX :=  '## ABEND カインズ以外は実行できません！ ##'
         SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
         ?KEKA4 :=  '異常－カインズ以外は実行できません！'
         GOTO      ABEND
    END

/.##出荷検品確定データ更新##./
NKE1070B:

    ?STEP :=   'NKE1070B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    OVRF      FILE-CZSUML3,TOFILE-?CZSUMX3
    CALL      PGM-NKE1070B.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?TDATE,?TTIME,?KENSU)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        @PGMEC    ^=   0   THEN
              ?KEKA4 :=  '異常－出荷検品確定データ更新'
              GOTO ABEND
    END

/.##リスト出力先制御##./
CHGSPLQ:

    ?STEP :=  'CHGSPLQ '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    CASE      ?OUTKBN   OF
    /.レーザー./
    /.現地./
    /. #'1'#
                  SNDMSG '＃本社レーザーへ出力　　　＃',
                   TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
                  OVRPRTF FILE-XU04LP,TOFILE-????????.????????
              END  ./                  /.↑実機設定に応じ変更./
    /.社内./
       #'1'#  OVRPRTF FILE-XU04LP,TOFILE-XU04LP.XUCL

    /.連帳./                           /.↓実機はXU04LPでOK？./
       #' '#  OVRPRTF FILE-XU04LP,TOFILE-XU04LP.XUCL
    END

/.##出荷検品取込リスト発行##./
NKE1060L:

    ?STEP :=   'NKE1060L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    OVRF      FILE-CZSUMXX2,TOFILE-?CZSUMX2
    CALL      PGM-NKE1060L.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?HAKKBN,?TDATE,?TTIME)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '異常－出荷検品取込リスト発行'
              GOTO ABEND END

RTN:

    ?KEKA1 :=  '（カインズＴＣ）出荷確定データ更新正常'
    ?KEKA2 :=  '終了しました。出荷検品取込リストを発行'
    ?KEKA3 :=  'して内容を確認して下さい。'
    ?KEKA4 :=  ''
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB
                    ,PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-?PGMEC

ABEND:

    ?KEKA1 :=  '（カインズＴＣ）出荷確定データ更新異常'
    ?KEKA2 :=  '終了しました。エラー内容を確認しＮＡＶ'
    ?KEKA3 :=  'へ連絡してください。'
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=    '### ' && ?PGMID && ' ABEND' && ' ###'
    ?MSG(2)   :=    '### ' && ' PGMEC = ' &&
                     %SBSTR(?PGMECX,8,4) && ' ###'
    ?MSG(3)   :=    '###' && ' LINE = '  && %LAST(LINE)      && ' ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

```
