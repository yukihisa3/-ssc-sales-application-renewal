# PFU32200

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PFU32200.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *   株式会社サカタのタネ　ＨＧ部                          *  ./
/. *   SYSTEM-NAME :    ＡＣＯＳ振替改善                     *  ./
/. *   JOB-ID      :    PFU32200                             *  ./
/. *   JOB-NAME    :    振替更新データリスト発行　　　　　   *  ./
/. *   UPDATE      :                                         *  ./
/. *                                                         *  ./
/. ***********************************************************  ./
    PGM

    VAR       ?WS       ,STRING*8,VALUE-'        ' /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR       ?WKSTN    ,NAME!MOD                  /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR       ?PGMEC    ,INTEGER
    VAR       ?PGMES    ,STRING*5,VALUE-'     '
    VAR       ?PGMECX   ,STRING*11
    VAR       ?PGMEM    ,STRING*99
    VAR       ?MSG      ,STRING*99(6)
    VAR       ?MSGX     ,STRING*99
    VAR       ?PGMID    ,STRING*8,VALUE-'PFU32200'
    VAR       ?STEP     ,STRING*8
    VAR       ?BUMON    ,STRING*4,VALUE-'    '     /.部門./
    VAR       ?TANCD8   ,STRING*8,VALUE-'      '   /.部門+担当者./
    VAR       ?TANCD    ,STRING*2,VALUE-'  '       /.担当者./

    VAR       ?SOKO     ,STRING*2,VALUE-'  '       /.倉庫ＣＤ    ./
    VAR       ?DSOKO    ,STRING*2,VALUE-'  '       /.代表倉庫    ./

    VAR       ?JYUST    ,STRING*8,VALUE-'00000000' /.受信日　開始./
    VAR       ?JYUED    ,STRING*8,VALUE-'00000000' /.受信日　終了./
    VAR       ?HSOKST   ,STRING*2,VALUE-'  '       /.倉庫ＣＤ開始./
    VAR       ?HSOKED   ,STRING*2,VALUE-'  '       /.倉庫ＣＤ終了./
    VAR       ?SKDST    ,STRING*8,VALUE-'00000000' /.出荷日　開始./
    VAR       ?SKDED    ,STRING*8,VALUE-'00000000' /.出荷日　終了./
    VAR       ?SYOST    ,STRING*8,VALUE-'00000000' /.商品ＣＤ開始./
    VAR       ?SYOED    ,STRING*8,VALUE-'00000000' /.商品ＣＤ終了./
    VAR       ?HKKBN    ,STRING*1,VALUE-' '        /.発行区分　　./
    VAR       ?SRKBN    ,STRING*1,VALUE-' '        /.出力区分　　./
    VAR       ?DENK1    ,STRING*2,VALUE-'  '       /.伝票区分１　./
    VAR       ?DENK2    ,STRING*2,VALUE-'  '       /.伝票区分２　./
    VAR       ?DENK3    ,STRING*2,VALUE-'  '       /.伝票区分３　./
    VAR       ?DENK4    ,STRING*2,VALUE-'  '       /.伝票区分４　./
    VAR       ?DENK5    ,STRING*2,VALUE-'  '       /.伝票区分５　./
    VAR       ?DENK6    ,STRING*2,VALUE-'  '       /.伝票区分６　./
    VAR       ?DENK7    ,STRING*2,VALUE-'  '       /.伝票区分７　./
    VAR       ?DENK8    ,STRING*2,VALUE-'  '       /.伝票区分８　./
    VAR       ?OUTKBN   ,STRING*1,VALUE-' '        /.ＤＴ区分　　./

    VAR       ?OPR1     ,STRING*50                 /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?OPR2     ,STRING*50                 /.      2    ./
    VAR       ?OPR3     ,STRING*50                 /.      3    ./
    VAR       ?OPR4     ,STRING*50                 /.      4    ./
    VAR       ?OPR5     ,STRING*50                 /.      5    ./

/.-----------------------------------------------------------./
    VAR       ?MSG1   ,STRING*80                  /.開始終了MSG./
    VAR       ?PGNM   ,STRING*40                  /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?KEKA1  ,STRING*40                  /.      2    ./
    VAR       ?KEKA2  ,STRING*40                  /.      3    ./
    VAR       ?KEKA3  ,STRING*40                  /.      4    ./
    VAR       ?KEKA4  ,STRING*40                  /.      5    ./
/.-----------------------------------------------------------./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM :=  '振替更新データリスト出力'

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL LIBL-TOKELIB/TOKFLIB/TOKDTLIB/TOKDLIB/TOKELIBO/TOKKLIB/
            ZAIEXCEL/TOMDLIB

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
STEP000:
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKO,?DSOKO)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 := '倉庫コード取得'
              GOTO ABEND
    END

/.##ログインユーザー情報取得##./
SIT9000B:

    ?STEP :=   'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := 'ログインユーザー情報取得'
              GOTO ABEND
    END
    ?TANCD := %SBSTR(?TANCD8,1,2)

/.----------------------------------------------------------------./
/.##振替更新データリスト指示入力##./
SFU3210I:

    ?STEP :=   'SFU3210I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKMDLIB
    CALL      PGM-SFU3220I.TOKSOLIB,PARA-(?SOKO,?DSOKO,
                                          ?JYUST,?JYUED,
                                          ?HSOKST,?HSOKED,
                                          ?SKDST,?SKDED,
                                          ?SYOST,?SYOED,
                                          ?HKKBN,?SRKBN,
                                          ?DENK1,?DENK2,
                                          ?DENK3,?DENK4,
                                          ?DENK5,?DENK6,
                                          ?DENK7,?DENK8,
                                          ?OUTKBN)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              GOTO   RTN
    END
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := '振替更新データリスト　指示'
              GOTO   ABEND
    END

    ?MSGX :=  '受信日　範囲＝'  && ?JYUST  && ' - ' && ?JYUED
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '倉庫ＣＤ範囲＝'  && ?HSOKST && ' - ' && ?HSOKED
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '出荷日　範囲＝'  && ?SKDST  && ' - ' && ?SKDED
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  'サカタ商範囲＝'  && ?SYOST  && ' - ' && ?SYOED
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '発行区分　　＝'  && ?HKKBN
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '出力区分　　＝'  && ?SRKBN
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '伝票区分　　＝'  && ?DENK1 && ':' && ?DENK2 && ':' &&
                                   ?DENK3 && ':' && ?DENK4 && ':' &&
                                   ?DENK5 && ':' && ?DENK6 && ':' &&
                                   ?DENK7 && ':' && ?DENK8 && ':' &&
                                   ?OUTKBN
    SNDMSG    ?MSGX,TO-XCTL

    IF  ?HKKBN = '1'  THEN
        GOTO          FILECHK
    END

/.----------------------------------------------------------------./
/.##振替更新データリスト発行_出力##./
SFU3230L:

    ?STEP :=   'SFU3230L'
      ?MSGX :=  '***   '  && ?STEP   &&   '        ***'

    CASE      ?SRKBN    OF
    /.レーザー./                                 /.↓社内にない./
    /.現地./   #' '#  OVRPRTF FILE-XU04LP,TOFILE-KAHMAPRT.XUCL
    /.社内./ /.#' '#  OVRPRTF FILE-XU04LP,TOFILE-TESTPRT.XUCL ./
                      ?MSGX :=  '#レーザー出力#'
                      SNDMSG    ?MSGX,TO-XCTL

    /.連帳./
       #'1'#  OVRPRTF FILE-XU04LP,TOFILE-XU04LP.XUCL
                      ?MSGX :=  '#  連帳出力  #'
                      SNDMSG    ?MSGX,TO-XCTL
    END
  /.↑2016/08/22./

    IF  ?OUTKBN = '2'  THEN
         OVRF FILE-FRACOSL2,TOFILE-FRACOEL2.TOKDTLIB
    ELSE
         OVRF FILE-FRACOSL2,TOFILE-FRACOSL2.TOKDTLIB
    END
    CALL      PGM-SFU3230L.TOKSOLIB,PARA-(?JYUST,?JYUED,
                                          ?HSOKST,?HSOKED,
                                          ?SKDST,?SKDED,
                                          ?SYOST,?SYOED,
                                          ?DENK1,?DENK2,
                                          ?DENK3,?DENK4,
                                          ?DENK5,?DENK6,
                                          ?DENK7,?DENK8,
                                          ?OUTKBN)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := '振替更新データリスト発行'
              GOTO ABEND
    END
    GOTO      RTN

/.----------------------------------------------------------------./
/.##ファイル排他##./
FILECHK:

    ?STEP :=   'FILECHK'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ASSIGN FILE-FURIKLST.TOKDTLIB!@XCL
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF    ?PGMEC  ^=    0    THEN
          ?KEKA4 := 'ファイル排他獲得'
           GOTO   ERR
    END

/.----------------------------------------------------------------./
/.##振替更新データリスト_ＣＳＶ作成##./
SFU3240V:

    ?STEP :=   'SFU3240V'
      ?MSGX :=  '***   '  && ?STEP   &&   '        ***'

    IF  ?OUTKBN = '2'  THEN
         OVRF FILE-FRACOSL2,TOFILE-FRACOEL2.TOKDTLIB
    ELSE
         OVRF FILE-FRACOSL2,TOFILE-FRACOSL2.TOKDTLIB
    END
    CALL      PGM-SFU3240V.TOKSOLIB,PARA-(?JYUST,?JYUED,
                                          ?HSOKST,?HSOKED,
                                          ?SKDST,?SKDED,
                                          ?SYOST,?SYOED,
                                          ?DENK1,?DENK2,
                                          ?DENK3,?DENK4,
                                          ?DENK5,?DENK6,
                                          ?DENK7,?DENK8,
                                          ?OUTKBN)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := '振替更新データリストＣＳＶ作成'
              GOTO ABEND
    END

FIMPORT1:   /.振替更新データリストＣＳＶ転送./

    ?STEP :=   'FIMPORT1'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FIMPORT   FILE-FURIKLST.TOKDTLIB,TYPE-@FILE,
              PARA-FURIKLST,UNIT-1,OPR-@NO
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '振替更新データリストＣＳＶ転送'
              GOTO ABEND
    END

RTN:

    RELEASE FILE-FURIKLST.TOKDTLIB!@XCL

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    RETURN    PGMEC-?PGMEC

ABEND:

    RELEASE FILE-FURIKLST.TOKDTLIB!@XCL

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  '振替更新データリストが異常しました。'
    ?KEKA2 :=  'ログ採取し，ＮＡＶへ連絡して下さい。'
    ?KEKA3 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

ERR:  /.資源の解放./

    ?OPR1  :=  '　　＃＃＃＃＃＃＃　資源使用中　＃＃＃＃＃＃＃　　'
    ?OPR2  :=  '　　現在、振替更新データＣＳＶＦを他で使用中　　　'
    ?OPR3  :=  '　　です。少し時間をおいて再度、実行して下さい。　'
    ?OPR4  :=  ''
    ?OPR5  :=  '　　ＥＮＴＥＲ＝再実行，ＰＦ９＝プログラム終了　　'
    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL      OHOM0900.TOKELIB,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
    GOTO   FILECHK

```
