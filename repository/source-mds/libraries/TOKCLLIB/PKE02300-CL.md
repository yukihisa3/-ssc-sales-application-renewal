# PKE02300

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PKE02300.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　本社システム                          *  ./
/. *   SYSTEM-NAME :    検品システム　　                     *  ./
/. *   JOB-ID      :    PKE02300                             *  ./
/. *   JOB-NAME    :    入荷検品データ取込　　　　　         *  ./
/. ***********************************************************  ./
    PGM
/.###ﾜｰｸｴﾘｱ定義####./
    VAR ?FILNM    ,STRING*8,VALUE-'        '    /.ﾌｧｲﾙ名合併用./
    VAR ?FILNMNK  ,STRING*6,VALUE-'RCVNYK'      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?LIBNM    ,STRING*8,VALUE-'TOKKPLIB'    /.ﾗｲﾌﾞﾗﾘ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILID    ,NAME                         /.ﾌｧｲﾙ名名前型./
    VAR ?LIBID    ,NAME                         /.ﾗｲﾌﾞﾗﾘ名名前型./
    VAR ?RCVNYK   ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?RCVNYKN  ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR ?PGMEC    ,INTEGER                      /.ﾌﾟﾛｸﾞﾗﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMECX   ,STRING*11                    /.ｼｽﾃﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMEM    ,STRING*99                    /.ｼｽﾃﾑｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ格納ﾃｰﾌﾞﾙ./
    VAR ?MSGX     ,STRING*99                    /.SNDMSG表示用./
    VAR ?PGMID    ,STRING*8,VALUE-'PKE02300'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.STEP-ID./
    VAR ?BUMON    ,STRING*4,VALUE-'    '        /.部門CD./
    VAR ?TANCD8   ,STRING*8,VALUE-'        '    /.担当者CD./
    VAR ?TANCD    ,STRING*2,VALUE-'  '          /.担当者CD./
    VAR ?JISOKO   ,STRING*2,VALUE-'00'          /.倉庫(自)  ./
    VAR ?DSOKO    ,STRING*2,VALUE-'00'          /.倉庫(代表)./
    VAR ?NO       ,STRING*1,VALUE-'4'           /.処理種別./
    VAR ?KENSU    ,STRING*7,VALUE-'0000000'     /.抽出件数./
    VAR ?HAKKBN   ,STRING*1,VALUE-' '           /.発注区分./
    VAR ?PRTKBN   ,STRING*1,VALUE-' '           /.ﾌﾟﾘﾝﾄ区分./
    VAR ?JISKBN   ,STRING*1,VALUE-' '           /.入荷実績区分./
    VAR ?TDATE    ,STRING*8,VALUE-'00000000'    /.取込日付./
    VAR ?TTIME    ,STRING*6,VALUE-'000000'      /.取込時刻./
    VAR ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1    ./
    VAR ?KEKA1    ,STRING*40                    /.      2    ./
    VAR ?KEKA2    ,STRING*40                    /.      3    ./
    VAR ?KEKA3    ,STRING*40                    /.      4    ./
    VAR ?KEKA4    ,STRING*40                    /.      5    ./

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM :=  '（検品）入荷検品データ取込'

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL   TOKELIB/TOKFLIB/TOKKPLIB/TOKKLIB/TOKDLIB/TOKDTLIB

/.##ログインユーザー情報取得##./
SIT9000B:

    ?STEP :=   'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【ログインユーザー情報取得】'
              GOTO ABEND
    END
    ?TANCD := ?TANCD8

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?JISOKO,?DSOKO)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 :=  '【倉庫コード取得】'
              GOTO ABEND
    END

/.##ﾌｧｲﾙ名称取得##./
FILNMCHG:

    ?MSGX    :=   '## 実行倉庫ｺｰﾄﾞ = ' && ?JISOKO && ' ##'
    SNDMSG   MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM   :=   ?FILNMNK && ?JISOKO   /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID   :=   %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID   :=   %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?RCVNYK  :=   %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?RCVNYKN :=   %STRING(?RCVNYK)
    ?MSGX    :=   '## 倉庫ﾌｧｲﾙ名 = ' && ?RCVNYKN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##資源獲得##./
PASSIGN:

    ?STEP :=   'PASSIGN'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ASSIGN FILE-?RCVNYK!@XCL/WHCHEDF.TOKKPLIB!@XCL
               /WHCMEIF.TOKKPLIB!@XCL
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 :=  '【資源獲得】'
              GOTO ABEND
    END

/.##ファイル初期化##./
PCLRFILE:

    ?STEP :=   'PCLRFILE'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CLRFILE FILE-?RCVNYK
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 :=  '【初期化：倉庫入荷検品データ】'
              GOTO ABEND
    END

    CLRFILE FILE-WHCHEDF.TOKKPLIB
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 :=  '【初期化：ＷＫ発注ヘッダＦ】'
              GOTO ABEND
    END

    CLRFILE FILE-WHCMEIF.TOKKPLIB
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 :=  '【初期化：ＷＫ発注明細Ｆ】'
              GOTO ABEND
    END

/.##入荷検品データ取込##./
PFEXPORT:

    ?STEP :=   'PFEXPORT'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FEXPORT FILE-?RCVNYK,MODE-@ADD,PARA-KENPIN,UNIT-8
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0   THEN
              ?KEKA4 :=  '【入荷検品データ取込】'
              GOTO ABEND
    END

/.##入荷検品データ取込指示入力##./
NKE0230I:

    ?STEP :=   'NKE0230I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF FILE-RCVNYKXX,TOFILE-?RCVNYK
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKMDLIB
    CALL PGM-NKE0230I.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,?JISKBN,
                                     ?PRTKBN,?TDATE,?TTIME,?KENSU)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
         IF   ?PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              GOTO RTN
         ELSE
              ?KEKA4 :=  '【入荷検品データ取込指示入力】'
              GOTO ABEND
         END
    END

    ?MSGX  :=  '?BUMON   =' && ?BUMON
     SNDMSG ?MSGX,TO-XCTL
    ?MSGX  :=  '?TANCD   =' && ?TANCD
     SNDMSG ?MSGX,TO-XCTL
    ?MSGX  :=  '?JISOKO  =' && ?JISOKO
     SNDMSG ?MSGX,TO-XCTL
    ?MSGX  :=  '?HAKKBN  =' && ?HAKKBN
     SNDMSG ?MSGX,TO-XCTL
    ?MSGX  :=  '?PRTKBN  =' && ?PRTKBN
     SNDMSG ?MSGX,TO-XCTL
    ?MSGX  :=  '?TDATE   =' && ?TDATE
     SNDMSG ?MSGX,TO-XCTL
    ?MSGX  :=  '?TTIME   =' && ?TTIME
     SNDMSG ?MSGX,TO-XCTL
    ?MSGX  :=  '?KENSU   =' && ?KENSU
     SNDMSG ?MSGX,TO-XCTL

/.##入荷検品データ取込１##./
NKE0240B:

    ?STEP :=   'NKE0240B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL      PGM-NKE0240B.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【入荷検品データ取込１】'
              GOTO ABEND END

/.##入荷検品データ取込２##./
NKE0241B:

    ?STEP :=   'NKE0241B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL      PGM-NKE0241B.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?KENSU,?TDATE,?TTIME)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【入荷検品データ取込２】'
              GOTO ABEND END

/.##入荷検品データ取込３##./
NKE0242B:

    ?STEP :=   'NKE0242B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL      PGM-NKE0242B.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?KENSU,?TDATE,?TTIME)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【入荷検品データ取込３】'
              GOTO ABEND END

/.##入荷検品データ取込（社内発注）##./
NKE0250B:

    ?STEP :=   'NKE0250B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL      PGM-NKE0250B.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?TDATE,?TTIME)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【入荷検品データ取込（社内発注）】'
              GOTO ABEND END

/.##（入荷確定）連携件数リスト##./
NKE0900L:

    ?STEP :=   'NKE0900L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL      PGM-NKE0900L.TOKSOLIB,PARA-(?BUMON,?TANCD,?NO,?KENSU)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【（入荷確定）連携件数リスト】'
              GOTO ABEND END

/.##倉庫入荷検品リスト発行##./
NKE0260L:

    ?STEP :=   'NKE0260L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL      PGM-NKE0260L.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,'1',
                                          ?TDATE,?TTIME)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【倉庫入荷検品リスト発行】'
              GOTO ABEND END

RTN:

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    ?KEKA1 :=  '入荷検品データ取込が正常終了しました'
    ?KEKA2 :=  '本社発注担当者様に取り込んだ旨、連絡'
    ?KEKA3 :=  'をお願い致します。'
    ?KEKA4 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-?PGMEC

ABEND:

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    ?KEKA1 :=  '入荷検品データ取込が異常終了しました'
    ?KEKA2 :=  'ログを採取してＮＡＶへ連絡した下さい'
    ?KEKA3 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=    '### ' && ?PGMID && ' ABEND' && ' ###'
    ?MSG(2)   :=    '### ' && ' PGMEC = ' &&
                     %SBSTR(?PGMECX,8,4) && ' ###'
    ?MSG(3)   :=    '###' && ' LINE = '  && %LAST(LINE)      && ' ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

```
