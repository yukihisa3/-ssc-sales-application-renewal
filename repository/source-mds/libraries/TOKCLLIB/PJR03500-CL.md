# PJR03500

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PJR03500.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　特販システム（本社システム）          *  ./
/. *   SYSTEM-NAME :    受領返品取機能構築　　　　　　　　　 *  ./
/. *   JOB-ID      :    PJR03500                             *  ./
/. *   JOB-NAME    :    返品計上確認リスト（通常／伝票纏め） *  ./
/. ***********************************************************  ./
    PGM  (P1-?JIKKBN)
/.### ﾊﾟﾗﾒﾀ定義  ###./
    PARA ?JIKKBN  ,STRING*1,IN,VALUE-' '
/.### ﾜｰｸｴﾘｱ定義 ###./
    VAR ?PGMEC    ,INTEGER                      /.ｴﾗｰｽﾃｲﾀｽ./
    VAR ?PGMECX   ,STRING*11                    /.ｴﾗｰｽﾃｲﾀｽ変換./
    VAR ?PGMEM    ,STRING*99                    /.ｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ定義./
    VAR ?MSGX     ,STRING*99                    /.ﾒｯｾｰｼﾞ定義変換./
    VAR ?PGMID    ,STRING*8,VALUE-'PJR03500'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.ﾌﾟﾛｸﾞﾗﾑｽﾃｯﾌﾟ./
    VAR ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1./
    VAR ?KEKA1    ,STRING*40                    /.      2./
    VAR ?KEKA2    ,STRING*40                    /.      3./
    VAR ?KEKA3    ,STRING*40                    /.      4./
    VAR ?KEKA4    ,STRING*40                    /.      5./
    VAR ?BUMON    ,STRING*4,VALUE-'    '        /.部門名./
    VAR ?TANCD    ,STRING*2,VALUE-'  '          /.担当者CD./
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
/. 共通パラメタ./
    VAR ?KEIKBN   ,STRING*1,VALUE-' '          /. 計上区分　　　　 ./
    VAR ?TOKCD    ,STRING*8,VALUE-'        '   /. 取引先ＣＤ　　　 ./
    VAR ?TANST    ,STRING*2,VALUE-'  '         /. 担当者開始　　　 ./
    VAR ?TANED    ,STRING*2,VALUE-'  '         /. 担当者終了　　　 ./
    VAR ?DKBN     ,STRING*1,VALUE-' '          /. 日付区分　　　　 ./
    VAR ?AFROM    ,STRING*8,VALUE-'        '   /. 検収日開始　　　 ./
    VAR ?ATO      ,STRING*8,VALUE-'        '   /. 検収日終了　　　 ./
    VAR ?NFROM    ,STRING*8,VALUE-'        '   /. 入力日開始　　　 ./
    VAR ?NTO      ,STRING*8,VALUE-'        '   /. 入力日終了　　　 ./
    VAR ?KFROM    ,STRING*8,VALUE-'        '   /. 計上日開始　　　 ./
    VAR ?KTO      ,STRING*8,VALUE-'        '   /. 計上日終了　　　 ./
    VAR ?SFROM    ,STRING*8,VALUE-'        '   /. 作成日開始　　　 ./
    VAR ?STO      ,STRING*8,VALUE-'        '   /. 作成日終了　　　 ./
    VAR ?HAKKBN   ,STRING*1,VALUE-' '          /. 発行区分　　　　 ./
    VAR ?SYUKBN   ,STRING*1,VALUE-' '          /. 出力区分　　　　 ./

/.### ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ ###./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

/.## ﾗｲﾌﾞﾗﾘﾘｽﾄ登録 ##./
    DEFLIBL TOKELIB/TOKFLIB/TOKDTLIB/TOKELIBO/TOKMDLIB/TOKKLIB/
            TOKDLIB

/.## ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ ##./
    ?PGNM :=  '返品計上確認リスト（通常／伝票纏め）'

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得 ##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

/.## ログインユーザー情報取得 ##./
SIT9000B:

    ?STEP :=   'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ログイン情報取得'
              GOTO ABEND
    END

/.## 返品計上確認リスト発行指示 ##./
SJR0350I:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    CALL PGM-SJR0350I.TOKSOLIB,PARA-(?BUMON,?TANCD,?JIKKBN,?KEIKBN,
         ?TOKCD,?TANST,?TANED,?DKBN,?AFROM,?ATO,?NFROM,?NTO,?KFROM,
         ?KTO,?SFROM,?STO,?HAKKBN,?SYUKBN)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF  ?PGMEC ^= 0  THEN
        IF ?PGMEC = 4010  THEN
           SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
           GOTO  RTN
        ELSE
           ?KEKA4 := '返品計上確認リスト発行指示'
           GOTO  ABEND
        END
    END

/.## 返品計上確認データ抽出 ##./
SJR0360B:

    ?STEP :=   'SJR0360B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    IF  ?JIKKBN = '1'  THEN
        OVRF FILE-COMRHEL4,TOFILE-COMRMHL4.TOKDTLIB
        OVRF FILE-COMRHEL5,TOFILE-COMRMHL5.TOKDTLIB
        OVRF FILE-COMRHEL8,TOFILE-COMRMHL8.TOKDTLIB
        OVRF FILE-COMRHEL9,TOFILE-COMRMHL9.TOKDTLIB
    END

    CALL PGM-SJR0360B.TOKSOLIB,PARA-(?KEIKBN,?TOKCD,?TANST,?TANED,
       ?DKBN,?AFROM,?ATO,?NFROM,?NTO,?KFROM,?KTO,?SFROM,?STO)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
        IF ?PGMEC = 4001  THEN
           GOTO  DATANG
        ELSE
           ?KEKA4 := '返品計上確認データ抽出'
           GOTO  ABEND
        END
    END

/.## 処理振り分け ##./
FURIWAKE:

    IF  ?HAKKBN = '1'  THEN
         GOTO  SJR0370V
    ELSE
         GOTO  SJR0310L
    END

/.## 返品計上確認リスト発行 ##./
SJR0310L:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    /. プリンタ出力先制御　空白：レーザー　１：連帳 ./
    IF    ?SYUKBN  =    '1'   THEN
          OVRPRTF FILE-XU04LP,TOFILE-XU04LP.XUCL
    ELSE
          OVRPRTF FILE-XU04LP,TOFILE-KAHMAPRT.XUCL
    END

    CALL PGM-SJR0310L.TOKSOLIB,PARA-(?JIKKBN,?KEIKBN,?TOKCD,?TANST,
             ?TANED,?DKBN,?AFROM,?ATO,?NFROM,?NTO,?KFROM,?KTO,
             ?SFROM,?STO)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF  ?PGMEC ^= 0  THEN
        ?KEKA4 := '返品計上確認リスト発行'
        GOTO  ABEND
    ELSE
        GOTO  RTN
    END

/.## 返品計上確認データＣＳＶ ##./
SJR0370V:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    CALL PGM-SJR0370V.TOKSOLIB,PARA-(?JIKKBN,?KEIKBN,?TOKCD,?TANST,
             ?TANED,?DKBN,?AFROM,?ATO,?NFROM,?NTO,?KFROM,?KTO,
             ?SFROM,?STO)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF  ?PGMEC ^= 0  THEN
        ?KEKA4 := '返品計上確認データＣＳＶ'
        GOTO  ABEND
    END

/.## 返品計上確認データＣＳＶ出力 ##./
FIMPORT:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    FIMPORT FILE-JYRHPCSV.TOKDLIB,PARA-MATOME,UNIT-2
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF  ?PGMEC ^= 0  THEN
           ?KEKA4 := '返品計上データＣＳＶ出力'
           GOTO  ABEND
    ELSE
        GOTO  RTN
    END


/.### ﾌﾟﾛｸﾞﾗﾑ終了 ###./
RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    RETURN    PGMEC-?PGMEC

ABEND:  /. ﾌﾟﾛｸﾞﾗﾑ異常終了時処理 ./
              /.１２３４５６７８９０１２３４５６７８９０./
    ?KEKA1 :=  '処理が異常終了しました。'
    ?KEKA2 :=  'ログリスト等を採取しＮＡＶへ連絡して下'
    ?KEKA3 :=  'さい。'
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO ?MSGX :=   ?MSG(?I)
           SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    END

    RETURN    PGMEC-?PGMEC

DATANG: /. 出力対象が存在しない場合 ./
              /.１２３４５６７８９０１２３４５６７８９０./
    ?KEKA1 :=  '指定範囲内の出力対象データがありません。'
    ?KEKA2 :=  '再度、出力条件を入力して下さい。'
    ?KEKA3 :=  ''
    ?KEKA4 :=  '！！返品計上確認データ抽出！！'
    CALL SMG0030I.TOKELIB
                    ,PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    RETURN    PGMEC-?PGMEC

```
