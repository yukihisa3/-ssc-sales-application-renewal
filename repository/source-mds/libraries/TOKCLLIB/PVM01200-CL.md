# PVM01200

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PVM01200.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *  （株）サカタのタネ                                     *  ./
/. *   SYSTEM-NAME :    ＨＧ基幹　　　　　　　　　　　       *  ./
/. *   JOB-NAME    :   （ＥＸＣＥＬ取込）　　　　　　　　　　*  ./
/. *                    商品変換テーブル取込チェック・更新   *  ./
/. *                    ※Ｄ３６５連携対応　　　　　　　　   *  ./
/. *   JOB-ID      :    PVM01200  2020/03/27                 *  ./
/. *   UPDATE      :    2024/02/16 NAV TAKAHASHI             *  ./
/. *                    たねまる情報追加                     *  ./
/. ***********************************************************  ./

    VAR  ?WS      ,STRING*8,VALUE-'        ' /.ﾜｰｸｽﾃｰｼｮﾝID./
    VAR  ?WKSTN   ,NAME!MOD                  /.ﾜｰｸｽﾃｰｼｮﾝID./
    VAR  ?PGMEC   ,INTEGER
    VAR  ?PGMES   ,STRING*5,VALUE-'     '
    VAR  ?PGMECX  ,STRING*11
    VAR  ?PGMEM   ,STRING*99
    VAR  ?MSG     ,STRING*99(6)
    VAR  ?MSGX    ,STRING*99
    VAR  ?PGMID   ,STRING*8,VALUE-'PVM01200'
    VAR  ?STEP    ,STRING*8
/.--------------------------------------------------------------./
    VAR  ?BUMON   ,STRING*4,VALUE-'    '     /.部門./
    VAR  ?TANCD8  ,STRING*8,VALUE-'        ' /.部門+担当者./
    VAR  ?TANCD   ,STRING*2,VALUE-'  '       /.担当者     ./
    VAR  ?SOKCD   ,STRING*2,VALUE-'00'       /.実行倉庫   ./
    VAR  ?DSOKCD  ,STRING*2,VALUE-'00'       /.代表倉庫   ./
/.--------------------------------------------------------------./
    VAR  ?CNT1    ,STRING*7,VALUE-'0000000'  /.件数(取込) ./
    VAR  ?CNT2    ,STRING*7,VALUE-'0000000'  /.件数(ＥＲ）./
/.--------------------------------------------------------------./
    VAR  ?OUTKBN  ,STRING*1,VALUE-' '        /.出力種別   ./
/.--------------------------------------------------------------./
    VAR  ?OPR1    ,STRING*50                 /.ﾒｯｾｰｼﾞ1    ./
    VAR  ?OPR2    ,STRING*50                 /.      2    ./
    VAR  ?OPR3    ,STRING*50                 /.      3    ./
    VAR  ?OPR4    ,STRING*50                 /.      4    ./
    VAR  ?OPR5    ,STRING*50                 /.      5    ./
/.--------------------------------------------------------------./
    VAR  ?MSG1    ,STRING*80                 /.開始終了MSG./
    VAR  ?PGNM    ,STRING*40                 /.ﾒｯｾｰｼﾞ1    ./
    VAR  ?KEKA1   ,STRING*40                 /.      2    ./
    VAR  ?KEKA2   ,STRING*40                 /.      3    ./
    VAR  ?KEKA3   ,STRING*40                 /.      4    ./
    VAR  ?KEKA4   ,STRING*40                 /.      5    ./
/.---------------------------------------------------------------./

  /.##端末別ファイルＩＤ編集用##./   /.当機能では非使用./

    /.共通_ライブラリ名./
    VAR  ?LIBNM    ,STRING*8,VALUE-'HGWORKLB'
    /.共通_ファイルＩＤワーク./
    VAR  ?FILID    ,NAME
    /.共通_ＬＩＢＩＤワーク./
    VAR  ?LIBID    ,NAME
    /.共通_端末番号./
    VAR  ?TANNO    ,STRING*03,VALUE-'   '

    /.データ_共通部_ファイルＩＤ先頭./
    VAR  ?STB      ,STRING*3,VALUE-'STB'
    /.データ_個別部_ファイルＩＤ末尾./
    VAR  ?CS       ,STRING*2,VALUE-'CS'
    VAR  ?WK       ,STRING*2,VALUE-'WK'
    VAR  ?W1       ,STRING*2,VALUE-'W1'
    VAR  ?W2       ,STRING*2,VALUE-'W2'
    VAR  ?W3       ,STRING*2,VALUE-'W3'

    /.データ_OVRF_ファイルＩＤ./
    VAR  ?STBXXXCS ,NAME!MOD
    VAR  ?STBXXXWK ,NAME!MOD
    VAR  ?STBXXXW1 ,NAME!MOD
    VAR  ?STBXXXW2 ,NAME!MOD
    VAR  ?STBXXXW3 ,NAME!MOD

    /.ファイルＩＤ表示用ワーク./
    VAR  ?DSPFILID,STRING*17
    VAR  ?FILNM7  ,STRING*7,VALUE-'       '
    VAR  ?FILNM8  ,STRING*8,VALUE-'        '

/.-----------------------------------------------------------./


/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL   TOKELIB/TOKELIBO/TOKSOLIB/TOKDLIB/TOKFLIB/TOKKLIB
             /HGWORKLB/TOKDTLIB/D365DLIB

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
CLSTART:
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM := '商品変換テーブル取込チェック＆更新'

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

/.##ログインユーザー情報取得##./
SIT9000B:

    ?STEP :=  'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMES    :=    @PGMES
    IF        ?PGMEC ^=   0    THEN
              GOTO   ABEND
    END
    ?TANCD := %SBSTR(?TANCD8,1,2)

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKCD,?DSOKCD)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMES    :=    @PGMES
    IF        ?PGMEC ^=   0   THEN
              ?KEKA4 := '倉庫コード取得'
              GOTO   ABEND
    END

/.##使用ファイルロック・クリア##./
ASSIGN:

    ?STEP :=  'ASSIGN'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    ASSIGN FILE-SBTMNTCS.HGWORKLB!@XCL
    ?PGMEC :=    @PGMEC
    ?PGMEM :=    @PGMEM
    ?PGMES :=    @PGMES
    IF     ?PGMEC ^=   1020 THEN
       IF  ?PGMEC ^=   0    THEN
           SNDMSG '連携処理が行えません',
                   TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
           ?KEKA4 :=  'ＣＳＶデータロック'
           GOTO   ABEND1
       END
    END

    ASSIGN FILE-STBMNTWK.HGWORKLB!@XCL
    ?PGMEC :=    @PGMEC
    ?PGMEM :=    @PGMEM
    ?PGMES :=    @PGMES
    IF     ?PGMEC ^=   0    THEN
           SNDMSG '連携処理が行えません',
                   TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
           ?KEKA4 :=  '取込ファイルロック'
           GOTO   ABEND1
    END
    CLRFILE FILE-STBMNTWK.HGWORKLB

/.##取込確認画面##./
SHOM0900:

    ?STEP :=  'SHOM0900'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    ?OPR1 := '　＜　商品変換テーブル　取込・更新　＞'
    ?OPR2 := '　　　　！！確認して下さい！！'
    ?OPR3 := '　 ＥＸＣＥＬデータの取込＆更新を行います'
    ?OPR4 := '　　　・連携するデータは出力済ですか？'
    ?OPR5 := '　　　・所定フォルダにセット済ですか？'

    CALL   OHOM0900.TOKELIB,
           PARA-(?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC ^= 0    THEN
              ?KEKA4 := '処理確認画面'
              GOTO   ABEND
    END

/.##メンテナンスＣＳＶデータ転送処理##./
PFEXPORT:

    ?STEP :=  'PFEXPORT'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

                FEXPORT FILE-STBMNTCS.HGWORKLB,
                        MODE-@REP,
                        PARA-SHTMNTCS,
                        UNIT-1
                ?PGMEC  := @PGMEC
                ?PGMES  := @PGMES
                IF      ?PGMEC ^= 0    THEN
                        ?KEKA4 := 'メンテナンスＣＳＶ転送異常！！'
                        GOTO   ABEND
                END

SORT1:

    SORT      INFILE-STBMNTCS.HGWORKLB,INRL-162,INBF-25,
              OUTFILE-STBMNTCS.HGWORKLB,OUTBF-25,
              KEY-5!8!DA,KEY1-14!13!CA,
              RCDL-@DSP
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＳＯＲＴ処理'
              GOTO   ABEND
    END

/.##商品変換テーブル取込チェック##./
NVM0120B:

    ?STEP :=  'NVM0120B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

                OVRF   FILE-STBMNTCS,TOFILE-STBMNTCS.HGWORKLB
                OVRF   FILE-STBMNTW1,TOFILE-STBMNTW1.HGWORKLB
                CALL   PGM-NVM0120B.TOKSOLIB,
                       PARA-(?BUMON,?TANCD,
                             ?CNT1,?CNT2)
                ?PGMEC := @PGMEC
                ?PGMES := @PGMES
                IF        ?PGMEC ^= 0    THEN
                          ?KEKA4 := 'データチェック'
                          GOTO   ABEND
                END

    IF  ?CNT1  =  '0000000'  THEN   /.CSVゼロ件時は終了./
        IF  ?CNT2  =  '0000000'  THEN
            SNDMSG 'ＥＸＣＥＬデータ０件です！',
                   ,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
            RETURN
        END
    END

    IF  ?CNT2 ^=  '0000000'  THEN   /.##エラーがあった場合##./
        GOTO  NVM0130L
    ELSE
        GOTO  NVM0140B
    END

/.##商品変換テーブル取込チェックリスト(エラー分)##./
NVM0130L:

    ?STEP :=  'NVM0130L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    ?OUTKBN   :=  '2'
/.実環境にあわせて変更ください./
/.  IF  ?BUMON  =  '2920'  THEN
        OVRPRTF FILE-XU04LP,TOFILE-LBPPRT01.TOKELIBO
    ELSE
        OVRPRTF FILE-XU04LP,TOFILE-LBPPRTE1.TOKELIBO
    END
./
    OVRF      FILE-STBMNTW3,TOFILE-STBMNTW3.HGWORKLB
    OVRF      FILE-STBMNTW2,TOFILE-STBMNTW2.HGWORKLB
    CALL      PGM-NVM0130L.TOKSOLIB,
              PARA-(?BUMON,?TANCD,?OUTKBN)
    ?PGMEC    := @PGMEC
    ?PGMES    := @PGMES
    IF        ?PGMEC ^= 0    THEN
              ?KEKA4 := '取込エラーリスト'
              GOTO   ABEND
    END

    GOTO  RTN

/.##商品変換テーブル更新処理##./
NVM0140B:

    ?STEP :=  'NVM0140B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    OVRF   FILE-STBMNTW1,TOFILE-STBMNTW1.HGWORKLB
/.##CALL   PGM-NVM0140B.TOKSOLIB,
           PARA-(?BUMON,?TANCD)
##./CALL   PGM-NVM0141B.TOKSOLIB,
           PARA-(?BUMON,?TANCD)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF     ?PGMEC ^= 0    THEN
           ?KEKA4 := '商品変換テーブル更新'
           GOTO   ABEND
    END

RTN:

    RELEASE   FILE-STBMNTCS.HGWORKLB!@XCL/STBMNTWK.HGWORKLB!@XCL

    IF        ?CNT2   =  '0000000'  THEN
              ?KEKA1 :=  '※商品変換テーブル'
              ?KEKA2 :=  '　ＥＸＣＥＬデータ取込処理が'
              ?KEKA3 :=  '　正常終了しました。'
              ?KEKA4 :=  '　（チェック・更新）'
    ELSE
              ?KEKA1 :=  '商品変換テーブル　データチェック'
              ?KEKA2 :=  '　エラー箇所があります　　　　　　　　'
              ?KEKA3 :=  '　チェックリストを確認し、修正／再取込'
              ?KEKA4 :=  '　を行って下さい。'
    END

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    RETURN    PGMEC-@PGMEC

ABEND:

    RELEASE   FILE-STBMNTCS.HGWORKLB!@XCL/STBMNTWK.HGWORKLB!@XCL

    ?KEKA1 :=  '商品変換テーブル取込・更新処理が'
    ?KEKA2 :=  '異常終了しました。'
    ?KEKA3 :=  'ログ採取し，ＮＡＶへ連絡して下さい。'
    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO    ?MSGX  := ?MSG(?I)
              SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
    END

    RETURN    PGMEC-?PGMEC

ABEND1:

    RELEASE   FILE-STBMNTCS.HGWORKLB!@XCL/STBMNTWK.HGWORKLB!@XCL

    ?KEKA1 :=  'ＥＸＣＥＬデータ取込処理関係ファイ'
    ?KEKA2 :=  'ルが、他で使用されています。'
    ?KEKA3 :=  '他端末で使用中でないか確認して下さい。'
    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO    ?MSGX  := ?MSG(?I)
              SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
    END

    RETURN    PGMEC-?PGMEC

```
