# PSY44850

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PSY44850.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *   サカタのタネ　ホームガーデン部　　　　　　　  　      *  ./
/. *   SYSTEM-NAME :    流通ＢＭＳ対応　　　　　　　　　　   *  ./
/. *   JOB-ID      :    PSY44850    2018/04/17               *  ./
/. *   JOB-NAME    :    流通ＢＭＳ出荷データ作成　　　　　   *  ./
/. *               :    　バロー（手書分）　　　　　　　　   *  ./
/. *               :    13/03/22 バロー植物　　　　　　　    *  ./
/. *               :    13/04/09 相手商品名対応　　　　　    *  ./
/. *               :    18/04/17 バロー手書対応　　　　　    *  ./
/. *               :    22/10/12 SBM0032I 0件制御追加　      *  ./
/. *               :    23/01/23 アレンザＨＤ追加　　　      *  ./
/. ***********************************************************  ./
    PGM   (P1-?TRICD)
    PARA ?TRICD   ,STRING*8,IN,VALUE-'00000000'  /.取引先コード ./
/.###ﾜｰｸｴﾘｱ定義####./
    VAR ?FILNM    ,STRING*8,VALUE-'        '    /.ﾌｧｲﾙ名合併用./
    VAR ?FILNMH   ,STRING*8,VALUE-'BMSTEGF'     /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMT   ,STRING*8,VALUE-'BMSTE2F'     /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMD   ,STRING*8,VALUE-'BMSTE7F'     /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMN   ,STRING*8,VALUE-'BMSTE8F'     /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMM   ,STRING*8,VALUE-'BMSTE9F'     /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?LIBNM    ,STRING*8,VALUE-'BMSFLIB '    /.ﾗｲﾌﾞﾗﾘ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILID    ,NAME                         /.ﾌｧｲﾙ名名前型./
    VAR ?LIBID    ,NAME                         /.ﾗｲﾌﾞﾗﾘ名名前型./
    VAR ?JHTDENF  ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?JHTTEGF  ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?JHTDENFN ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?JHTTEGFN ,STRING*17                    /.ﾌｧｲﾙ名表示用./

    VAR       ?PGMEC  ,INTEGER
    VAR       ?PGMECX ,STRING*11
    VAR       ?PGMEM  ,STRING*99
    VAR       ?MSG    ,STRING*99(6)
    VAR       ?MSGX   ,STRING*99
    VAR       ?PGMID  ,STRING*8,VALUE-'PSY44850'
    VAR       ?STEP   ,STRING*8
    VAR       ?WKSTN  ,NAME!MOD
    VAR       ?JBNM   ,STRING*24!MIXED            /.業務漢字名 ./
    VAR       ?JBID   ,STRING*10                  /.業務ＩＤ   ./
    VAR       ?PGID   ,STRING*10                  /.ＰＧＩＤ　 ./
    VAR       ?CHK    ,STRING*1,VALUE-'0'         /.ﾊﾟﾗﾒﾀﾁｪｯｸ./
    VAR       ?CHK1   ,STRING*1,VALUE-'0'         /.ﾊﾟﾗﾒﾀﾁｪｯｸ./
    VAR       ?CHK2   ,STRING*1,VALUE-'0'         /.ﾊﾟﾗﾒﾀﾁｪｯｸ./
    VAR       ?CHK3   ,STRING*1,VALUE-'0'         /.ﾊﾟﾗﾒﾀﾁｪｯｸ./
    VAR       ?OK1    ,STRING*2,VALUE-'  '        /.実行確認1./
    VAR       ?OK2    ,STRING*2,VALUE-'  '        /.実行確認2./
    VAR       ?OK3    ,STRING*2,VALUE-'  '        /.実行確認3./
/.-----------------------------------------------------------./
    VAR       ?CLID   ,STRING*8                   /.ＣＬＩＤ   ./
    VAR       ?MSG1   ,STRING*80                  /.開始終了MSG./
    VAR       ?OPR1   ,STRING*50                  /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?OPR2   ,STRING*50                  /.      2    ./
    VAR       ?OPR3   ,STRING*50                  /.      3    ./
    VAR       ?OPR4   ,STRING*50                  /.      4    ./
    VAR       ?OPR5   ,STRING*50                  /.      5    ./
    VAR       ?PGNM   ,STRING*40                  /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?KEKA1  ,STRING*40                  /.      2    ./
    VAR       ?KEKA2  ,STRING*40                  /.      3    ./
    VAR       ?KEKA3  ,STRING*40                  /.      4    ./
    VAR       ?KEKA4  ,STRING*40                  /.      5    ./
    VAR       ?PGCHK1 ,STRING*1,VALUE-' '         /.      5    ./
    VAR       ?PGCHK2 ,STRING*1,VALUE-' '         /.      5    ./
    VAR       ?PGCHK3 ,STRING*1,VALUE-' '         /.      5    ./
    VAR       ?SYORINM1 ,STRING*40
    VAR       ?SYORINM2 ,STRING*50
    VAR       ?SYORINM3 ,STRING*50
    VAR       ?SYORIKN1 ,STRING*7,VALUE-'0000000'
    VAR       ?SYORIKN2 ,STRING*7,VALUE-'0000000'
    VAR       ?SYORIKN3 ,STRING*7,VALUE-'0000000'
    VAR       ?SYORIKN4 ,STRING*7,VALUE-'0000000'
    VAR       ?SYORIKN5 ,STRING*7,VALUE-'0000000'
    VAR       ?BUMON    ,STRING*4,VALUE-'    '   /.部門名./
    VAR       ?TANCD6   ,STRING*6,VALUE-'      ' /.担当者CD./
    VAR       ?TANCD    ,STRING*2,VALUE-'  '     /.担当者CD./
    VAR       ?WS ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR       ?SOKCD  ,STRING*2,VALUE-'  '         /.自倉庫ｺｰﾄﾞ./
    VAR       ?DSOKCD ,STRING*2,VALUE-'  '         /.代表倉庫ｺｰﾄﾞ./
    VAR       ?JDATE  ,STRING*8,VALUE-'00000000'   /.受信日付./
    VAR       ?JTIME  ,STRING*4,VALUE-'0000'       /.受信時間./
    VAR       ?TORICD ,STRING*8,VALUE-'00000000'   /.取引先./
/.  VAR       ?SOKO   ,STRING*8,VALUE-'00000000'./ /.出荷場所./
    VAR       ?SOKO   ,STRING*2,VALUE-'00'         /.出荷場所./
    VAR       ?NOUDT  ,STRING*8,VALUE-'00000000'   /.納品日./
/.  VAR       ?BMNCD  ,STRING*8,VALUE-'00000000'./ /.部門./
    VAR       ?BMNCD  ,STRING*4,VALUE-'0000'       /.部門./
/.  VAR       ?TNCD   ,STRING*5,VALUE-'00000'   ./ /.担当者　./
    VAR       ?TNCD   ,STRING*2,VALUE-'00'         /.担当者　./
    DEFLIBL BMSFLIB/TOKELIB/TOKFLIB/TOKKLIB

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM :=  '流通ＢＭＳ出荷ＤＴ作成（バロー）'
    ?SYORINM1 :=  '流通ＢＭＳ出荷ＤＴ作成（バロー）'

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
STEP000:
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##確認画面##./
STEP010:

    ?OPR1  :=  '【流通ＢＭＳ出荷ＤＴ作成】'
    ?OPR2  :=  '流通ＢＭＳ出荷データを作成します。'
    ?OPR3  :=  '　　　（バロー）　　　　　　'
    ?OPR4  :=  '　　　　　　　　　　　　　　　　　　'
    CALL      OHOM0900.XUCL,PARA-
                            (?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
STEP020:
    DEFLIBL BMSFLIB/TOKELIB/TOKFLIB/TOKKLIB

/.##ログインユーザー情報取得##./
STEP030:

    ?STEP :=   'STEP030'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD6)
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ログイン情報取得'
              GOTO ABEND
    END
    ?TANCD := %SBSTR(?TANCD6,1,2)


/.##倉庫ｺｰﾄﾞ取得##./
STEP040:

    ?STEP :=   'STEP040'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKCD,?DSOKCD)
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END
    SNDMSG    '?WS=',TO-XCTL
    SNDMSG     ?WS,TO-XCTL
    SNDMSG    '?SOKCD=',TO-XCTL
    SNDMSG     ?SOKCD,TO-XCTL
    SNDMSG    '?DSOKCD=',TO-XCTL
    SNDMSG     ?DSOKCD,TO-XCTL
/.##倉庫ｺｰﾄﾞ選択##./
SKY1702I:

    ?STEP :=   'SKY1702I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRDSPF   FILE-PRTF,TOFILE-PRTF.XUCL,MEDLIB-TOKELIB
    CALL      PGM-SKY1702I.TOKELIB,PARA-(?SOKCD,?DSOKCD)
    IF        @PGMEC    ^=   0    THEN
              ?PGMEC := @PGMEC
              IF    ?PGMEC  =  4010
                 THEN
                    ?MSGX := '##取消終了##'
                    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                    RETURN PGMEC-@PGMEC
                 ELSE
                    GOTO   ABEND
                 END
    END


/.##ﾌｧｲﾙ名称取得##./
/.----------------------------------
FILNMCH1:

    ?MSGX := '## 実行倉庫ｺｰﾄﾞ = ' && ?SOKCD && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                                     -----------------------./
/.  IF    ?TRICD   =    '00013020'   THEN
        ?FILNM    :=    ?FILNMH
    ELSE
        ?FILNM    :=    ?FILNMT
    END
./  CASE  ?TRICD  OF
          # '00013020' #  /.バロー（資材）./
          ?FILNM    :=    ?FILNMH
          # '00013020' #  /.バロー（植物）./
          ?FILNM    :=    ?FILNMT
          # '00751225' #  /.ダイユーエイト./
          ?FILNM    :=    ?FILNMD
          # '00781225' #  /.日敷　　　　　./
          ?FILNM    :=    ?FILNMN
          # '00761995' #  /.タイム　　　　./
          ?FILNM    :=    ?FILNMM
    ELSE        /.異常受信./
          ?FILNM    :=    ?FILNMH
    END
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?JHTTEGF  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?JHTTEGFN :=    %STRING(?JHTTEGF)
    ?MSGX     :=    ?JHTTEGFN
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##出力ファイル確認##./
PASSIGN1:

    ?STEP :=   'PASSIGN1'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ASSIGN FILE-?JHTTEGF!@XCL
 /. ASSIGN FILE-BMSTEGF.BMSFLIB!@XCL    ./
    IF     @PGMEC  ^=    0    THEN
           SNDMSG '他で連携処理が行われています',TOWS-@ORGWS
           ?KEKA4 :=  '伝票データ（手書）ロック'
           GOTO  ABEND
    END

PASSIGN2:

    ?STEP :=   'PASSIGN2'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ASSIGN FILE-BMSSYKF.BMSFLIB!@XCL
    IF     @PGMEC  ^=    0    THEN
           SNDMSG '他で連携処理が行われています',TOWS-@ORGWS
           ?KEKA4 :=  '出荷メッセージファイルロック'
           GOTO  ABEND
    END

PASSIGN3:

    ?STEP :=   'PASSIGN3'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ASSIGN FILE-SYKSNDF.BMSFLIB!@XCL
    IF     @PGMEC  ^=    0    THEN
           SNDMSG '他で連携処理が行われています',TOWS-@ORGWS
           ?KEKA4 :=  '出荷メッセージ送信ファイルロック'
           GOTO  ABEND
    END

/.##バロー出荷データ作成処理（手書）##./
STEP050:

    ?STEP :=   'STEP050 '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    /. データＳＯＲＴ ./
    SORTF  INFILE-?JHTTEGF,
           OUTFILE-?JHTTEGF,
           KEY-'F01!A',
           KEY1-'F07!A',
           KEY2-'F112!A',
           KEY3-'F132!A',
           KEY4-'F12!A',
           KEY5-'F02!A',
           KEY6-'F03!A',
     /.    SELECT-'F01!EQ!#00013020', ./
           RCDL-@DSP
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4    :=   '伝票データＳＯＲＴ処理'
              GOTO ABEND
    END

/. 処理前データクリア 累積ファイルなのでクリアなし ./
/.  CLRFILE   FILE-BMSSYKF.BMSFLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4    :=   '処理前データクリア処理'
              GOTO ABEND
    END                      ./

/.##出荷データ作成##./
STEP060:

    ?STEP :=   'STEP060 '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF FILE-JHTTEGF,TOFILE-?JHTTEGF
 /. OVRF FILE-JHTTEGF,TOFILE-BMSTEGF.BMSFLIB  ./
    OVRF FILE-TENMS1,TOFILE-TENMS1.TOKFLIB
    OVRF FILE-MEIMS1,TOFILE-MEIMS1.TOKFLIB
    OVRF FILE-SYOMEIL1,TOFILE-SYOMEIL1.TOKKLIB
    OVRF FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    OVRF FILE-BMSSYKL3,TOFILE-BMSSYKL3.BMSFLIB
    CALL SSY4485B.TOKSOLIB,PARA-(?SYORIKN1,?JDATE,?JTIME,?TORICD)
    IF      @PGMEC    ^=   0    THEN
       IF      @PGMEC    =  4010    THEN
               GOTO ZERO
       ELSE
               ?KEKA4 := '【出荷データ作成（バロー手書）】'
               GOTO ABEND
       END
    END


/.##出荷データフォーマット変換##./
STEP070:

    ?STEP :=   'STEP070 '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF FILE-BMSSYKL2,TOFILE-BMSSYKL2.BMSFLIB
    OVRF FILE-SYKSNDF,TOFILE-SYKSNDF.BMSFLIB
    CALL  SBM0083B.TOKSOLIB,PARA-(?JDATE,?JTIME,?TORICD,?SYORIKN2)
    IF      @PGMEC    ^=   0    THEN
            ?KEKA4 := '【出荷送信データフォーマット変換】'
                    GOTO ABEND
    END

/. 出荷送信データ複写 ./
STEP080:

    ?STEP :=   'STEP080 '
    ?MSGX :=  '***   '  && ?STEP   &&  ?TRICD && '        ***'
    SNDMSG    ?MSGX,TO-XCTL
    CASE ?TRICD OF
      #'00013020'# /. バロー ./
        CNVFILE   FILE-SYKSNDF.BMSFLIB,
                  TOFILE-SYKSNDBR.ONLBLIB,
                  ADD-@YES,BF-1
        IF        @PGMEC    ^=   0    THEN
              ?KEKA4    :=   '手書出荷送信ＤＴセット（バ資）'
              GOTO ABEND
        END
      #'00013052'# /. バロー植物 ./
        CNVFILE   FILE-SYKSNDF.BMSFLIB,
                  TOFILE-SYKSNDB2.ONLBLIB,
                  ADD-@YES,BF-1
        IF        @PGMEC    ^=   0    THEN
              ?KEKA4    :=   '手書出荷送信ＤＴセット（バ植）'
              GOTO ABEND
        END
      #'00751225'# /. バロー植物 ./
        CNVFILE   FILE-SYKSNDF.BMSFLIB,
                  TOFILE-SYKSNDDY.ONLBLIB,
                  ADD-@YES,BF-1
        IF        @PGMEC    ^=   0    THEN
              ?KEKA4    :=   '手書出荷送信ＤＴセット（ダイ）'
              GOTO ABEND
        END
      #'00781225'# /. バロー植物 ./
        CNVFILE   FILE-SYKSNDF.BMSFLIB,
                  TOFILE-SYKSNDNS.ONLBLIB,
                  ADD-@YES,BF-1
        IF        @PGMEC    ^=   0    THEN
              ?KEKA4    :=   '手書出荷送信ＤＴセット（日敷）'
              GOTO ABEND
        END
      #'00761995'# /. バロー植物 ./
        CNVFILE   FILE-SYKSNDF.BMSFLIB,
                  TOFILE-SYKSNDTM.ONLBLIB,
                  ADD-@YES,BF-1
        IF        @PGMEC    ^=   0    THEN
              ?KEKA4    :=   '手書出荷送信ＤＴセット（タイ）'
              GOTO ABEND
        END
    END

/.##プログラム正常終了##./
RTN:

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  '出荷データ作成処理が正常終了しました。'
    ?KEKA2 :=  '件数リスト等を確認して下さい。'
    ?KEKA3 :=  ''
    ?KEKA4 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    DEFLIBL TOKELIBO/TOKELIB/TOKFLIB/BMSFLIB
    DLTOVRF
    OVRPRTF FILE-PRTF,TOFILE-PRTF.TOKELIB,MEDLIB-TOKELIBO

    ?SYORINM2 := '出荷データ作成処理が正常終了しました。　　　　'
    ?SYORINM3 := '件数１：出荷件数（手書）　　　　'
    CALL SSN0050L.TOKELIBO,
          PARA-(?SYORINM1,?SYORINM2,?SYORINM3,?SYORIKN1,?SYORIKN2,
                ?SYORIKN3,?SYORIKN4,?SYORIKN5)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

/.##プログラム異常終了##./
ABEND:

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  '出荷データ作成処理が異常終了しました。'
    ?KEKA2 :=  'ログ採取し，ＮＡＶへ連絡して下さい。'
    ?KEKA3 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

/.##対象データなし時##./
ZERO:

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  '　　　　　　処理対象データ　　　　　　'
    ?KEKA2 :=  '　　　　　　　　無！！　　　　　　　　'
    ?KEKA3 :=  '　　　　　　　　無！！　　　　　　　　'
    ?KEKA4 :=  '　　　　　　確認してください　　　　　'
    CALL SMG0032I.TOKSOLIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

```
