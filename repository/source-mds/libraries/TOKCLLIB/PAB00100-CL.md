# PAB00100

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PAB00100.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *  （株）サカタのタネ                                     *  ./
/. *   SYSTEM-NAME :    ＨＧ基幹　　　　　　　　　　　       *  ./
/. *   JOB-NAME    :   （ＥＸＣＥＬ取込）　　　　　　　　　　*  ./
/. *                    倉庫商品別_番設定マスタ　　　　　   *  ./
/. *                    　取込チェック・更新                 *  ./
/. *                    ※Ｄ３６５連携対応　　　　　　　　   *  ./
/. *   JOB-ID      :    PAB00100  2020/04/21                 *  ./
/. *   UPDATE      :                                         *  ./
/. *                                                         *  ./
/. ***********************************************************  ./

    VAR  ?WS      ,STRING*8,VALUE-'        ' /.ﾜｰｸｽﾃｰｼｮﾝID./
    VAR  ?WKSTN   ,NAME!MOD                  /.ﾜｰｸｽﾃｰｼｮﾝID./
    VAR  ?PGMEC   ,INTEGER
    VAR  ?PGMES   ,STRING*5,VALUE-'     '
    VAR  ?PGMECX  ,STRING*11
    VAR  ?PGMEM   ,STRING*99
    VAR  ?MSG     ,STRING*99(6)
    VAR  ?MSGX    ,STRING*99
    VAR  ?PGMID   ,STRING*8,VALUE-'PAB00100'
    VAR  ?STEP    ,STRING*8
/.--------------------------------------------------------------./
    VAR  ?BUMON   ,STRING*4,VALUE-'    '     /.部門./
    VAR  ?TANCD8  ,STRING*8,VALUE-'        ' /.部門+担当者./
    VAR  ?TANCD   ,STRING*2,VALUE-'  '       /.担当者     ./
    VAR  ?SOKCD   ,STRING*2,VALUE-'00'       /.実行倉庫   ./
    VAR  ?DSOKCD  ,STRING*2,VALUE-'00'       /.代表倉庫   ./
/.--------------------------------------------------------------./
    VAR  ?CNT1    ,STRING*7,VALUE-'0000000'  /.件数(取込) ./
    VAR  ?CNT2    ,STRING*7,VALUE-'0000000'  /.件数(ＥＲ）./
/.--------------------------------------------------------------./
    VAR  ?OUTKBN  ,STRING*1,VALUE-' '        /.出力種別   ./
/.--------------------------------------------------------------./
    VAR  ?OPR1    ,STRING*50                 /.ﾒｯｾｰｼﾞ1    ./
    VAR  ?OPR2    ,STRING*50                 /.      2    ./
    VAR  ?OPR3    ,STRING*50                 /.      3    ./
    VAR  ?OPR4    ,STRING*50                 /.      4    ./
    VAR  ?OPR5    ,STRING*50                 /.      5    ./
/.--------------------------------------------------------------./
    VAR  ?MSG1    ,STRING*80                 /.開始終了MSG./
    VAR  ?PGNM    ,STRING*40                 /.ﾒｯｾｰｼﾞ1    ./
    VAR  ?KEKA1   ,STRING*40                 /.      2    ./
    VAR  ?KEKA2   ,STRING*40                 /.      3    ./
    VAR  ?KEKA3   ,STRING*40                 /.      4    ./
    VAR  ?KEKA4   ,STRING*40                 /.      5    ./
/.---------------------------------------------------------------./

  /.##端末別ファイルＩＤ編集用##./   /.当機能では非使用./

    /.共通_ライブラリ名./
    VAR  ?LIBNM    ,STRING*8,VALUE-'HGWORKLB'
    /.共通_ファイルＩＤワーク./
    VAR  ?FILID    ,NAME
    /.共通_ＬＩＢＩＤワーク./
    VAR  ?LIBID    ,NAME
    /.共通_端末番号./
    VAR  ?TANNO    ,STRING*03,VALUE-'   '

    /.データ_共通部_ファイルＩＤ先頭./
    VAR  ?STB      ,STRING*3,VALUE-'SYO'
    /.データ_個別部_ファイルＩＤ末尾./
    VAR  ?CS       ,STRING*2,VALUE-'CS'
    VAR  ?WK       ,STRING*2,VALUE-'WK'
    VAR  ?W1       ,STRING*2,VALUE-'W1'
    VAR  ?W2       ,STRING*2,VALUE-'W2'
    VAR  ?W3       ,STRING*2,VALUE-'W3'

    /.データ_OVRF_ファイルＩＤ./
    VAR  ?SMSXXXCS ,NAME!MOD
    VAR  ?SMSXXXWK ,NAME!MOD
    VAR  ?SMSXXXW1 ,NAME!MOD
    VAR  ?SMSXXXW2 ,NAME!MOD
    VAR  ?SMSXXXW3 ,NAME!MOD

    /.ファイルＩＤ表示用ワーク./
    VAR  ?DSPFILID,STRING*17
    VAR  ?FILNM7  ,STRING*7,VALUE-'       '
    VAR  ?FILNM8  ,STRING*8,VALUE-'        '

/.-----------------------------------------------------------./


/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL   TOKELIB/TOKELIBO/TOKSOLIB/TOKDLIB/TOKFLIB/TOKKLIB
             /HGWORKLB/TOKDTLIB/D365DLIB

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
CLSTART:
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM := '倉庫商品別_番設定　取込チェック＆更新'

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

/.##ログインユーザー情報取得##./
SIT9000B:

    ?STEP :=  'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMES    :=    @PGMES
    IF        ?PGMEC ^=   0    THEN
              GOTO   ABEND
    END
    ?TANCD := %SBSTR(?TANCD8,1,2)

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKCD,?DSOKCD)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMES    :=    @PGMES
    IF        ?PGMEC ^=   0   THEN
              ?KEKA4 := '倉庫コード取得'
              GOTO   ABEND
    END

/.##使用ファイルロック・クリア##./
ASSIGN:

    ?STEP :=  'ASSIGN'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    ASSIGN FILE-SYOTANCS.HGWORKLB!@XCL
    ?PGMEC :=    @PGMEC
    ?PGMEM :=    @PGMEM
    ?PGMES :=    @PGMES
    IF     ?PGMEC ^=   1020 THEN
       IF  ?PGMEC ^=   0    THEN
           SNDMSG '連携処理が行えません',
                   TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
           ?KEKA4 :=  'ＣＳＶデータロック'
           GOTO   ABEND1
       END
    END

    ASSIGN FILE-SYOTANWK.HGWORKLB!@XCL
    ?PGMEC :=    @PGMEC
    ?PGMEM :=    @PGMEM
    ?PGMES :=    @PGMES
    IF     ?PGMEC ^=   0    THEN
           SNDMSG '連携処理が行えません',
                   TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
           ?KEKA4 :=  '取込ファイルロック'
           GOTO   ABEND1
    END
    CLRFILE FILE-SYOTANWK.HGWORKLB

/.##取込確認画面##./
SHOM0900:

    ?STEP :=  'SHOM0900'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    ?OPR1 := '　倉庫商品別_番設定データ　取込・更新　'
    ?OPR2 := '　　　　！！確認して下さい！！'
    ?OPR3 := '　 ＥＸＣＥＬデータの取込＆更新を行います'
    ?OPR4 := '　　　・連携するデータは出力済ですか？'
    ?OPR5 := '　　　・所定フォルダにセット済ですか？'

    CALL   OHOM0900.TOKELIB,
           PARA-(?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC ^= 0    THEN
              ?KEKA4 := '処理確認画面'
              GOTO   ABEND
    END

/.##メンテナンスＣＳＶデータ転送処理##./
PFEXPORT:

    ?STEP :=  'PFEXPORT'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

                FEXPORT FILE-SYOTANCS.HGWORKLB,
                        MODE-@REP,
                        PARA-SYOTANCS,
                        UNIT-1
                ?PGMEC  := @PGMEC
                ?PGMES  := @PGMES
                IF      ?PGMEC ^= 0    THEN
                        ?KEKA4 := 'メンテナンスＣＳＶ転送異常！！'
                        GOTO   ABEND
                END

SORT1:

    SORT      INFILE-SYOTANCS.HGWORKLB,INRL-107,INBF-38,
              OUTFILE-SYOTANCS.HGWORKLB,OUTBF-38,
              KEY-3!2!CA,KEY1-6!13!CA,
              RCDL-@DSP
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＳＯＲＴ処理'
              GOTO   ABEND
    END

/.##倉庫商品別_番設定データ　取込チェック##./
TAB0010B:

    ?STEP :=  'TAB0010B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

                OVRF   FILE-SYOTANCS,TOFILE-SYOTANCS.HGWORKLB
                OVRF   FILE-SYOTANW1,TOFILE-SYOTANW1.HGWORKLB
                CALL   PGM-TAB0010B.TOKSOLIB,
                       PARA-(?BUMON,?TANCD,
                             ?CNT1,?CNT2)
                ?PGMEC := @PGMEC
                ?PGMES := @PGMES
                IF        ?PGMEC ^= 0    THEN
                          ?KEKA4 := 'データチェック'
                          GOTO   ABEND
                END

    IF  ?CNT1  =  '0000000'  THEN   /.CSVゼロ件時は終了./
        IF  ?CNT2  =  '0000000'  THEN
            SNDMSG 'ＥＸＣＥＬデータ０件です！',
                   ,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
            RETURN
        END
    END

    IF  ?CNT2 ^=  '0000000'  THEN   /.##エラーがあった場合##./
        GOTO  TAB0030L
    ELSE
        GOTO  TAB0020B
    END

/.##倉庫商品別_番設定データ　取込チェックリスト(エラー分)##./
TAB0030L:

    ?STEP :=  'TAB0030L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    ?OUTKBN   :=  '2'
/.実環境にあわせて変更ください./
/.  IF  ?BUMON  =  '2920'  THEN
        OVRPRTF FILE-XU04LP,TOFILE-LBPPRT01.TOKELIBO
    ELSE
        OVRPRTF FILE-XU04LP,TOFILE-LBPPRTE1.TOKELIBO
    END
./
    OVRF      FILE-SYOTANW3,TOFILE-SYOTANW3.HGWORKLB
    OVRF      FILE-SYOTANW2,TOFILE-SYOTANW2.HGWORKLB
    CALL      PGM-TAB0030L.TOKSOLIB,
              PARA-(?BUMON,?TANCD,?OUTKBN)
    ?PGMEC    := @PGMEC
    ?PGMES    := @PGMES
    IF        ?PGMEC ^= 0    THEN
              ?KEKA4 := '取込エラーリスト'
              GOTO   ABEND
    END

    GOTO  RTN

/.##倉庫商品別_番設定マスタ　更新処理##./
TAB0020B:

    ?STEP :=  'TAB0020B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    OVRF   FILE-SYOTANW1,TOFILE-SYOTANW1.HGWORKLB
    CALL   PGM-TAB0020B.TOKSOLIB,
           PARA-(?BUMON,?TANCD)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF     ?PGMEC ^= 0    THEN
           ?KEKA4 := '倉庫商品別_番設定マスタ更新'
           GOTO   ABEND
    END

RTN:

    RELEASE   FILE-SYOTANCS.HGWORKLB!@XCL/SYOTANWK.HGWORKLB!@XCL

    IF        ?CNT2   =  '0000000'  THEN
              ?KEKA1 :=  '※倉庫商品別_番設定マスタ　'
              ?KEKA2 :=  '　ＥＸＣＥＬデータ取込処理が'
              ?KEKA3 :=  '　正常終了しました。'
              ?KEKA4 :=  '　（チェック・更新）'
    ELSE
              ?KEKA1 :=  '倉庫商品別_番設定データチェック'
              ?KEKA2 :=  '　エラー箇所があります　　　　　　　　'
              ?KEKA3 :=  '　チェックリストを確認し、修正／再取込'
              ?KEKA4 :=  '　を行って下さい。'
    END

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    RETURN    PGMEC-@PGMEC

ABEND:

    RELEASE   FILE-SYOTANCS.HGWORKLB!@XCL/SYOTANWK.HGWORKLB!@XCL

    ?KEKA1 :=  '倉庫商品別_番設定　取込・更新処理が'
    ?KEKA2 :=  '異常終了しました。'
    ?KEKA3 :=  'ログ採取し，ＮＡＶへ連絡して下さい。'
    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO    ?MSGX  := ?MSG(?I)
              SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
    END

    RETURN    PGMEC-?PGMEC

ABEND1:

    RELEASE   FILE-SYOTANCS.HGWORKLB!@XCL/SYOTANWK.HGWORKLB!@XCL

    ?KEKA1 :=  'ＥＸＣＥＬデータ取込処理関係ファイ'
    ?KEKA2 :=  'ルが、他で使用されています。'
    ?KEKA3 :=  '他端末で使用中でないか確認して下さい。'
    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO    ?MSGX  := ?MSG(?I)
              SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
    END

    RETURN    PGMEC-?PGMEC

```
