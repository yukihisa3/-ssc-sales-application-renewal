# PKE10400

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PKE10400.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　本社システム                          *  ./
/. *   SYSTEM-NAME :    検品システム　　                     *  ./
/. *   JOB-ID      :    PKE10400                             *  ./
/. *   JOB-NAME    :    出荷検品データ取込・チェック         *  ./
/. *                      （カインズＴＣ１）                 *  ./
/. ***********************************************************  ./
    PGM
/.###ﾜｰｸｴﾘｱ定義####./
    VAR ?FILNM    ,STRING*8,VALUE-'        '    /.ﾌｧｲﾙ名合併用./
    VAR ?FILNMH   ,STRING*6,VALUE-'      '      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMK   ,STRING*6,VALUE-'      '      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?SNDFIN   ,STRING*6,VALUE-'      '      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMH1  ,STRING*6,VALUE-'RCVCZD'      /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMH2  ,STRING*5,VALUE-'CZSUM'       /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILNMH3  ,STRING*5,VALUE-'CZKON'       /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR ?LIBNM    ,STRING*8,VALUE-'TOKKPLIB'    /.ﾗｲﾌﾞﾗﾘ名ﾃﾞﾌｫﾙﾄ./
    VAR ?FILID    ,NAME                         /.ﾌｧｲﾙ名名前型./
    VAR ?LIBID    ,NAME                         /.ﾗｲﾌﾞﾗﾘ名名前型./
    VAR ?RCVCZDF  ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?RCVCZDFN ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?CZSUMF   ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZSUMFN  ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?CZSUM1   ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZSUM1N  ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?CZSUM2   ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZSUM2N  ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?CZSUM3   ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZSUM3N  ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?CZKONF   ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZKONFN  ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?CZKON1   ,NAME!MOD                     /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR ?CZKON1N  ,STRING*17                    /.ﾌｧｲﾙ名表示用./
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR ?PGMEC    ,INTEGER                      /.ﾌﾟﾛｸﾞﾗﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMECX   ,STRING*11                    /.ｼｽﾃﾑｴﾗｰｺｰﾄﾞ./
    VAR ?PGMEM    ,STRING*99                    /.ｼｽﾃﾑｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ格納ﾃｰﾌﾞﾙ./
    VAR ?MSGX     ,STRING*99                    /.SNDMSG表示用./
    VAR ?CLID     ,STRING*8,VALUE-'PKE10400'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?CLNM     ,STRING*40                    /.CL名称       ./
    VAR ?STEP     ,STRING*8                     /.STEP-ID./
    VAR ?BUMON    ,STRING*4,VALUE-'    '        /.部門CD./
    VAR ?TANCD8   ,STRING*8,VALUE-'        '    /.担当者CD./
    VAR ?TANCD    ,STRING*2,VALUE-'  '          /.担当者CD./
    VAR ?JISOKO   ,STRING*2,VALUE-'00'          /.倉庫(自)  ./
    VAR ?DSOKO    ,STRING*2,VALUE-'00'          /.倉庫(代表)./
    VAR ?NO       ,STRING*1,VALUE-'2'           /.処理種別./
    VAR ?KENSU    ,STRING*7,VALUE-'0000000'     /.抽出件数./
    VAR ?DATKBN   ,STRING*1,VALUE-' '           /.取込対象区分./
    VAR ?HAKKBN   ,STRING*1,VALUE-' '           /.出荷実績発行区分./
    VAR ?OUTKBN   ,STRING*1,VALUE-' '           /.出力区分./
    VAR ?TDATE    ,STRING*8,VALUE-'00000000'    /.取込日付./
    VAR ?TTIME    ,STRING*6,VALUE-'000000'      /.取込時刻./
    VAR ?KEKKA    ,STRING*2,VALUE-'  '          /.チェック結果./
    VAR ?SYORINM1 ,STRING*50
    VAR ?SYORINM2 ,STRING*40
    VAR ?OPR1     ,STRING*50                 /.ﾒｯｾｰｼﾞ1    ./
    VAR ?OPR2     ,STRING*50                 /.      2    ./
    VAR ?OPR3     ,STRING*50                 /.      3    ./
    VAR ?OPR4     ,STRING*50                 /.      4    ./
    VAR ?OPR5     ,STRING*50                 /.      5    ./
/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?CLID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL
/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL   TOKELIB/TOKFLIB/TOKKLIB/TOKDLIB/TOKDTLIB/TOKKPLIB
/.##実行PG名称ｾｯﾄ##./
    ?CLNM := '出荷検品データ取込・チェック'

/.##ログインユーザー情報取得##./
STEP030:

    ?STEP :=   'STEP030'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND
    END
    ?TANCD := ?TANCD8

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?JISOKO,?DSOKO)
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END

/.##ﾌｧｲﾙ名称取得##./
FILNMCHG:

    ?MSGX := '## 実行倉庫ｺｰﾄﾞ = ' && ?JISOKO && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    /.検品結果データSF./
    ?FILNMH   :=    ?FILNMH1
    ?FILNM    :=    ?FILNMH && ?JISOKO    /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?RCVCZDF  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?RCVCZDFN :=    %STRING(?RCVCZDF)
    ?MSGX     :=    '## 検品結果 = ' && ?RCVCZDFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    /.検品結果集計ファイルPF./
    ?FILNMH   :=    ?FILNMH2
    ?FILNM    :=    ?FILNMH && ?JISOKO    /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZSUMF   :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZSUMFN  :=    %STRING(?CZSUMF)
    ?MSGX     :=    '## 結果集計 = ' && ?CZSUMFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    /.検品結果集計ファイルL1./
    ?FILNMH  :=  ?FILNMH2
    ?FILNM   :=  ?FILNMH && ?JISOKO && '1'  /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID   :=  %NAME(?FILNM)              /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID   :=  %NAME(?LIBNM)              /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZSUM1  :=  %NCAT(?FILID,?LIBID)       /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZSUM1N :=  %STRING(?CZSUM1)
    ?MSGX    :=  '## 結果集計 = ' && ?CZSUM1N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    /.検品結果集計ファイルL2./
    ?FILNMH  :=  ?FILNMH2
    ?FILNM   :=  ?FILNMH && ?JISOKO && '2'  /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID   :=  %NAME(?FILNM)              /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID   :=  %NAME(?LIBNM)              /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZSUM2  :=  %NCAT(?FILID,?LIBID)       /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZSUM2N :=  %STRING(?CZSUM2)
    ?MSGX    :=  '## 結果集計 = ' && ?CZSUM2N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    /.検品結果集計ファイルL3./
    ?FILNMH  :=  ?FILNMH2
    ?FILNM   :=  ?FILNMH && ?JISOKO && '3'  /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID   :=  %NAME(?FILNM)              /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID   :=  %NAME(?LIBNM)              /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZSUM3  :=  %NCAT(?FILID,?LIBID)       /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZSUM3N :=  %STRING(?CZSUM3)
    ?MSGX    :=  '## 結果集計 = ' && ?CZSUM3N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    /.検品紐付情報ファイルPF./
    ?FILNMH   :=    ?FILNMH3
    ?FILNM    :=    ?FILNMH && ?JISOKO    /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZKONF   :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZKONFN  :=    %STRING(?CZKONF)
    ?MSGX     :=    '## 紐付情報 = ' && ?CZKONFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    /.検品紐付情報ファイルL1./
    ?FILNMH  :=  ?FILNMH3
    ?FILNM   :=  ?FILNMH && ?JISOKO && '1'  /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID   :=  %NAME(?FILNM)              /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID   :=  %NAME(?LIBNM)              /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?CZKON1  :=  %NCAT(?FILID,?LIBID)       /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?CZKON1N :=  %STRING(?CZKON1)
    ?MSGX    :=  '## 紐付情報 = ' && ?CZKON1N && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾌｧｲﾙ排他##./

    ?STEP :=   'ASSIGN'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ASSIGN    FILE-?RCVCZDF!@XCL
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=  1020  THEN
       IF     ?PGMEC    ^=   0    THEN
              SNDMSG '他で連携処理が行われています',TOWS-@ORGWS
              SNDMSG '検品結果データファイルロック',TOWS-@ORGWS
              GOTO ABEND
       END
    END

    ASSIGN    FILE-?CZSUMF!@XCL
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=  1020  THEN
       IF     ?PGMEC    ^=   0    THEN
              SNDMSG '他で連携処理が行われています',TOWS-@ORGWS
              SNDMSG '検品結果集計ファイルロック　',TOWS-@ORGWS
              GOTO ABEND
       END
    END

    ASSIGN    FILE-?CZKONF!@XCL
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=  1020  THEN
       IF     ?PGMEC    ^=   0    THEN
              SNDMSG '他で連携処理が行われています',TOWS-@ORGWS
              SNDMSG '検品紐付情報ファイルロック　',TOWS-@ORGWS
              GOTO ABEND
       END
    END

/.##出荷検品確定データ取込指示入力##./
NKE0060I:

    ?STEP :=   'NKE0060I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKMDLIB
    OVRF      FILE-RCVCZDXX,TOFILE-?RCVCZDF
    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRF      FILE-TANMS1,TOFILE-TANMS1.TOKKLIB
    CALL      PGM-NKE0060I.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?DATKBN,?HAKKBN,?OUTKBN,
                                          ?TDATE,?TTIME)
  /.?TDATE    :=    '00000000'
    ?TTIME    :=    '0000'    ./

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
         IF   ?PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              GOTO RTN
         ELSE
              GOTO ABEND
         END
    END
    SNDMSG ?BUMON,TO-XCTL
    SNDMSG ?TANCD,TO-XCTL
    SNDMSG ?JISOKO,TO-XCTL
    SNDMSG ?DATKBN,TO-XCTL
    SNDMSG ?HAKKBN,TO-XCTL
    SNDMSG ?OUTKBN,TO-XCTL
    SNDMSG ?TDATE,TO-XCTL
    SNDMSG ?TTIME,TO-XCTL

/.現状　ＴＫのみがカインズＴＣ出荷倉庫./
    IF   ?DATKBN   ^=   '2' THEN
         SNDMSG MSG-'##########################'
                                  ,TO-XCTL.@ORGPROF,JLOG-@YES
         SNDMSG MSG-'##カインズ以外は無効です##'
                                  ,TO-XCTL.@ORGPROF,JLOG-@YES
         SNDMSG MSG-'##########################'
                                  ,TO-XCTL.@ORGPROF,JLOG-@YES
         GOTO ABEND
    END

/.##出荷検品結果集計##./
NKE1040B:

    ?STEP :=   'NKE1040B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ?MSGX :=  'SORTF-IN   '
    SNDMSG    ?MSGX,TO-XCTL
    /. データＳＯＲＴ ./
    SORTF  INFILE-?RCVCZDF,
           OUTFILE-?RCVCZDF,
       /.  RCD-50,  ./
           RCD-10,
           KEY-'F01!A',             /.バッチ（日付）./
           KEY1-'F02!A',            /.バッチ（時刻）./
           KEY2-'F03!A',            /.バッチ（取引先ＣＤ）./
           KEY3-'F04!A',            /.倉庫ＣＤ./
           KEY4-'F05!A',            /.センター納品日./
           KEY5-'F06!A',            /.センターＣＤ./
           KEY6-'F07!A',            /.店舗納品日./
           KEY7-'F08!A',            /.店舗ＣＤ./
           KEY8-'F10!A',            /.伝票番号./
           KEY9-'F11!A',            /.行番号./
           RCDL-@DSP
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND
    END
    ?MSGX :=  'SORTF-OUT  '
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-RCVCZDXX,TOFILE-?RCVCZDF
    OVRF      FILE-CZSUMXX,TOFILE-?CZSUMF
    OVRF      FILE-CZKONXX,TOFILE-?CZKONF
    CALL      PGM-NKE1040B.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?TDATE,?TTIME)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##梱包数カウント##./
NKE1041B:

    ?STEP :=   'NKE1041B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ?MSGX :=  'SORTF-IN   '
    SNDMSG    ?MSGX,TO-XCTL
    /. データＳＯＲＴ ./
    SORTF  INFILE-?RCVCZDF,
           OUTFILE-?RCVCZDF,
       /.  RCD-50,  ./
           RCD-10,
           KEY-'F01!A',             /.バッチ（日付）./
           KEY1-'F02!A',            /.バッチ（時刻）./
           KEY2-'F03!A',            /.バッチ（取引先ＣＤ）./
           KEY3-'F04!A',            /.倉庫ＣＤ./
           KEY4-'F05!A',            /.センター納品日./
           KEY5-'F06!A',            /.センターＣＤ./
           KEY6-'F09!A',            /.梱包_./
           KEY7-'F08!A',            /.店舗ＣＤ./
           KEY8-'F10!A',            /.伝票番号./
           KEY9-'F11!A',            /.行番号./
           RCDL-@DSP
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND
    END
    ?MSGX :=  'SORTF-OUT  '
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-RCVCZDXX,TOFILE-?RCVCZDF
    OVRF      FILE-CZKONXX1,TOFILE-?CZKON1
    CALL      PGM-NKE1041B.TOKSOLIB

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##出荷検品結果チェック##./
NKE1050B:

    ?STEP :=   'NKE1050B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-CZSUMXX1,TOFILE-?CZSUM1
    OVRF      FILE-SHTDENLA,TOFILE-SHTDENLA.TOKFLIB
    OVRF      FILE-ZAMZAIL1,TOFILE-ZAMZAIL1.TOKFLIB
    OVRF      FILE-BMSHACL1,TOFILE-BMSHACL1.BMSFLIB
    CALL      PGM-NKE1050B.TOKSOLIB,PARA-(?KEKKA)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              GOTO ABEND END

/.##リスト出力先制御##./
CHGSPLQ:

    ?STEP :=  'CHGSPLQ '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    CASE      ?OUTKBN   OF
    /.レーザー./
    /.現地./
    /. #'1'#
                  SNDMSG '＃本社レーザーへ出力　　　＃',
                   TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
                  OVRPRTF FILE-XU04LP,TOFILE-????????.????????
              END  ./                  /.↑実機設定に応じ変更./
    /.社内./
       #'1'#  OVRPRTF FILE-XU04LP,TOFILE-XU04LP.XUCL

    /.連帳./                           /.↓実機はXU04LPでOK？./
       #' '#  OVRPRTF FILE-XU04LP,TOFILE-XU04LP.XUCL
    END

/.##出荷検品取込リスト発行##./
NKE1060L:

    ?STEP :=   'NKE1060L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'

    OVRF      FILE-CZSUMXX2,TOFILE-?CZSUM2
    CALL      PGM-NKE1060L.TOKSOLIB,PARA-(?BUMON,?TANCD,?JISOKO,
                                          ?HAKKBN,?TDATE,?TTIME)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              GOTO ABEND END

RTN:

    ?MSGX :=  '***   '  && ?CLID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-?PGMEC

ABEND:

 /. ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM  ./
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=    '### ' && ?CLID && ' ABEND' && ' ###'
    ?MSG(2)   :=    '### ' && ' PGMEC = ' &&
                     %SBSTR(?PGMECX,8,4) && ' ###'
    ?MSG(3)   :=    '###' && ' LINE = '  && %LAST(LINE)      && ' ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

```
