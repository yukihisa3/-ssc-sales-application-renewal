# PNJ80500

**種別**: JCL  
**ライブラリ**: TOKCLLIB  
**ソースファイル**: `source/navs/cobol/programs/TOKCLLIB/PNJ80500.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　新基幹システム                        *  ./
/. *   SYSTEM-NAME :    基幹　　　　　　　　　　　　         *  ./
/. *   JOB-ID      :    PNJ80500                             *  ./
/. *   JOB-NAME    :    オンライン伝票変換エラーデータCSV出力*  ./
/. ***********************************************************  ./
    PGM
    VAR       ?PGMEC    ,INTEGER
    VAR       ?PGMECX   ,STRING*11
    VAR       ?PGMEM    ,STRING*99
    VAR       ?MSG      ,STRING*99(6)
    VAR       ?MSGX     ,STRING*99
    VAR       ?PGMID    ,STRING*8,VALUE-'PNJ80500'
    VAR       ?STEP     ,STRING*8
    VAR       ?SOKO     ,STRING*2
    VAR       ?JISOKO   ,STRING*2
    VAR       ?OUTKBN   ,STRING*1,VALUE-' '
    VAR       ?BDATE    ,STRING*8,VALUE-'        '
    VAR       ?BTIME    ,STRING*4,VALUE-'    '
    VAR       ?BTORI    ,STRING*8,VALUE-'        '
    VAR       ?WKSTN    ,STRING*8
    VAR       ?NWKSTN   ,NAME
    VAR       ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?KEKA1    ,STRING*40                    /.      2    ./
    VAR       ?KEKA2    ,STRING*40                    /.      3    ./
    VAR       ?KEKA3    ,STRING*40                    /.      4    ./
    VAR       ?KEKA4    ,STRING*40                    /.      5    ./
    ?NWKSTN    :=       @ORGWS
    ?WKSTN    :=        %STRING(?NWKSTN)

    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM :=  'オンライン伝票変換エラーＣＳＶ出力'
/.ﾗｲﾌﾞﾗﾘﾘｽﾄ登録./
    DEFLIBL    TOKFLIB/TOKELIB/TOKELIBO/TOKSOLIB/TOKMDLIB/
               TOKDTLIB
/.倉庫ＣＤ取得./
SKY1601B:
    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WKSTN,?SOKO,?JISOKO)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【倉庫ＣＤ取得】'
              GOTO ABEND END


/.オンライン伝票変換エラーデータCSV指示./
SNJ8050I:
    ?STEP :=   'SNJ8050I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKMDLIB
    OVRF      FILE-ONLERRL1,TOFILE-ONLERRL1.TOKDTLIB
    CALL      PGM-SNJ8050I.TOKSOLIB,PARA-(?BDATE,?BTIME,?BTORI)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?MSGX :=  '**  バッチ(日) = ' && ?BDATE && '      **'
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '**  バッチ(時) = ' && ?BTIME && '          **'
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '**  バッチ(取) = ' && ?BTORI && '      **'
    SNDMSG    ?MSGX,TO-XCTL
    IF        ?PGMEC    ^=   0    THEN
              IF  ?PGMEC     =   4010 THEN
                  SNDMSG    '## 取 消 ##',TO-XCTL
                  GOTO RTN
              ELSE
                  ?KEKA4 :=  '【エラーデータＣＳＶ指示】'
                  GOTO ABEND
              END
    END

/.オンライン伝票変換エラーデータCSV出力./
SNJ8060V:
    ?STEP :=   'SNJ8060V'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ONLERRL1,TOFILE-ONLERRL1.TOKDTLIB
    OVRF      FILE-ONLERRCV,TOFILE-ONLERRCV.TOKDTLIB
    CALL      PGM-SNJ8060V.TOKSOLIB,PARA-(?BDATE,?BTIME,?BTORI)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【エラーデータＣＳＶ出力】'
              GOTO ABEND END

/.##データＰＣ転送##./
PFIMPORT:

    ?STEP :=   'PFIMPORT'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FIMPORT FILE-ONLERRCV.TOKDTLIB,PARA-ONLERR,UNIT-1
    ?PGMEC    :=   @PGMEC
    ?PGMEM    :=   @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【エラーデータＣＳＶ転送】'
              GOTO ABEND END

RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-?PGMEC

ABEND:

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    ?KEKA1 :=  '処理が異常終了しました。'
    ?KEKA2 :=  'ログリストを採取後、ＮＡＶへ連絡'
    ?KEKA3 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
  /.?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM   ./
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-?PGMEC

```
