# PED00200

**種別**: JCL  
**ライブラリ**: TOKCLIBS  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIBS/PED00200.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　特販システム（本社システム）          *  ./
/. *   SYSTEM-NAME :    ＥＤＩＣオンライン構築　　　　　　　 *  ./
/. *   JOB-ID      :    PED00200                             *  ./
/. *   JOB-NAME    :    ＥＤＩＣ返品明細書                   *  ./
/. *               :                                         *  ./
/. ***********************************************************  ./
    PGM
    VAR ?PGMEC    ,INTEGER
    VAR ?PGMECX   ,STRING*11
    VAR ?PGMEM    ,STRING*99
    VAR ?MSG      ,STRING*99(6)
    VAR ?MSGX     ,STRING*99
    VAR ?PGMID    ,STRING*8,VALUE-'PED00200'
    VAR ?STEP     ,STRING*8
    VAR ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1    ./
    VAR ?KEKA1    ,STRING*40                    /.      2    ./
    VAR ?KEKA2    ,STRING*40                    /.      3    ./
    VAR ?KEKA3    ,STRING*40                    /.      4    ./
    VAR ?KEKA4    ,STRING*40                    /.      5    ./

    VAR ?OUTPTN   ,STRING*1,VALUE-'0'           /.出力パターン./
    VAR ?TORICD   ,STRING*8,VALUE-'00000000'    /.取引先./
    VAR ?JYUDT    ,STRING*8,VALUE-'00000000'    /.受信日./
    VAR ?JTIME    ,STRING*4,VALUE-'0000'        /.受信時刻./

/.##実行PG名称ｾｯﾄ##./
    ?PGNM := 'ＥＤＩＣ　返品明細書発行'

/.## ﾗｲﾌﾞﾗﾘﾘｽﾄ登録 ##./
    DEFLIBL   TOKELIB/TOKFLIB/TOKELIBO/TOKDLIB/TOKWLIB/TOKKLIB

    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.##ＥＤＩＣ返品明細書発行指示##./
SED0020I:

    ?STEP :=   'SED0020I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF  FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIBO
    CALL  PGM-SED0020I.TOKELIBO,PARA-(?OUTPTN,?TORICD,?JYUDT,
                                      ?JTIME)
    ?PGMEC := @PGMEC
    ?PGMEM := @PGMEM
    IF        ?PGMEC    ^=   0    THEN
         IF   ?PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              GOTO RTN
         ELSE
              ?KEKA4 := 'ＥＤＩＣ返品明細書発行指示'
              GOTO ABEND
         END
    END


    ?MSGX :=  '出力種類＝'  && '？？？'
    IF      ?OUTPTN = '1'  THEN
            ?MSGX :=  '出力種類＝'  && '帳票'
    END
    IF      ?OUTPTN = '2'  THEN
            ?MSGX :=  '出力種類＝'  && 'ＣＳＶ'
    END
    SNDMSG    ?MSGX,TO-XCTL

    ?MSGX :=  '取引先　＝'  && ?TORICD
    SNDMSG    ?MSGX,TO-XCTL


    ?MSGX :=  '受信日付＝'  && ?JYUDT
    SNDMSG    ?MSGX,TO-XCTL

    ?MSGX :=  '受信時刻＝'  && ?JTIME
    SNDMSG    ?MSGX,TO-XCTL

/.##ＥＤＩＣ受領データ抽出##./
SED0020W:

    ?STEP :=   %LAST(LABEL)
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL     PGM-SED0020W.TOKELIBO,PARA-(?TORICD,?JYUDT,
                                         ?JTIME)
    ?PGMEC := @PGMEC
    ?PGMEM := @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := 'ＥＤＩＣ受領データ抽出'
              GOTO ABEND
    END
/.##出力区分が２の場合、CSV出力へ##./
    ?MSGX :=  'OUTPTN = '  && ?OUTPTN
    SNDMSG    ?MSGX,TO-XCTL
    IF        ?OUTPTN   =   '2'     THEN
              GOTO SED0020V
    END

/.##ＥＤＩＣ返品明細書発行##./
SED0020L:

    ?STEP :=   'SED0020L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-BMSJYRW1,TOFILE-BMSJYRW1.BMSFLIB
    CALL      PGM-SED0020L.TOKELIBO,PARA-(?OUTPTN,?TORICD,
                                          ?JYUDT,?JTIME)
    ?PGMEC := @PGMEC
    ?PGMEM := @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := 'ＥＤＩＣ返品明細書発行'
              GOTO ABEND
    ELSE
              GOTO RTN
    END

/.##ＥＤＩＣ返品明細書ＣＳＶデータ出力##./
SED0020V:

    ?STEP :=   'SED0020V'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CALL      PGM-SED0020V.TOKELIBO
    ?PGMEC := @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := 'ＥＤＩＣ返品明細書ＣＳＶデータ出力'
              GOTO ABEND
    END

/.##ＣＳＶデータ転送##./
PFIMPORT:

    ?STEP :=   'PFIMPORT'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FIMPORT FILE-EWHENPWK.TOKWLIB,PARA-EDIC,UNIT-2
    ?PGMEC := @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              ?KEKA4 := 'ＣＳＶデータ転送'
              GOTO ABEND
    END

RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:

    ?PGMECX   :=    %STRING(?PGMEC)

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    ?KEKA1 :=  '処理が異常終了しました。'
    ?KEKA2 :=  'ログリストを採取後、ＮＡＶへ連絡'
    ?KEKA3 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'

    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
