# PSY39300

**種別**: JCL  
**ライブラリ**: TOKCLIBS  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIBS/PSY39300.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *  （株）サカタのタネ                                     *  ./
/. *   SYSTEM-NAME :    ナフコ出荷支援　　　　　　　　       *  ./
/. *   JOB-NAME    :    ナフコ店舗パターンマスタ　　　　　   *  ./
/. *                    取込チェック・更新                   *  ./
/. *   JOB-ID      :    PSY39300  2016/08/23                 *  ./
/. *   UPDATE      :                                         *  ./
/. *                                                         *  ./
/. ***********************************************************  ./
    PGM  (P1-?JIKKOU)

/.##INパラメタ##./
    PARA  ?JIKKOU  ,STRING*1,IN,VALUE-' '
            /.実行区分                               ./
            /.  '1'    :   チェックのみ　　　　     ./
            /.  '2'    :   チェック・更新　　　　   ./

/.--------------------------------------------------------------./
    VAR  ?WS      ,STRING*8,VALUE-'        ' /.ﾜｰｸｽﾃｰｼｮﾝID./
    VAR  ?WKSTN   ,NAME!MOD                  /.ﾜｰｸｽﾃｰｼｮﾝID./
    VAR  ?PGMEC   ,INTEGER
    VAR  ?PGMES   ,STRING*5,VALUE-'     '
    VAR  ?PGMECX  ,STRING*11
    VAR  ?PGMEM   ,STRING*99
    VAR  ?MSG     ,STRING*99(6)
    VAR  ?MSGX    ,STRING*99
    VAR  ?PGMID   ,STRING*8,VALUE-'PSY39300'
    VAR  ?STEP    ,STRING*8
/.--------------------------------------------------------------./
    VAR  ?BUMON   ,STRING*4,VALUE-'    '     /.部門./
    VAR  ?TANCD8  ,STRING*8,VALUE-'        ' /.部門+担当者./
    VAR  ?TANCD   ,STRING*2,VALUE-'  '       /.担当者     ./
    VAR  ?SOKCD   ,STRING*2,VALUE-'00'       /.実行倉庫   ./
    VAR  ?DSOKCD  ,STRING*2,VALUE-'00'       /.代表倉庫   ./
/.--------------------------------------------------------------./
    VAR  ?CNT1    ,STRING*7,VALUE-'0000000'  /.件数(取込) ./
    VAR  ?CNT2    ,STRING*7,VALUE-'0000000'  /.件数(ＥＲ）./
/.--------------------------------------------------------------./
    VAR  ?OPR1    ,STRING*50                 /.ﾒｯｾｰｼﾞ1    ./
    VAR  ?OPR2    ,STRING*50                 /.      2    ./
    VAR  ?OPR3    ,STRING*50                 /.      3    ./
    VAR  ?OPR4    ,STRING*50                 /.      4    ./
    VAR  ?OPR5    ,STRING*50                 /.      5    ./
/.--------------------------------------------------------------./
    VAR  ?MSG1    ,STRING*80                 /.開始終了MSG./
    VAR  ?PGNM    ,STRING*40                 /.ﾒｯｾｰｼﾞ1    ./
    VAR  ?KEKA1   ,STRING*40                 /.      2    ./
    VAR  ?KEKA2   ,STRING*40                 /.      3    ./
    VAR  ?KEKA3   ,STRING*40                 /.      4    ./
    VAR  ?KEKA4   ,STRING*40                 /.      5    ./
/.---------------------------------------------------------------./

    VAR   ?FILNM7  ,STRING*7,VALUE-'       '
    VAR   ?FILNM8  ,STRING*8,VALUE-'        '
    VAR   ?FILID   ,NAME
    VAR   ?LIBID   ,NAME


/.-----------------------------------------------------------./

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL   TOKELIB/TOKELIBO/TOKDLIB/TOKFLIB/TOKKLIB

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
CLSTART:
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    CASE  ?JIKKOU     OF
          #'1'# ?PGNM := 'ＥＸＣＥＬ取込_チェックのみ'
          #'2'# ?PGNM := 'ＥＸＣＥＬ取込_チェック・更新'
          ELSE  SNDMSG   '！！！！！！！！！！！！！！！！！！',
                          TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
                SNDMSG   '！実行区分を指定し、実行して下さい！',
                          TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
                SNDMSG   '！！！！！！！！！！！！！！！！！！',
                          TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
                RETURN
    END

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

/.##ログインユーザー情報取得##./
SIT9000B:

    ?STEP :=  'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD8)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMES    :=    @PGMES
    IF        ?PGMEC ^=   0    THEN
              GOTO   ABEND
    END
    ?TANCD := %SBSTR(?TANCD8,1,2)

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKCD,?DSOKCD)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMES    :=    @PGMES
    IF        ?PGMEC ^=   0   THEN
              ?KEKA4 := '倉庫コード取得'
              GOTO   ABEND
    END


/.##使用ファイルロック##./
ASSIGN:

    ?STEP :=  'ASSIGN'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES


    /.EXCEL取込ファイル　全実行区分共通./

    ASSIGN FILE-NFTEPCSV.TOKDLIB!@XCL
   /.PGMEC :=    @PGMEC
    ?PGMEM :=    @PGMEM
    ?PGMES :=    @PGMES
    IF     ?PGMEC ^=   1020 THEN
       IF  ?PGMEC ^=   0    THEN
           SNDMSG '他で連携処理が行われています',
                   TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
           ?KEKA4 :=  'ＥＸＣＥＬデータロック'
           GOTO   ABEND1
       END
    END ↑このファイルはノーチェック./

    ASSIGN FILE-NFTEPWK.TOKDLIB!@XCL
    ?PGMEC :=    @PGMEC
    ?PGMEM :=    @PGMEM
    ?PGMES :=    @PGMES
    IF     ?PGMEC ^=   1020 THEN
       IF  ?PGMEC ^=   0    THEN
           SNDMSG '他で連携処理が行われています',
                   TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
           ?KEKA4 :=  'ＥＸＣＥＬ取込ファイルロック'
           GOTO   ABEND1
       END
    END
    CLRFILE FILE-NFTEPWK.TOKDLIB

    CASE  ?JIKKOU    OF
          #'2'# /.チェック・更新     ./

                ASSIGN FILE-NFTEPTF.TOKDLIB!@XCL
                ?PGMEC :=    @PGMEC
                ?PGMEM :=    @PGMEM
                ?PGMES :=    @PGMES
                IF     ?PGMEC ^=   1020 THEN
                   IF  ?PGMEC ^=   0    THEN
                       SNDMSG '他で連携処理が行われています',
                              TOWS-@ORGWS,JLOG-@YES,SLOG-@YES
                       ?KEKA4 :=  '店舗パターンロック'
                       GOTO   ABEND1
                   END
                END

              /.CLRFILE FILE-NFTEPTF.TOKDLIB./

    END
/.##取込確認画面##./
SHOM0900:

    ?STEP :=  'SHOM0900'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    CASE      ?JIKKOU    OF
       #'1'#
           ?OPR1 := '＜ナフコ店舗パターンマスタ　チェック＞'
           ?OPR2 := ''
           ?OPR3 := '　ＥＸＣＥＬデータの取り込みを行います。'
           ?OPR4 := '　　・連携するデータは出力済ですか？'
           ?OPR5 := '　　・所定フォルダにセット済ですか？'
       #'2'#
           ?OPR1 := '＜ナフコ店舗パターンマスタ　（更新）＞'
           ?OPR2 := '　　　　　！！確認して下さい！！'
           ?OPR3 := '　 ＥＸＣＥＬデータの取込＆更新を行います'
           ?OPR4 := '　・更新まで実施されるため注意願います。'
           ?OPR5 := '　・チェック処理はエラーなく終了していますか？'
    END

    CALL   OHOM0900.TOKELIB,
           PARA-(?OPR1,?OPR2,?OPR3,?OPR4,?OPR5)
    ?PGMEC := @PGMEC
    ?PGMES := @PGMES
    IF        ?PGMEC ^= 0    THEN
              ?KEKA4 := '処理確認画面'
              GOTO   ABEND
    END

/.##ＥＸＣＥＬデータ取込処理##./
PFEXPORT:

    ?STEP :=  'PFEXPORT'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

                FEXPORT FILE-NFTEPCSV.TOKDLIB,
                        MODE-@REP,
                        PARA-NFHACCSK,
                        UNIT-6
                ?PGMEC  := @PGMEC
                ?PGMES  := @PGMES
                IF      ?PGMEC ^= 0    THEN
                       ?KEKA4 := 'ＥＸＣＥＬ（店舗）取込異常！！'
                        GOTO   ABEND
                END

/.##店舗パターンＥＸＣＥＬデータチェック##./
SSY3930B:

    ?STEP :=  'SSY3930B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

             /. OVRF   FILE-NFTEPCSV,TOFILE-NFTEPCSV.TOKDLIB
                OVRF   FILE-NFTEPWK2,TOFILE-NFTEPWK2.TOKDLIB
                OVRF   FILE-NFTENMS1,TOFILE-NFTENMS1.TOKDLIB
                OVRF   FILE-NFSHBPL1,TOFILE-NFSHBPL1.TOKDLIB./
                CALL   PGM-SSY3930B.TOKELIBO,
                       PARA-(?BUMON,?TANCD,
                             ?CNT1,?CNT2)
                ?PGMEC := @PGMEC
                ?PGMES := @PGMES
                IF        ?PGMEC ^= 0    THEN
                          ?KEKA4 := 'データチェック'
                          GOTO   ABEND
                END


    IF  ?CNT2 ^=  '0000000'  THEN   /.##エラーがあった場合##./
     /. ?MSGX :=  '##################################'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '#　エラーチェックにおいてエラー　#'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '#　箇所があります。エラーリスト　#'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '#　を確認し修正／再取込を行って　#'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '#　下さい。　　　　　　　　　　　#'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '##################################'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES./

        GOTO  SSY3931L

    END
    IF  ?CNT1  =  '0000000'  THEN        /.CSVゼロ件時は終了./
        IF  ?CNT2  =  '0000000'  THEN
            SNDMSG 'ＥＸＣＥＬデータ０件です！',
                   ,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
            RETURN
        END
    END

/.##ＥＸＣＥＬ取込チェックリスト##./
SSY3931L:

    ?STEP :=  'SSY3931L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

  /.OVRPRTF FILE-XU04LP,TOFILE-AMAGILBP.XUCL       ./
  /.OVRF   FILE-NFTEPWK1,TOFILE-NFTEPWK1.TOKDLIB
    OVRF   FILE-TANMS1,TOFILE-TANMS1.TOKKLIB       ./
    CALL      PGM-SSY3931L.TOKELIBO,
              PARA-(?BUMON,?TANCD)
    ?PGMEC    := @PGMEC
    ?PGMES    := @PGMES
    IF        ?PGMEC ^= 0    THEN
              ?KEKA4 := '取込チェックリスト'
              GOTO   ABEND
    END

    IF  ?JIKKOU  =  '1'  THEN   /.##実行区分：チェック##./
        ?MSGX :=  '##################################'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '#　チェックのみを行ないました。　#'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '#　チェックＯＫの場合は、更新処　#'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '#　理を開始して下さい。　　　　　#'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        ?MSGX :=  '##################################'
        SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
        GOTO  RTN
    END


    IF  ?JIKKOU  =  '2'  THEN   /.##実行区分：チェック・更新##./
        IF  ?CNT2  =  '0000000'  THEN
             GOTO  SSY3932B
        ELSE
             GOTO  RTN
        END
    END

/.##データ更新処理##./
SSY3932B:

    ?STEP :=  'SSY3932B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    CASE  ?JIKKOU  OF

          #'2'# /.チェック・更新　   ./
              /.OVRF   FILE-NFTEPTF,TOFILE-NFTEPTF.TOKDLIB ./
                CALL   PGM-SSY3932B.TOKELIBO,
                       PARA-(?BUMON,?TANCD)
                ?PGMEC := @PGMEC
                ?PGMES := @PGMES
                IF     ?PGMEC ^= 0    THEN
                       ?KEKA4 := 'データ更新'
                       GOTO   ABEND
                END

    END

RTN:

    RELEASE FILE-NFTEPCSV.TOKDLIB!@XCL/NFTEPTF.TOKDLIB!@XCL/
                 NFTEPWK.TOKDLIB!@XCL

    CASE ?JIKKOU OF
       #'1'#  ?KEKA1 :=  '※ナフコ店舗パターンマスタ'
              ?KEKA2 :=  '　ＥＸＣＥＬデータ取込処理が'
              ?KEKA3 :=  '　正常終了しました。'
              ?KEKA4 :=  '　（チェックのみ）'
       #'2'#  ?KEKA1 :=  '※ナフコ店舗パターンマスタ'
              ?KEKA2 :=  '　ＥＸＣＥＬデータ取込処理が'
              ?KEKA3 :=  '　正常終了しました。'
              ?KEKA4 :=  '　（チェック・更新）'
    END

    IF        ?CNT2  ^=  '0000000'  THEN
              ?KEKA1 :=  'ナフコ店舗パターン　データチェック'
              ?KEKA2 :=  '　エラー箇所があります　　　　　　　　'
              ?KEKA3 :=  '　チェックリストを確認し、修正／再取込'
              ?KEKA4 :=  '　を行って下さい。'
    END

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES

    RETURN    PGMEC-@PGMEC

ABEND:

    RELEASE FILE-NFTEPCSV.TOKDLIB!@XCL/NFTEPTF.TOKDLIB!@XCL/
                 NFTEPWK.TOKDLIB!@XCL

    ?KEKA1 :=  '店舗パターンＥＸＣＥＬデータ取込が'
    ?KEKA2 :=  '異常終了しました。'
    ?KEKA3 :=  'ログ採取し，ＮＡＶへ連絡して下さい。'
    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO    ?MSGX  := ?MSG(?I)
              SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
    END

    RETURN    PGMEC-?PGMEC

ABEND1:

    RELEASE FILE-NFTEPCSV.TOKDLIB!@XCL/NFTEPTF.TOKDLIB!@XCL/
                 NFTEPWK.TOKDLIB!@XCL

    ?KEKA1 :=  'ＥＸＣＥＬデータ取込処理関係ファイ'
    ?KEKA2 :=  'ルが、他で使用されています。'
    ?KEKA3 :=  '他端末で使用中でないか確認して下さい。'
    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    CALL SMG0030I.TOKELIB,
         PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO    ?MSGX  := ?MSG(?I)
              SNDMSG    ?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES,SLOG-@YES
    END

    RETURN    PGMEC-?PGMEC

```
