# PSK00300

**種別**: JCL  
**ライブラリ**: TOKCLIBS  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIBS/PSK00300.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　ＨＧ基幹システム　　　　　　          *  ./
/. *   SYSTEM-NAME :    ケーヨー伝票レスシステム  　　　     *  ./
/. *   JOB-ID      :    PSK00300                             *  ./
/. *   JOB-NAME    :    売上、受領マッチング　　　　　　　　 *  ./
/. *               :                                         *  ./
/. ***********************************************************  ./
    PGM     (P1-?PRTKBN)

/.### ﾊﾟﾗﾒﾀ 定義 ###./
    PARA ?PRTKBN  ,STRING*1,IN,VALUE-' '        /.用紙区分 ./

/.### ﾜｰｸｴﾘｱ定義 ###./
    VAR ?PGMEC    ,INTEGER                      /.ｴﾗｰｽﾃｲﾀｽ./
    VAR ?PGMECA   ,INTEGER                      /.ｴﾗｰｽﾃｲﾀｽ./
    VAR ?PGMECB   ,INTEGER                      /.ｴﾗｰｽﾃｲﾀｽ./
    VAR ?PGMECC   ,INTEGER                      /.ｴﾗｰｽﾃｲﾀｽ./
    VAR ?PGMECX   ,STRING*11                    /.ｴﾗｰｽﾃｲﾀｽ変換./
    VAR ?PGMEM    ,STRING*99                    /.ｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ定義./
    VAR ?MSGX     ,STRING*99                    /.ﾒｯｾｰｼﾞ定義変換./
    VAR ?PGMID    ,STRING*8,VALUE-'PSK00300'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.ﾌﾟﾛｸﾞﾗﾑｽﾃｯﾌﾟ./
    VAR ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1./
    VAR ?KEKA1    ,STRING*40                    /.      2./
    VAR ?KEKA2    ,STRING*40                    /.      3./
    VAR ?KEKA3    ,STRING*40                    /.      4./
    VAR ?KEKA4    ,STRING*40                    /.      5./
    VAR ?BUMON    ,STRING*4,VALUE-'    '        /.部門名./
    VAR ?TANCD    ,STRING*2,VALUE-'  '          /.担当者CD./
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
/. 共通パラメタ./
    VAR ?SKBN     ,STRING*1,VALUE-' '          /. 出力区分 ./
    VAR ?DKBN     ,STRING*1,VALUE-' '          /. 日付区分 ./
    VAR ?DFROM    ,STRING*8,VALUE-'        '   /. 日付Ｆ   ./
    VAR ?DTO      ,STRING*8,VALUE-'        '   /. 日付Ｔ   ./
    VAR ?KKBN     ,STRING*1,VALUE-' '          /. 計上区分 ./
    VAR ?TANF     ,STRING*2,VALUE-'  '         /. 担当者Ｆ ./
    VAR ?TANT     ,STRING*2,VALUE-'  '         /. 担当者Ｔ ./
    VAR ?DENK1    ,STRING*2,VALUE-'  '         /. 伝区１   ./
    VAR ?DENK2    ,STRING*2,VALUE-'  '         /. 伝区２   ./
    VAR ?DENK3    ,STRING*2,VALUE-'  '         /. 伝区３   ./
    VAR ?DENK4    ,STRING*2,VALUE-'  '         /. 伝区４   ./
    VAR ?DENK5    ,STRING*2,VALUE-'  '         /. 伝区５   ./
    VAR ?TENF     ,STRING*5,VALUE-'     '      /. 店舗Ｆ   ./
    VAR ?TENT     ,STRING*5,VALUE-'     '      /. 店舗Ｔ   ./
    VAR ?DENNF    ,STRING*9,VALUE-'         '  /. 伝番Ｆ   ./
    VAR ?DENNT    ,STRING*9,VALUE-'         '  /. 伝番Ｔ   ./
    VAR ?SKBF     ,STRING*2,VALUE-'  '         /. 出場Ｆ   ./
    VAR ?SKBT     ,STRING*2,VALUE-'  '         /. 出場Ｔ   ./
    VAR ?DENKF    ,STRING*2,VALUE-'  '         /. 伝区Ｆ　 ./
    VAR ?DENKT    ,STRING*2,VALUE-'  '         /. 伝区Ｔ   ./


/.### ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ ###./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

/.## ﾗｲﾌﾞﾗﾘﾘｽﾄ登録 ##./
    DEFLIBL TOKELIB/TOKFLIB/TOKELIBO/TOKKLIB

/.## ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ ##./
    ?PGNM :=  'ケーヨー　売上・受領マッチング'

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得 ##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

/.## ログインユーザー情報取得 ##./
SIT9000B:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ログイン情報取得'
              GOTO ABEND
    END

/.## 受領アンマッチリスト発行指示 ##./
SSK0030I:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRDSPF FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIBO
    CALL PGM-SSK0030I.TOKELIBO,PARA-(?BUMON,?TANCD,
                                    ?SKBN,?DKBN,?DFROM,?DTO,
                                    ?KKBN,?TANF,?TANT,?DENK1,
                                    ?DENK2,?DENK3,?DENK4,?DENK5,
                                    ?TENF,?TENT,?DENNF,?DENNT,
                                    ?SKBF,?SKBT,?DENKF,?DENKT)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF  ?PGMEC ^= 0  THEN
        IF @PGMEC = 4010  THEN
           SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
           GOTO  RTN
        ELSE
           ?KEKA4 := '受領アンマッチリスト発行指示'
           GOTO  ABEND
        END
    END

    ?MSGX :=  '# 出力区分 = ' && ?SKBN && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 日付区分 = ' && ?DKBN && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 日付Ｆ = ' && ?DFROM && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 日付Ｔ = ' && ?DTO && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 計上区分 = ' && ?KKBN && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 担当者Ｆ = ' && ?TANF && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 担当者Ｔ = ' && ?TANT && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝区１　 = ' && ?DENK1 && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝区２　 = ' && ?DENK2 && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝区３　 = ' && ?DENK3 && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝区４　 = ' && ?DENK4 && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝区５　 = ' && ?DENK5 && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 店舗Ｆ　 = ' && ?TENF && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 店舗Ｔ　 = ' && ?TENT && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝番Ｆ　 = ' && ?DENNF && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝番Ｔ　 = ' && ?DENNT && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 出場Ｆ　 = ' && ?SKBF && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 出場Ｔ　 = ' && ?SKBT && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝区Ｆ　 = ' && ?DENKF && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 伝区Ｔ　 = ' && ?DENKT && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

/.## 出荷実績データ初期化 ##./
CLRFILE1:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    CLRFILE FILE-KEISYKF.TOKKLIB
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF   ?PGMEC    ^=   0    THEN
           ?KEKA4 := '出荷実績データ初期化'
         GOTO ABEND
    END

/.## 出荷実績データ抽出（当月分） ##./
SSY0031B:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-SHTDENLP,TOFILE-SHTDENLP.TOKKLIB
    OVRF FILE-KEISYKF,TOFILE-KEISYKF.TOKKLIB
    CALL SSK0031B.TOKELIBO,PARA-(?SKBN,?DKBN,?DFROM,?DTO,
                                 ?KKBN,?TANF,?TANT,?DENK1,
                                 ?DENK2,?DENK3,?DENK4,?DENK5,
                                 ?TENF,?TENT,?DENNF,?DENNT,
                                 ?SKBF,?SKBT,?DENKF,?DENKT)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECA   :=    ?PGMEC
    IF   ?PGMEC    ^=   0    THEN
       IF  ?PGMECA  ^=  4001 THEN
           ?KEKA4 := '出荷実績データ抽出（当月分）'
           GOTO ABEND
       END
    END

/.## 出荷実績データ抽出（過去分） ##./
SSK0035B:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-KEIURIL3,TOFILE-KEIURIL3.TOKKLIB
    OVRF FILE-KEISYKL1,TOFILE-KEISYKL1.TOKKLIB
    CALL SSK0035B.TOKELIBO,PARA-(?SKBN,?DKBN,?DFROM,?DTO,
                                 ?KKBN,?TANF,?TANT,?DENK1,
                                 ?DENK2,?DENK3,?DENK4,?DENK5,
                                 ?TENF,?TENT,?DENNF,?DENNT,
                                 ?SKBF,?SKBT,?DENKF,?DENKT)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECB   :=    ?PGMEC
    IF   ?PGMEC    ^=   0    THEN
       IF  ?PGMECB  ^=  4001 THEN
           ?KEKA4 := '出荷実績データ抽出（過去分）'
           GOTO ABEND
       END
    END

/.## 受領実績データ抽出 ##./
SSK0021B:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-KEIJYRL3,TOFILE-KEIJYRL3.TOKKLIB
    OVRF FILE-KEIJYRL4,TOFILE-KEIJYRL4.TOKKLIB
    OVRF FILE-KEIJYRL5,TOFILE-KEIJYRL5.TOKKLIB
    OVRF FILE-KEIJYRL6,TOFILE-KEIJYRL6.TOKKLIB
    OVRF FILE-KEIJYWF,TOFILE-KEIJYWF.TOKKLIB
    CALL SSK0021B.TOKELIBO,PARA-(?SKBN,?DKBN,?DFROM,?DTO,
                                 ?KKBN,?TANF,?TANT,?DENK1,
                                 ?DENK2,?DENK3,?DENK4,?DENK5,
                                 ?TENF,?TENT,?DENNF,?DENNT,
                                 ?SKBF,?SKBT,?DENKF,?DENKT)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECC   :=    ?PGMEC
    IF   ?PGMEC    ^=   0    THEN
       IF  ?PGMECC  ^=  4001 THEN
           ?KEKA4 := '受領実績データ抽出'
           GOTO ABEND
       END
    END

    /.## 対象データ無判定 ##./
    IF   ?PGMECA  =  4001  THEN
         IF  ?PGMECB  =  4001  THEN
             IF  ?PGMECC  =  4001  THEN
                 ?MSGX :=  '##########################'
                 SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
                 ?MSGX :=  '## 指定された範囲ではア ##'
                 SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
                 ?MSGX :=  '## ンマッチ処理対象のデ ##'
                 SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
                 ?MSGX :=  '## ータはありません！！ ##'
                 SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
                 ?MSGX :=  '## 再度、確認して下さい ##'
                 SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
                 ?MSGX :=  '##########################'
                 SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
                 RETURN    PGMEC-?PGMECA
             END
         END
    END

/.## アンマッチデータ作成 ##./
SSK0032B:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-KEISYKL2,TOFILE-KEISYKL2.TOKKLIB
    OVRF FILE-KEIJYWL3,TOFILE-KEIJYWL3.TOKKLIB
    OVRF FILE-KEIUMCF,TOFILE-KEIUMCF.TOKKLIB
    CALL SSK0032B.TOKELIBO,PARA-(?BUMON,?TANCD)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF   ?PGMEC    ^=   0    THEN
           ?KEKA4 := 'アンマッチデータ作成'
         GOTO ABEND
    END

/.## 出力区分により処理を遷移させる ##./
HANTEI:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    CASE  ?SKBN   OF
          # '1' #  /.##リストへ出力##./
          GOTO   SSK0033L
          # '2' #  /.##ＣＳＶへ出力##./
          GOTO   SSK0034V
    ELSE           /.##判定不能　　##./
          ?KEKA4 := '出力区分判定不能'
          GOTO ABEND
    END

/.## アンマッチリスト出力 ##./
SSK0033L:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-KEIUMCL1,TOFILE-KEIUMCL1.TOKKLIB
    OVRF FILE-TENMS1,TOFILE-TENMS1.TOKFLIB
    CASE  ?PRTKBN   OF
          # '1' #  /.##ドットプリンタへ出力　##./
          OVRPRTF FILE-XU04LP,TOFILE-KAHMAPRT.XUCL
          # '2' #  /.##レーザープリンタへ出力##./
          OVRPRTF FILE-XU04LP,TOFILE-XU04LP.XUCL
    ELSE           /.##未指定の場合は、ドットへ出力##./
          OVRPRTF FILE-XU04LP,TOFILE-XU04LP.XUCL
    END
    CALL SSK0033L.TOKELIBO,PARA-(?SKBN,?DKBN,?DFROM,?DTO,
                                 ?KKBN,?TANF,?TANT,?DENK1,
                                 ?DENK2,?DENK3,?DENK4,?DENK5,
                                 ?TENF,?TENT,?DENNF,?DENNT,
                                 ?SKBF,?SKBT,?DENKF,?DENKT)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF   ?PGMEC    ^=   0    THEN
           ?KEKA4 := 'アンマッチリスト出力'
         GOTO ABEND
    END
    GOTO RTN  /.##終了へ##./

/.## アンマッチ　ＣＳＶデータ出力 ##./
SSK0034V:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-KEIUMCL1,TOFILE-KEIUMCL1.TOKKLIB
    OVRF FILE-TENMS1,TOFILE-TENMS1.TOKFLIB
    OVRF FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    OVRF FILE-KEICSVUM,TOFILE-KEICSVUM.TOKKLIB
    CALL SSK0034V.TOKELIBO,PARA-(?SKBN,?DKBN,?DFROM,?DTO,
                                 ?KKBN,?TANF,?TANT,?DENK1,
                                 ?DENK2,?DENK3,?DENK4,?DENK5,
                                 ?TENF,?TENT,?DENNF,?DENNT,
                                 ?SKBF,?SKBT,?DENKF,?DENKT)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF   ?PGMEC    ^=   0    THEN
           ?KEKA4 := 'アンマッチ　ＣＳＶデータ出力'
         GOTO ABEND
    END

/.## ＣＳＶデータ転送 ##./
PFIMPORT:

    ?STEP :=  %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    FIMPORT FILE-KEICSVUM.TOKKLIB,PARA-PARAKEI,UNIT-2
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF   ?PGMEC    ^=   0    THEN
           ?KEKA4 := 'アンマッチ　ＣＳＶデータ出力'
         GOTO ABEND
    END

/.### ﾌﾟﾛｸﾞﾗﾑ終了 ###./
RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    RETURN    PGMEC-?PGMEC

ABEND:  /. ﾌﾟﾛｸﾞﾗﾑ異常終了時処理 ./
    DEFLIBL TOKELIB/TOKFLIB/TOKELIBO/TOKKLIB
    OVRDSPF FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
              /.１２３４５６７８９０１２３４５６７８９０./
    ?KEKA1 :=  '受領アンマッチリスト発行にて異常発生！！'
    ?KEKA2 :=  'ログリスト等を採取しＮＡＶへ連絡して下'
    ?KEKA3 :=  'さい。'
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO ?MSGX :=   ?MSG(?I)
           SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    END

    RETURN    PGMEC-?PGMEC

```
