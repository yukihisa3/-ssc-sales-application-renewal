# PBM02410

**種別**: JCL  
**ライブラリ**: TOKCLIBS  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIBS/PBM02410.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　特販システム（本社システム）          *  ./
/. *   SYSTEM-NAME :    流通ＢＭＳ                           *  ./
/. *   JOB-ID      :    PBM02410                             *  ./
/. *   JOB-NAME    :    請求データ削除（サンデー・ジョイ）   *  ./
/. ***********************************************************  ./
    PGM
    VAR       ?TORICD   ,STRING*8,VALUE-'        ' /.取引先コード./
    VAR       ?PGMEC    ,INTEGER
    VAR       ?PGMECX   ,STRING*11
    VAR       ?PGMEM    ,STRING*99
    VAR       ?MSG      ,STRING*99(6)
    VAR       ?MSGX     ,STRING*99
    VAR       ?PGMID    ,STRING*8,VALUE-'PBM02410'
    VAR       ?STEP     ,STRING*8
    VAR       ?OPR1     ,STRING*50                  /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?OPR2     ,STRING*50                  /.      2    ./
    VAR       ?OPR3     ,STRING*50                  /.      3    ./
    VAR       ?OPR4     ,STRING*50                  /.      4    ./
    VAR       ?OPR5     ,STRING*50                  /.      5    ./
    VAR       ?SYORINM1 ,STRING*40
    VAR       ?SYORINM2 ,STRING*50
    VAR       ?SYORINM3 ,STRING*50
    VAR       ?SYORIKN1 ,STRING*7,VALUE-'0000000'
    VAR       ?SYORIKN2 ,STRING*7,VALUE-'0000000'
    VAR       ?SYORIKN3 ,STRING*7,VALUE-'0000000'
    VAR       ?SYORIKN4 ,STRING*7,VALUE-'0000000'
    VAR       ?SYORIKN5 ,STRING*7,VALUE-'0000000'
    VAR       ?SIMEBI   ,STRING*8,VALUE-'00000000'    /.締日./
    VAR       ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?KEKA1    ,STRING*40                    /.      2    ./
    VAR       ?KEKA2    ,STRING*40                    /.      3    ./
    VAR       ?KEKA3    ,STRING*40                    /.      4    ./
    VAR       ?KEKA4    ,STRING*40                    /.      5    ./

    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?SYORINM1 :=  '流通ＢＭＳサンデージョイ請求データ消込'
    ?PGNM     :=  '流通ＢＭＳサンデージョイ請求データ消込'

    DEFLIBL TOKELIB/TOKELIBO/TOKFLIB/TOKKLIB/BMSFLIB

/.##支払ワークファイルロック##./
ASSIGN:

    ?STEP :=   'ASSIGN'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    ASSIGN    FILE-BMSSIHW.BMSFLIB!@XCL
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    IF        ?PGMEC    ^=   0    THEN
              SNDMSG '他で処理が行われています',TOWS-@ORGWS
              ?KEKA4 :=  'サンデージョイ支払ワークファイルロック'
              GOTO ABEND
    END

/.##請求データ削除指示##./
SBM0250I:

    ?STEP :=   'SBM0250I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIBO
    OVRF      FILE-TOKMS2,TOFILE-TOKMS2.TOKFLIB
    CALL      PGM-SBM0251I.TOKELIBO,PARA-(?TORICD,?SIMEBI)
    IF        @PGMEC    ^=   0    THEN
         IF   @PGMEC     =   4010 THEN
              SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
              RETURN
         ELSE
              ?KEKA4 :=  '請求データ削除指示'
              GOTO ABEND
         END
    END

    ?MSGX :=  '消込取引先＝' && ?TORICD && '　締日　＝' && ?SIMEBI
    SNDMSG    ?MSGX,TO-XCTL

/.##流通ＢＭＳ支払ワーク初期化##./
PCLRFILE:

    ?STEP :=   'PCLRFILE'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CLRFILE FILE-BMSSIHW.BMSFLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '流通ＢＭＳ支払ワーク初期化'
              GOTO ABEND
    END

/.##処理判定　パラ：取引先＝30402の場合は連続処理へ##./
/.##                上記以外は、個別取引先処理へ 　##./
PHANTEI:

    ?STEP :=   'PHANTEI '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    IF  ?TORICD  =  '00030402'  THEN
        ?MSGX :=  '＃連続処理開始！！＃'
        SNDMSG    ?MSGX,TO-XCTL
        GOTO      T30402
    END

/.##支払ワーク作成（個別取引先ＣＤの場合）##./
SBM0260B:

    ?STEP :=   'SBM0260B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-BMSSIHL2,TOFILE-BMSSIHL2.BMSFLIB
    OVRF      FILE-BMSSIHW,TOFILE-BMSSIHW.BMSFLIB
    CALL      PGM-SBM0260B.TOKELIBO,PARA-(?TORICD,?SIMEBI)
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '支払ワーク作成'
              GOTO ABEND
    ELSE
              ?MSGX :=  '＃請求合計Ｆ消込処理　開始＃'
              SNDMSG    ?MSGX,TO-XCTL
              GOTO SBM0240B
    END

/.##支払ワーク作成（取引先＝３０４０２）##./
T30402:

    ?STEP :=   'T30402  '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-BMSSIHL2,TOFILE-BMSSIHL2.BMSFLIB
    OVRF      FILE-BMSSIHW,TOFILE-BMSSIHW.BMSFLIB
    CALL      PGM-SBM0260B.TOKELIBO,PARA-('00030402',?SIMEBI)
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '支払ワーク作成（３０４０２）'
              GOTO ABEND
    END

/.##支払ワーク作成（取引先＝３０４０２１）##./
T304021:

    ?STEP :=   'T304021 '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-BMSSIHL2,TOFILE-BMSSIHL2.BMSFLIB
    OVRF      FILE-BMSSIHW,TOFILE-BMSSIHW.BMSFLIB
    CALL      PGM-SBM0260B.TOKELIBO,PARA-('00304021',?SIMEBI)
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '支払ワーク作成（３０４０２１）'
              GOTO ABEND
    END

/.##請求合計Ｆ消し込み##./
SBM0240B:

    ?STEP :=   'SBM0240B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-SETGKFP3,TOFILE-SETGKFS4.TOKKLIB
    OVRF      FILE-BMSSIHW1,TOFILE-BMSSIHW1.BMSFLIB
    CALL      PGM-SBM0240B.TOKELIBO,PARA-(?SYORIKN1)
    IF        @PGMEC    ^=   0    THEN
              GOTO ABEND
    END

/.##請求合計Ｆ圧縮処理##./
CNDPF:

    ?STEP :=   'CNDPF   '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    DACTLF FILE-SETGKFS1.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＤＡＣＴＬＦ１'
              GOTO ABEND END
    DACTLF FILE-SETGKFS2.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＤＡＣＴＬＦ２'
              GOTO ABEND END
    DACTLF FILE-SETGKFS3.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＤＡＣＴＬＦ３'
              GOTO ABEND END
    DACTLF FILE-SETGKFS4.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＤＡＣＴＬＦ４'
              GOTO ABEND END
    CNDPF  FILE-SETGKFS.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＣＮＤＰＦ'
              GOTO ABEND END
    ACTLF  FILE-SETGKFS1.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＡＣＴＬＦ１'
              GOTO ABEND END
    ACTLF  FILE-SETGKFS2.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＡＣＴＬＦ２'
              GOTO ABEND END
    ACTLF  FILE-SETGKFS3.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＡＣＴＬＦ３'
              GOTO ABEND END
    ACTLF  FILE-SETGKFS4.TOKKLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ＡＣＴＬＦ４'
              GOTO ABEND END

RTN:

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  'サンデー・ジョイ請求データ消込処理が'
    ?KEKA2 :=  '正常終了しました。'
    ?KEKA3 :=  '消込結果を確認して下さい。'
    ?KEKA4 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    DEFLIBL TOKELIBO/TOKELIB/TOKFLIB
    OVRPRTF FILE-PRTF,TOFILE-PRTF.TOKELIB,MEDLIB-TOKELIBO
    ?SYORINM2 := '流通ＢＭＳ請求ＤＴ消込処理が正常終了しました。'
    ?SYORINM3 := '件数を確認して下さい。（サンデー・ジョイ）　　　'
    CALL SSN0050L.TOKELIBO,
          PARA-(?SYORINM1,?SYORINM2,?SYORINM3,?SYORIKN1,?SYORIKN2,
                ?SYORIKN3,?SYORIKN4,?SYORIKN5)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL
    RETURN    PGMEC-@PGMEC

ABEND:

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  'サンデー・ジョイ請求データ消込処理が'
    ?KEKA2 :=  '異常終了しました。ＮＡＶへ連絡して下さい。'
    ?KEKA3 :=  '↓下記が、エラー発生箇所です。'
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    DEFLIBL TOKELIBO/TOKELIB/TOKFLIB
    OVRPRTF FILE-PRTF,TOFILE-PRTF.TOKELIB,MEDLIB-TOKELIBO
    ?SYORINM2 := '流通ＢＭＳ請求ＤＴ消込処理が異常終了しました。　'
    ?SYORINM3 := 'ログを採取して、ＮＡＶへ連絡して下さい。　　　　'
    CALL SSN0050L.TOKELIBO,
          PARA-(?SYORINM1,?SYORINM2,?SYORINM3,?SYORIKN1,?SYORIKN2,
                ?SYORIKN3,?SYORIKN4,?SYORIKN5)
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
