# PSY37850

**種別**: JCL  
**ライブラリ**: TOKCLIBS  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIBS/PSY37850.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　特販システム（本社システム）          *  ./
/. *   SYSTEM-NAME :    新受配信サブシステム      　　　     *  ./
/. *   JOB-ID      :    PSY3767B                             *  ./
/. *   JOB-NAME    :    受領書発行                           *  ./
/. *               :    受領書ＣＳＶ出力                     *  ./
/. ***********************************************************  ./
/.### ﾜｰｸｴﾘｱ定義 ###./
    PGM
    VAR ?PGMEC    ,INTEGER                      /.ｴﾗｰｽﾃｲﾀｽ./
    VAR ?PGMECX   ,STRING*11                    /.ｴﾗｰｽﾃｲﾀｽ変換./
    VAR ?PGMEM    ,STRING*99                    /.ｴﾗｰﾒｯｾｰｼﾞ./
    VAR ?MSG      ,STRING*99(6)                 /.ﾒｯｾｰｼﾞ定義./
    VAR ?MSGX     ,STRING*99                    /.ﾒｯｾｰｼﾞ定義変換./
    VAR ?PGMID    ,STRING*8,VALUE-'PSY37850'    /.ﾌﾟﾛｸﾞﾗﾑID./
    VAR ?STEP     ,STRING*8                     /.ﾌﾟﾛｸﾞﾗﾑｽﾃｯﾌﾟ./
    VAR ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1./
    VAR ?KEKA1    ,STRING*40                    /.      2./
    VAR ?KEKA2    ,STRING*40                    /.      3./
    VAR ?KEKA3    ,STRING*40                    /.      4./
    VAR ?KEKA4    ,STRING*40                    /.      5./
    VAR ?BUMON    ,STRING*4,VALUE-'    '        /.部門名./
    VAR ?TANCD    ,STRING*2,VALUE-'  '          /.担当者CD./
    VAR ?WS       ,STRING*8,VALUE-'        '    /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR ?WKSTN    ,NAME!MOD                     /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR ?SNTK1    ,STRING*1,VALUE-' '        /. 区分１ ./
    VAR ?SNTK2    ,STRING*1,VALUE-' '        /. 区分２ ./
    VAR ?STYMD    ,STRING*8,VALUE-'        ' /. 日付開始 ./
    VAR ?EDYMD    ,STRING*8,VALUE-'        ' /. 日付終了 ./

/.### ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ ###./
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

/.## ﾗｲﾌﾞﾗﾘﾘｽﾄ登録 ##./
    DEFLIBL TOKELIB/TOKFLIB/TOKKLIB/TOKWLIB/TOKELIBO

/.## ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ ##./
    ?PGNM :=  '受領書発行'

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得 ##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

/.## ログインユーザー情報取得 ##./
SIT9000B:

    ?STEP :=   'SIT9000B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-LOGINUSR,TOFILE-LOGINUSR.@TEMP
    CALL      PGM-SIT9000B.TOKELIBO,PARA-(?BUMON,?TANCD)
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  'ログイン情報取得'
              GOTO ABEND
    END

/.## 受領書発行指示 ##./
SSY3785I:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    CALL PGM-SSY3785I.TOKELIBO,PARA-(?SNTK1,?SNTK2,?STYMD,?EDYMD)
    IF  @PGMEC ^= 0  THEN
        IF @PGMEC = 4010  THEN
           SNDMSG MSG-'##取消終了##',TO-XCTL.@ORGPROF,JLOG-@YES
           GOTO  RTN
        ELSE
           ?KEKA4 := '受領書発行指示'
           GOTO  ABEND
        END
    END

/.## 処理振り分け ##./
FURIWAKE:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    ?MSGX :=  '# 区分１ = ' && ?SNTK1 && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 区分２ = ' && ?SNTK2 && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 日付Ｓ = ' && ?STYMD && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    ?MSGX :=  '# 日付Ｅ = ' && ?EDYMD && ' #'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    IF  ?SNTK1 = '1'  THEN
        IF  ?SNTK2  = '1'  THEN
            GOTO  SSY3768B
        ELSE
            IF  ?SNTK2  =  '2'  THEN
                GOTO  SSY3768A
            ELSE
                ?KEKA4 := '発行指定区分エラー（受領書）'
                GOTO  ABEND
            END
        END
    END

    IF  ?SNTK1 = '2'  THEN
        IF  ?SNTK2  = '1'  THEN
            GOTO  SSY3769B
        ELSE
            IF  ?SNTK2  =  '2'  THEN
                GOTO  SSY3769A
            ELSE
                ?KEKA4 := '発行指定区分エラー（ＣＳＶ）'
                GOTO  ABEND
            END
        END
    END

/.## 受領書発行（計上日） ##./
SSY3768A:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-NFJYURL1,TOFILE-NFJYURL1.TOKKLIB
    OVRF FILE-NFSHOMS1,TOFILE-NFSHOMS1.TOKDLIB
    CALL PGM-SSY3768A.TOKELIBO,PARA-(?STYMD,?EDYMD)
    IF  @PGMEC ^= 0  THEN
        ?KEKA4 := '受領書発行（計上日）'
        GOTO  ABEND
    ELSE
        GOTO  RTN
    END

/.## 受領書発行（受信日） ##./
SSY3768B:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-NFJYURL4,TOFILE-NFJYURL4.TOKKLIB
    OVRF FILE-NFSHOMS1,TOFILE-NFSHOMS1.TOKDLIB
    CALL PGM-SSY3768B.TOKELIBO,PARA-(?STYMD,?EDYMD)
    IF  @PGMEC ^= 0  THEN
        ?KEKA4 := '受領書発行（受信日）'
        GOTO  ABEND
    ELSE
        GOTO  RTN
    END

/.## 受領書ＣＳＶ（計上日） ##./
SSY3769A:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-NFJYURL1,TOFILE-NFJYURL1.TOKKLIB
    OVRF FILE-NFSHOMS1,TOFILE-NFSHOMS1.TOKDLIB
    CALL PGM-SSY3769A.TOKELIBO,PARA-(?STYMD,?EDYMD)
    IF  @PGMEC ^= 0  THEN
        ?KEKA4 := '受領書ＣＳＶ（計上日）'
        GOTO  ABEND
    END

/.## 受領データＣＳＶ出力 ##./
PFIMPOR1:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    FIMPORT FILE-NFJYRDT.TOKWLIB,PARA-JYURYO,UNIT-2
    IF  @PGMEC ^= 0  THEN
           ?KEKA4 := '受領データＣＳＶ出力'
           GOTO  ABEND
    ELSE
        GOTO  RTN
    END


/.## 受領書ＣＳＶ（受信日） ##./
SSY3769B:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    OVRF FILE-NFJYURL4,TOFILE-NFJYURL4.TOKKLIB
    OVRF FILE-NFSHOMS1,TOFILE-NFSHOMS1.TOKDLIB
    CALL PGM-SSY3769B.TOKELIBO,PARA-(?STYMD,?EDYMD)
    IF  @PGMEC ^= 0  THEN
        ?KEKA4 := '受領書ＣＳＶ（受信日）'
        GOTO  ABEND
    END

/.## 受領データＣＳＶ出力 ##./
PFIMPOR2:

    ?STEP :=   %LAST(LABEL)      /.##ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾒｯｾｰｼﾞ##./
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    FIMPORT FILE-NFJYRDT.TOKWLIB,PARA-JYURYO,UNIT-2
    IF  @PGMEC ^= 0  THEN
           ?KEKA4 := '受領データＣＳＶ出力'
           GOTO  ABEND
    ELSE
        GOTO  RTN
    END


/.### ﾌﾟﾛｸﾞﾗﾑ終了 ###./
RTN:

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES

    RETURN    PGMEC-@PGMEC

ABEND:  /. ﾌﾟﾛｸﾞﾗﾑ異常終了時処理 ./
              /.１２３４５６７８９０１２３４５６７８９０./
    ?KEKA1 :=  'ＴＲＡＮＴＲＡＮ連携データ作成異常終了。'
    ?KEKA2 :=  'ログリスト等を採取しＮＡＶへ連絡して下'
    ?KEKA3 :=  'さい。'
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO ?MSGX :=   ?MSG(?I)
           SNDMSG ?MSGX,TO-XCTL.@ORGPROF,SLOG-@YES,JLOG-@YES
    END

    RETURN    PGMEC-@PGMEC

```
