# PNA04300

**種別**: JCL  
**ライブラリ**: TOKCLIBS  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIBS/PNA04300.CL`

## ソースコード

```jcl
/. ************************************************************  ./
/. *   _サカタのタネ　ホームガーデン部　　　　　　　　        *  ./
/. *   SYSTEM-NAME :    苗業務システム　ＨＧ部連携　　　　    *  ./
/. *   JOB-ID      :    PNA04300                              *  ./
/. *   JOB-NAME    :    サカタ商品ＣＤ再取得処理　　　　　　　*  ./
/. ************************************************************  ./
    PGM
    VAR       ?PGMEC  ,INTEGER
    VAR       ?PGMECX ,STRING*11
    VAR       ?PGMEM  ,STRING*99
    VAR       ?MSG    ,STRING*99(6)
    VAR       ?MSGX   ,STRING*99
    VAR       ?PGMID  ,STRING*8,VALUE-'PNA04300'
    VAR       ?STEP   ,STRING*8
    VAR       ?WKSTN  ,STRING*8
    VAR       ?TOKCD  ,STRING*8,VALUE-'        '  /.バッチ時間./
/.-----------------------------------------------------------./
    VAR       ?CLID   ,STRING*8                   /.ＣＬＩＤ   ./
    VAR       ?MSG1   ,STRING*80                  /.開始終了MSG./
    VAR       ?PGNM   ,STRING*40                  /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?KEKA1  ,STRING*40                  /.      2    ./
    VAR       ?KEKA2  ,STRING*40                  /.      3    ./
    VAR       ?KEKA3  ,STRING*40                  /.      4    ./
    VAR       ?KEKA4  ,STRING*40                  /.      5    ./
/.-----------------------------------------------------------./
    VAR       ?KBN    ,STRING*1,VALUE-' '
    VAR       ?JDATE  ,STRING*8,VALUE-'        '
    VAR       ?JTIME  ,STRING*4,VALUE-'    '
    VAR       ?JTORCD ,STRING*8,VALUE-'        '
    VAR       ?JSOKO  ,STRING*2,VALUE-'  '
    VAR       ?JAITE  ,STRING*13,VALUE-'             '
    VAR       ?TTORCD ,STRING*8,VALUE-'        '
    VAR       ?TSOKO  ,STRING*2,VALUE-'  '
    VAR       ?TAITE  ,STRING*13,VALUE-'             '
    VAR       ?TDENST ,STRING*9,VALUE-'         '
    VAR       ?TDENED ,STRING*9,VALUE-'         '

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL TOKELIB/TOKFLIB/TOKKLIB

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM :=  '苗業務：サカタ商品ＣＤ再取得処理'

/.##ﾌﾟﾛｸﾞﾗﾑ開始ﾒｯｾｰｼﾞ##./
STEP000:
    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.##範囲指定画面##./
STEP010:

    ?STEP :=   'STEP010 '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIBO
    OVRF FILE-SHOTBL1,TOFILE-SHOTBL1.TOKFLIB
    OVRF FILE-TOKMS2,TOFILE-TOKMS2.TOKFLIB
    OVRF FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    CALL SNA0430I.TOKELIBO,PARA-(?KBN,?JDATE,?JTIME,?JTORCD,?JSOKO,
         ?JAITE,?TTORCD,?TSOKO,?TAITE,?TDENST,?TDENED)
    IF   @PGMEC    ^=   0    THEN
         IF  @PGMEC  =  4010   THEN
             SNDMSG MSG-'＃取消終了＃',TO-XCTL
             RETURN
         ELSE
              ?KEKA4 :=  '【サカタ商品ＣＤ再取得指示】'
              GOTO ABEND
         END
    END

/.##判定##./
STEP020:

    ?STEP :=   'STEP020 '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    IF  ?KBN  =  '2'  THEN
        GOTO  STEP040
    END

/.##オンライン関係##./
STEP030:

    ?STEP :=   'STEP030 '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF FILE-SHTDENLA,TOFILE-SHTDENLA.TOKFLIB
    OVRF FILE-SHOTBL1,TOFILE-SHOTBL1.TOKFLIB
    OVRF FILE-MEIMS1,TOFILE-MEIMS1.TOKFLIB
    CALL SNA0440B.TOKELIBO,PARA-(?JDATE,?JTIME,?JTORCD,?JSOKO,?JAITE)
    IF   @PGMEC    ^=   0    THEN
         ?KEKA4 :=  '【オンライン分　再取得】'
         GOTO ABEND
    END
/.##終了へ##./
    GOTO RTN

/.##手書関係##./
STEP040:

    ?STEP :=   'STEP040 '
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF FILE-SHTDENLC,TOFILE-SHTDENLC.TOKFLIB
    OVRF FILE-SHOTBL1,TOFILE-SHOTBL1.TOKFLIB
    OVRF FILE-MEIMS1,TOFILE-MEIMS1.TOKFLIB
    CALL SNA0450B.TOKELIBO,PARA-(?TTORCD,?TSOKO,?TAITE,?TDENST,?TDENED)
    IF   @PGMEC    ^=   0    THEN
         ?KEKA4 :=  '【手書分　　　　再取得】'
         GOTO ABEND
    END


/.##プログラム正常終了（資源の開放）##./
RTN:

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  'サカタ商品ＣＤ再取得処理が正常終了しまし'
    ?KEKA2 :=  'た。更新内容を確認して下さい。'
    ?KEKA3 :=  ''
    ?KEKA4 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('1',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

/.##プログラム異常終了（資源の開放－＞ログリスト出力）##./
ABEND:

    OVRDSPF FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  'サカタ商品ＣＤ再取得処理が異常終了しまし'
    ?KEKA2 :=  'た。この画面でエラーリストを発行して下さ'
    ?KEKA3 :=  'い。また、ＮＡＶへ連絡して下さい。'
    ?KEKA4 :=  ''
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
