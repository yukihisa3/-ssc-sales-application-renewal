# PTN00100

**種別**: JCL  
**ライブラリ**: TOKCLIBS  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIBS/PTN00100.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　富岡配送センター専用                  *  ./
/. *   SYSTEM-NAME :    _番票出力                           *  ./
/. *   JOB-ID      :    PTN00100                              *  ./
/. *   JOB-NAME    :    _番票出力                           *  ./
/. ***********************************************************  ./
    PGM
    VAR       ?PGMEC    ,INTEGER
    VAR       ?PGMECX   ,STRING*11
    VAR       ?PGMEM    ,STRING*99
    VAR       ?MSG      ,STRING*99(6)
    VAR       ?MSGX     ,STRING*99
    VAR       ?PGMID    ,STRING*8,VALUE-'PTN00100'
    VAR       ?STEP     ,STRING*8
    VAR       ?PGNM     ,STRING*40                    /.ﾒｯｾｰｼﾞ1    ./
    VAR       ?KEKA1    ,STRING*40                    /.      2    ./
    VAR       ?KEKA2    ,STRING*40                    /.      3    ./
    VAR       ?KEKA3    ,STRING*40                    /.      4    ./
    VAR       ?KEKA4    ,STRING*40                    /.      5    ./
    VAR       ?WS       ,STRING*8,VALUE-'        '
    VAR       ?WKSTN    ,NAME!MOD
    VAR       ?SOKCD    ,STRING*2,VALUE-'  '
    VAR       ?DSOKCD   ,STRING*2,VALUE-'  '
    VAR       ?OSOKCD   ,STRING*2,VALUE-'  '
    VAR       ?TANAST   ,STRING*6,VALUE-'      '
    VAR       ?TANAED   ,STRING*6,VALUE-'      '
    VAR       ?DDATA    ,STRING*1,VALUE-' '

    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
     DEFLIBL   TOKFLIB/TOKELIB/TOKWLIB/TOKELIBO

/.##ﾌﾟﾛｸﾞﾗﾑ名称ｾｯﾄ##./
    ?PGNM :=  '富岡　_番票'

/.##富岡チェック##./
TANMCHK:

    IF     ?WS     ^=    'WKSTNH83'
        THEN
            SNDMSG '＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊',TO-XCTL
            SNDMSG '＊富岡配送センターのみ使用が許可＊',TO-XCTL
            SNDMSG '＊されております。他の配送センタ＊',TO-XCTL
            SNDMSG '＊ーでは使用できません。　　　　＊',TO-XCTL
            SNDMSG '＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊',TO-XCTL
            RETURN
    END

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKCD,?DSOKCD)
    IF        @PGMEC ^= 0 THEN
              ?KEKA4 :=  '【代表倉庫取得】'
              GOTO ABEND
    END

/.##_番票出力指示##./
STN0010I:

    ?STEP :=   'STN0010I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIBO
    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    CALL      PGM-STN0010I.TOKELIBO,PARA-(?SOKCD,?DSOKCD,?OSOKCD,
              ?TANAST,?TANAED,?DDATA)
    IF        @PGMEC    ^=   0    THEN
              ?PGMEC := @PGMEC
              IF   ?PGMEC = 4010  THEN
                   ?MSGX := '##取消終了##'
                   SNDMSG MSG-?MSGX,TO-XCTL
                   RETURN PGMEC-@PGMEC
              END
                   ?KEKA4 :=  '【_番票出力指示】'
              GOTO ABEND
    END

    ?MSGX :=  '##開始_番 = ' && ?TANAST && '  ##'
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '##終了_番 = ' && ?TANAED && '  ##'
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '##ﾃﾞｰﾀ区分 = ' && ?DDATA  && '  ##'
    SNDMSG    ?MSGX,TO-XCTL
    IF        ?DDATA = '2'  THEN
               GOTO   STN0030L
    END

/.##_番票データ削除##./
PCLRFILE:

    ?STEP :=   'PCLRFILE'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    CLRFILE FILE-ZTANAF.TOKWLIB
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【_番票データ削除】'
              GOTO ABEND END

/.##_番票データ作成##./
STN0020B:

    ?STEP :=   'STN0020B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-SHOTBL5,TOFILE-SHOTBL5.TOKFLIB
    OVRF      FILE-MEIMS1,TOFILE-MEIMS1.TOKFLIB
    OVRF      FILE-ZTANAL1,TOFILE-ZTANAL1.TOKWLIB
    CALL      PGM-STN0020B.TOKELIBO,PARA-(?OSOKCD,?TANAST,?TANAED)
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【_番票データ作成】'
              GOTO ABEND END

/.##_番票発行##./
STN0030L:

    ?STEP :=   'STN0030L'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZTANAL1,TOFILE-ZTANAL1.TOKWLIB
    OVRPRTF   FILE-PRTF,TOFILE-PRTF.TOKELIB,MEDLIB-TOKELIBO
    CALL      PGM-STN0030L.TOKELIBO,PARA-(?OSOKCD,?TANAST,?TANAED)
    IF        @PGMEC    ^=   0    THEN
              ?KEKA4 :=  '【_番票出力】'
              GOTO ABEND END


RTN:

    ?MSGX :=  '【_番票出力　正常終了】'
    SNDMSG    ?MSGX,TO-XCTL
    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:

    OVRPRTF   FILE-DSPF,TOFILE-DSPF.TOKELIB,MEDLIB-TOKELIB
    ?KEKA1 :=  '_番票発行処理が異常終了しました。'
    ?KEKA2 :=  'エラー発生場所が、下記に表示されており'
    ?KEKA3 :=  'ます。'
    CALL SMG0030I.TOKELIB
                    ,PARA-('2',?PGNM,?KEKA1,?KEKA2,?KEKA3,?KEKA4)
    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=   '### ' && ?PGMID && ' ABEND' &&   '    ###'
    ?MSG(2)   :=   '###' && ' PGMEC = ' &&
                    %SBSTR(?PGMECX,8,4) &&         '      ###'
    ?MSG(3)   :=   '###' && ' STEP = '  && ?STEP
                                                   && '   ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
