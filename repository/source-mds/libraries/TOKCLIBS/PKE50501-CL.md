# PKE50501

**種別**: JCL  
**ライブラリ**: TOKCLIBS  
**ソースファイル**: `source/navs/cobol/programs/TOKCLIBS/PKE50501.CL`

## ソースコード

```jcl
/. ***********************************************************  ./
/. *     サカタのタネ　出荷検品システム（本社システム）      *  ./
/. *   SYSTEM-NAME :    出荷検品システム（ケーヨーのとき）　 *  ./
/. *   JOB-ID      :    PKE50501                             *  ./
/. *   JOB-NAME    :    送信データ件数カウント               *  ./
/. ***********************************************************  ./
    PGM
/.##ﾜｰｸ定義##./
    VAR   ?SOKO  ,STRING*2,VALUE-'  '      /.倉庫CD./
    VAR   ?DSOKO ,STRING*2,VALUE-'  '      /.倉庫CD./
    VAR   ?JOHO  ,STRING*6,VALUE-'      ' /.出荷情報./
    VAR   ?LABH  ,STRING*6,VALUE-'      ' /.封筒情報./
    VAR   ?LABM  ,STRING*6,VALUE-'      ' /.梱包情報./
    VAR   ?RERR  ,STRING*6,VALUE-'      ' /.ｴﾗｰ 情報./
    VAR   ?KUBUN ,STRING*1,VALUE-' '      /.区分情報./
    VAR   ?WS    ,STRING*8,VALUE-'        ' /.ﾜｰｸｽﾃｰｼｮﾝ文字./
    VAR   ?WKSTN ,NAME!MOD                  /.ﾜｰｸｽﾃｰｼｮﾝ名前./
    VAR   ?PGMEC ,INTEGER
    VAR   ?PGMECX,STRING*11
    VAR   ?PGMEM ,STRING*99
    VAR   ?MSG   ,STRING*99(6)
    VAR   ?MSGX  ,STRING*99
    VAR   ?PGMID ,STRING*8,VALUE-'PKE50501'
    VAR   ?STEP  ,STRING*8
    VAR   ?FILNM ,STRING*8,VALUE-'        ' /.ﾌｧｲﾙ名合併用./
    VAR   ?FILNMS,STRING*6,VALUE-'SYJOHO'   /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR   ?FILNMF,STRING*6,VALUE-'SYLABH'   /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR   ?FILNMK,STRING*6,VALUE-'SYLABM'   /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR   ?FILNME,STRING*6,VALUE-'RCVERR'   /.ﾌｧｲﾙ名ﾃﾞﾌｫﾙﾄ./
    VAR   ?LIBNM ,STRING*8,VALUE-'TOKWLIB ' /.ﾗｲﾌﾞﾗﾘ名ﾃﾞﾌｫﾙﾄ./
    VAR   ?FILID ,NAME                      /.ﾌｧｲﾙ名名前型./
    VAR   ?LIBID ,NAME                      /.ﾗｲﾌﾞﾗﾘ名名前型./
    VAR   ?RCVSYUF ,NAME!MOD                /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR   ?RCVSYUFN,STRING*17               /.ﾌｧｲﾙ名表示用./
    VAR   ?RCVFUTF ,NAME!MOD                /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR   ?RCVFUTFN,STRING*17               /.ﾌｧｲﾙ名表示用./
    VAR   ?RCVKONF ,NAME!MOD                /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR   ?RCVKONFN,STRING*17               /.ﾌｧｲﾙ名表示用./
    VAR   ?RCVERRF ,NAME!MOD                /.ﾌｧｲﾙ.ﾗｲﾌﾞﾗﾘ./
    VAR   ?RCVERRFN,STRING*17               /.ﾌｧｲﾙ名表示用./

    ?MSGX :=  '***   '  && ?PGMID  &&   ' START  ***'
    SNDMSG    ?MSGX,TO-XCTL

/.##ﾗｲﾌﾞﾗﾘﾘｽﾄ登録##./
    DEFLIBL TOKELIB/TOKFLIB/TOKELIBO

/.## ﾜｰｸｽﾃｰｼｮﾝ名取得##./
    ?WKSTN   :=  @ORGWS
    ?WS      :=  %STRING(?WKSTN)
    ?MSGX    :=  '## ﾜｰｸｽﾃｰｼｮﾝ名 = ' && ?WS
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

/.##倉庫ｺｰﾄﾞ取得##./
SKY1601B:

    ?STEP :=   'SKY1601B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-JYOKEN1,TOFILE-JYOKEN1.TOKFLIB
    CALL      PGM-SKY1601B.TOKELIB,PARA-(?WS,?SOKO,?DSOKO)
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END

/.##倉庫ｺｰﾄﾞ選択##./
SKY1701I:

    ?STEP :=   'SKY1701I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIB
    CALL      PGM-SKY1702I.TOKELIB,PARA-(?SOKO,?DSOKO)
    IF        @PGMEC    ^=   0    THEN
              ?PGMEC := @PGMEC
              IF    ?PGMEC  =  4010
                 THEN
                    ?MSGX := '##取消終了##'
                    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
                    RETURN PGMEC-@PGMEC
                 ELSE
                    GOTO   ABEND
              END
    END

/.##ﾌｧｲﾙ名称取得##./
FILNMCHG:

    ?MSGX := '## 実行倉庫ｺｰﾄﾞ = ' && ?SOKO && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES
    ?FILNM    :=    ?FILNMS && ?SOKO      /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?RCVSYUF  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?RCVSYUFN :=    %STRING(?RCVSYUF)
    ?MSGX     :=    '## 検品GF名 = ' && ?RCVSYUFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ?FILNM    :=    ?FILNMF && ?SOKO      /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?RCVFUTF  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?RCVFUTFN :=    %STRING(?RCVFUTF)
    ?MSGX     :=    '## 封筒F名  = ' && ?RCVFUTFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ?FILNM    :=    ?FILNMK && ?SOKO      /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?RCVKONF  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?RCVKONFN :=    %STRING(?RCVKONF)
    ?MSGX     :=    '## 梱包F名  = ' && ?RCVKONFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ?FILNM    :=    ?FILNME && ?SOKO      /.ﾃﾞﾌｫﾙﾄﾌｧｲﾙ名+倉庫CD./
    ?FILID    :=    %NAME(?FILNM)         /.ﾌｧｲﾙ名名前型変換   ./
    ?LIBID    :=    %NAME(?LIBNM)         /.ﾗｲﾌﾞﾗﾘ名名前型変換 ./
    ?RCVERRF  :=    %NCAT(?FILID,?LIBID)  /.ﾌｧｲﾙ名.ﾗｲﾌﾞﾗﾘ名    ./
    ?RCVERRFN :=    %STRING(?RCVERRF)
    ?MSGX     :=    '## ｴﾗｰF名   = ' && ?RCVERRFN && ' ##'
    SNDMSG MSG-?MSGX,TO-XCTL.@ORGPROF,JLOG-@YES

    ?MSGX     :=    '## PARA-KUBUN = ' && ?KUBUN
    SNDMSG MSG-?MSGX,TO-XCTL

/.##倉庫の場合、受信処理を行なう##./
PFEXPORT:

    ?STEP :=   'PFEXPORT'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    FEXPORT FILE-?RCVSYUF,MODE-@REP,PARA-SNDKENPN,
            UNIT-1
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END

    FEXPORT FILE-?RCVKONF,MODE-@REP,PARA-SNDKENPN,
            UNIT-2
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END

    FEXPORT FILE-?RCVFUTF,MODE-@REP,PARA-SNDKENPN,
            UNIT-3
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END

/.##出荷検品関連Ｆ件数カウント##./
SKE5040B:

    ?STEP :=   'SKE5040B'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-SYJOHOF,TOFILE-?RCVSYUF
    OVRF      FILE-SYLABHF,TOFILE-?RCVKONF
    OVRF      FILE-SYLABMF,TOFILE-?RCVFUTF
    OVRF      FILE-RCVERRF,TOFILE-?RCVERRF
    CALL      PGM-SKE5040B.TOKELIBO,PARA-(?JOHO,?LABH,?LABM,?RERR,
                                         ?KUBUN,?KUBUN)
    IF        @PGMEC    ^=   0   THEN
              GOTO ABEND
    END
    SNDMSG ?JOHO,TO-XCTL.@ORGPROF
    SNDMSG ?LABH,TO-XCTL.@ORGPROF
    SNDMSG ?LABM,TO-XCTL.@ORGPROF
    SNDMSG ?RERR,TO-XCTL.@ORGPROF

/.##倉庫別出荷検品送信件数照会##./
SKE0830I:

    ?STEP :=   'SKE5050I'
    ?MSGX :=  '***   '  && ?STEP   &&   '        ***'
    SNDMSG    ?MSGX,TO-XCTL

    OVRF      FILE-ZSOKMS1,TOFILE-ZSOKMS1.TOKFLIB
    OVRDSPF   FILE-DSPF,TOFILE-DSPF.XUCL,MEDLIB-TOKELIBO
    CALL      PGM-SKE5050I.TOKELIBO,PARA-(?SOKO,?JOHO,?LABH,?LABM,
                                         ?RERR,?KUBUN)
    IF        @PGMEC    ^=   0    THEN
              GOTO   ABEND
    END

RTN: /.##ﾌﾟﾛｸﾞﾗﾑ正常時、終了ﾒｯｾｰｼﾞ##./

    ?MSGX :=  '***   '  && ?PGMID  &&   ' END    ***'
    SNDMSG    ?MSGX,TO-XCTL

    RETURN    PGMEC-@PGMEC

ABEND:/.##ﾌﾟﾛｸﾞﾗﾑ異常終了時、終了ﾒｯｾｰｼﾞ##./

    ?PGMEC    :=    @PGMEC
    ?PGMEM    :=    @PGMEM
    ?PGMECX   :=    %STRING(?PGMEC)
    ?MSG(1)   :=    '### ' && ?PGMID && ' ABEND' && ' ###'
    ?MSG(2)   :=    '### ' && ' PGMEC = ' &&
                     %SBSTR(?PGMECX,8,4) && ' ###'
    ?MSG(3)   :=    '###' && ' LINE = '  && %LAST(LINE)      && ' ###'
    FOR ?I    :=     1 TO 3
        DO     ?MSGX :=   ?MSG(?I)
               SNDMSG    ?MSGX,TO-XCTL
    END

    RETURN    PGMEC-@PGMEC

```
